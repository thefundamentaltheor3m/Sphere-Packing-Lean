import Mathlib.Analysis.Complex.UpperHalfPlane.Manifold
import Mathlib.NumberTheory.ModularForms.CongruenceSubgroups
import Mathlib.NumberTheory.ModularForms.SlashActions

open UpperHalfPlane hiding I

open Real Complex ContinuousMap Matrix CongruenceSubgroup ModularGroup

open scoped Interval Real Topology Manifold ModularForm MatrixGroups

/--
Restrict a function `F : ‚Ñç ‚Üí ‚ÑÇ` to the positive imaginary axis, i.e. `t ‚Ü¶ F (I * t)`.
If $t \le 0$, then `F (I * t)` is not defined, and we return `0` in that case.
-/
noncomputable def ResToImagAxis (F : ‚Ñç ‚Üí ‚ÑÇ) : ‚Ñù ‚Üí ‚ÑÇ :=
  fun t => if ht : 0 < t then F ‚ü®(Complex.I * t), by simp [ht]‚ü© else 0

namespace Function

/-- Dot notation alias for `ResToImagAxis`. -/
noncomputable def resToImagAxis (F : ‚Ñç ‚Üí ‚ÑÇ) : ‚Ñù ‚Üí ‚ÑÇ :=
  ResToImagAxis F

@[simp] lemma resToImagAxis_eq_resToImagAxis (F : ‚Ñç ‚Üí ‚ÑÇ) :
    F.resToImagAxis = ResToImagAxis F := rfl

@[simp] lemma resToImagAxis_apply (F : ‚Ñç ‚Üí ‚ÑÇ) (t : ‚Ñù) :
    F.resToImagAxis t = ResToImagAxis F t := rfl

end Function

/--
Function $F : \mathbb{H} \to \mathbb{C}$ whose restriction to the imaginary axis is real-valued,
i.e. imaginary part is zero.
-/
noncomputable def ResToImagAxis.Real (F : ‚Ñç ‚Üí ‚ÑÇ) : Prop :=
  ‚àÄ t : ‚Ñù, 0 < t ‚Üí (F.resToImagAxis t).im = 0

/--
Function $F : \mathbb{H} \to \mathbb{C}$ is real and positive on the imaginary axis.
-/
noncomputable def ResToImagAxis.Pos (F : ‚Ñç ‚Üí ‚ÑÇ) : Prop :=
  ResToImagAxis.Real F ‚àß ‚àÄ t : ‚Ñù, 0 < t ‚Üí 0 < (F.resToImagAxis t).re

/--
Function $F : \mathbb{H} \to \mathbb{C}$ whose restriction to the imaginary axis is eventually
positive, i.e. there exists $t_0 > 0$ such that for all $t \ge t_0$, $F(it)$ is real and positive.
-/
noncomputable def ResToImagAxis.EventuallyPos (F : ‚Ñç ‚Üí ‚ÑÇ) : Prop :=
  ResToImagAxis.Real F ‚àß ‚àÉ t‚ÇÄ : ‚Ñù, 0 < t‚ÇÄ ‚àß ‚àÄ t : ‚Ñù, t‚ÇÄ ‚â§ t ‚Üí 0 < (F.resToImagAxis t).re

theorem ResToImagAxis.Differentiable (F : ‚Ñç ‚Üí ‚ÑÇ) (hF : MDifferentiable ùìò(‚ÑÇ) ùìò(‚ÑÇ) F) (t : ‚Ñù)
    (ht : 0 < t) : DifferentiableAt ‚Ñù F.resToImagAxis t := by
  rw [Function.resToImagAxis_eq_resToImagAxis]
  have := hF ‚ü®Complex.I * t, by norm_num [Complex.I_re, ht]‚ü©
  rw [mdifferentiableAt_iff] at this
  have h_diff :
      DifferentiableAt ‚Ñù (fun t : ‚Ñù => F (ofComplex (Complex.I * t))) t := by
    convert this.restrictScalars ‚Ñù |> DifferentiableAt.comp t <|
      DifferentiableAt.const_mul ofRealCLM.differentiableAt _ using 1
  apply h_diff.congr_of_eventuallyEq
  filter_upwards [lt_mem_nhds ht] with t ht
  simp_all only [coe_mk_subtype, ResToImagAxis, ‚ÜìreduceDIte]
  rw [ofComplex_apply_of_im_pos]

/--
Restriction and slash action under S: $(F |_k S) (it) = t^{-k} * F(it)$
-/
theorem ResToImagAxis.SlashActionS (F : ‚Ñç ‚Üí ‚ÑÇ) (k : ‚Ñ§) (t : ‚Ñù)
    (ht : 0 < t) : (F ‚à£[k] S).resToImagAxis t = Complex.I ^ k * t ^ (-k) * F.resToImagAxis (1 / t)
    := by
  sorry

/--
Realenss, positivity and essential positivity are closed under the addition and multiplication.
-/
theorem ResToImagAxis.Real.add {F G : ‚Ñç ‚Üí ‚ÑÇ} (hF : ResToImagAxis.Real F)
    (hG : ResToImagAxis.Real G) : ResToImagAxis.Real (F + G) := by
  intro t ht
  have hFreal := hF t ht
  have hGreal := hG t ht
  simp [Function.resToImagAxis, ResToImagAxis, ht] at hFreal hGreal
  simp [Function.resToImagAxis, ResToImagAxis, ht, add_im, hFreal, hGreal]

theorem ResToImagAxis.Real.mul {F G : ‚Ñç ‚Üí ‚ÑÇ} (hF : ResToImagAxis.Real F)
    (hG : ResToImagAxis.Real G) : ResToImagAxis.Real (F * G) := by
  intro t ht
  have hFreal := hF t ht
  have hGreal := hG t ht
  simp [Function.resToImagAxis, ResToImagAxis, ht] at hFreal hGreal
  simp [Function.resToImagAxis, ResToImagAxis, ht, mul_im, hFreal, hGreal]

theorem ResToImagAxis.Real.hmul {F : ‚Ñç ‚Üí ‚ÑÇ} {c : ‚Ñù} (hF : ResToImagAxis.Real F) :
    ResToImagAxis.Real (c ‚Ä¢ F) := by
  intro t ht
  have hFreal := hF t ht
  simp [Function.resToImagAxis, ResToImagAxis, ht] at hFreal
  simp [Function.resToImagAxis, ResToImagAxis, ht, hFreal]

theorem ResToImagAxis.Pos.add {F G : ‚Ñç ‚Üí ‚ÑÇ} (hF : ResToImagAxis.Pos F)
    (hG : ResToImagAxis.Pos G) : ResToImagAxis.Pos (F + G) := by
  rw [Pos]
  refine ‚ü®Real.add hF.1 hG.1, ?_‚ü©
  intro t ht
  have hFpos := hF.2 t ht
  have hGpos := hG.2 t ht
  simp [Function.resToImagAxis, ResToImagAxis, ht] at hFpos hGpos
  simp [Function.resToImagAxis, ResToImagAxis, ht, add_re, add_pos hFpos hGpos]

theorem ResToImagAxis.Pos.mul {F G : ‚Ñç ‚Üí ‚ÑÇ} (hF : ResToImagAxis.Pos F)
    (hG : ResToImagAxis.Pos G) : ResToImagAxis.Pos (F * G) := by
  rw [Pos]
  refine ‚ü®Real.mul hF.1 hG.1, ?_‚ü©
  intro t ht
  have hFreal := hF.1 t ht
  have hGreal := hG.1 t ht
  have hFpos := hF.2 t ht
  have hGpos := hG.2 t ht
  simp [Function.resToImagAxis, ResToImagAxis, ht] at hFreal hGreal
  simp [Function.resToImagAxis, ResToImagAxis, ht] at hFpos hGpos
  simp [Function.resToImagAxis, ResToImagAxis, ht, mul_re, hFreal, hGreal, mul_pos hFpos hGpos]

theorem ResToImagAxis.Pos.hmul {F : ‚Ñç ‚Üí ‚ÑÇ} {c : ‚Ñù} (hF : ResToImagAxis.Pos F)
    (hc : 0 < c) : ResToImagAxis.Pos (fun z => c * F z) := by
  rw [Pos]
  refine ‚ü®Real.hmul hF.1, ?_‚ü©
  intro t ht
  have hFreal := hF.1 t ht
  have hFpos := hF.2 t ht
  simp [Function.resToImagAxis, ResToImagAxis, ht] at hFreal
  simp [Function.resToImagAxis, ResToImagAxis, ht] at hFpos
  simp [Function.resToImagAxis, ResToImagAxis, ht, mul_re, hFreal, mul_pos hc hFpos]

theorem ResToImagAxis.EventuallyPos.add {F G : ‚Ñç ‚Üí ‚ÑÇ}
    (hF : ResToImagAxis.EventuallyPos F) (hG : ResToImagAxis.EventuallyPos G) :
    ResToImagAxis.EventuallyPos (F + G) := by
  rw [EventuallyPos]
  refine ‚ü®ResToImagAxis.Real.add hF.1 hG.1, ?_‚ü©
  obtain ‚ü®tF, hF0, hFpos‚ü© := hF.2
  obtain ‚ü®tG, hG0, hGpos‚ü© := hG.2
  let t‚ÇÄ := max tF tG
  use t‚ÇÄ
  refine ‚ü®by positivity, ?_‚ü©
  intro t ht
  have htF‚ÇÄ : tF ‚â§ t‚ÇÄ := by grind
  have htG‚ÇÄ : tG ‚â§ t‚ÇÄ := by grind
  have htF : tF ‚â§ t := htF‚ÇÄ.trans ht
  have htG : tG ‚â§ t := htG‚ÇÄ.trans ht
  have hFpos_t := hFpos t htF
  have hGpos_t := hGpos t htG
  have htpos : 0 < t := by grind
  simp [Function.resToImagAxis, ResToImagAxis, htpos] at hFpos_t hGpos_t
  simp [ResToImagAxis, htpos]
  exact add_pos hFpos_t hGpos_t

theorem ResToImagAxis.EventuallyPos.mul {F G : ‚Ñç ‚Üí ‚ÑÇ}
    (hF : ResToImagAxis.EventuallyPos F) (hG : ResToImagAxis.EventuallyPos G) :
    ResToImagAxis.EventuallyPos (F * G) := by
  rw [EventuallyPos]
  refine ‚ü®ResToImagAxis.Real.mul hF.1 hG.1, ?_‚ü©
  obtain ‚ü®tF, hF0, hFpos‚ü© := hF.2
  obtain ‚ü®tG, hG0, hGpos‚ü© := hG.2
  let t‚ÇÄ := max tF tG
  use t‚ÇÄ
  refine ‚ü®by positivity, ?_‚ü©
  intro t ht
  have htpos : 0 < t := by grind
  have hFreal_t := hF.1 t htpos
  have hGreal_t := hG.1 t htpos
  have htF‚ÇÄ : tF ‚â§ t‚ÇÄ := by grind
  have htG‚ÇÄ : tG ‚â§ t‚ÇÄ := by grind
  have htF : tF ‚â§ t := htF‚ÇÄ.trans ht
  have htG : tG ‚â§ t := htG‚ÇÄ.trans ht
  have hFpos_t := hFpos t htF
  have hGpos_t := hGpos t htG
  have htpos : 0 < t := by grind
  simp [Function.resToImagAxis, ResToImagAxis, htpos] at hFpos_t hGpos_t
  simp [Function.resToImagAxis, ResToImagAxis, htpos] at hFreal_t hGreal_t
  simp [ResToImagAxis, htpos, hFreal_t, hGreal_t]
  exact mul_pos hFpos_t hGpos_t

theorem ResToImagAxis.EventuallyPos.hmul {F : ‚Ñç ‚Üí ‚ÑÇ} {c : ‚Ñù}
    (hF : ResToImagAxis.EventuallyPos F) (hc : 0 < c) :
    ResToImagAxis.EventuallyPos (fun z => c * F z) := by
  rw [EventuallyPos]
  refine ‚ü®ResToImagAxis.Real.hmul hF.1, ?_‚ü©
  obtain ‚ü®t‚ÇÄ, hF0, hFpos‚ü© := hF.2
  use t‚ÇÄ
  refine ‚ü®hF0, ?_‚ü©
  intro t ht
  have htpos : 0 < t := by grind
  have hFreal_t := hF.1 t htpos
  have hFpos_t := hFpos t ht
  simp [Function.resToImagAxis, ResToImagAxis, htpos] at hFreal_t
  simp [Function.resToImagAxis, ResToImagAxis, htpos] at hFpos_t
  simp [ResToImagAxis, htpos, hFreal_t]
  exact mul_pos hc hFpos_t
