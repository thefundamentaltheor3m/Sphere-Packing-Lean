import SpherePacking.ForMathlib.AtImInfty
import SpherePacking.ModularForms.JacobiTheta
import SpherePacking.ModularForms.DimensionFormulas
import SpherePacking.ModularForms.IsCuspForm

/-!
# Limits at infinity

In this file we prove the limit of Î˜áµ¢(z) as z tends to iâˆ. This will be useful as we do
computations with Fourier coefficients, e.g. comparing two q-expansions. This is also useful when we
compute limits of forms later on (following Seewoo's approach).
-/

open scoped Real MatrixGroups ModularForm
open UpperHalfPlane hiding I
open Complex Asymptotics Topology Filter Manifold SlashInvariantForm Matrix ModularGroup
  SlashAction

lemma Int.ne_half (a : â„¤) : â†‘a â‰  (1 / 2 : â„) :=
  ne_of_apply_ne Int.fract <| by
    rw [fract_intCast, fract_eq_self.mpr âŸ¨by linarith, by linarithâŸ©]
    norm_num

theorem jacobiThetaâ‚‚_half_mul_apply_tendsto_atImInfty :
    Tendsto (fun x : â„ â†¦ jacobiThetaâ‚‚ (x / 2) x) atImInfty (ğ“ 2) := by
  simp_rw [jacobiThetaâ‚‚, jacobiThetaâ‚‚_term]
  convert tendsto_tsum_of_dominated_convergence
    (f := fun z (n : â„¤) â†¦ cexp (2 * Ï€ * I * n * (z / 2) + Ï€ * I * n ^ 2 * z))
    (ğ“• := atImInfty)
    (g := Set.indicator {-1, 0} 1)
    (bound := fun n : â„¤ â†¦ rexp (Ï€ / 4) * rexp (-Ï€ * ((n : â„) + 1 / 2) ^ 2)) ?_ ?_ ?_
  Â· simp [â† tsum_subtype]
  Â· -- TODO: merge this with proof of isBoundedAtImInfty_Hâ‚‚
    apply summable_ofReal.mp
    have (n : â„¤) := jacobiThetaâ‚‚_rel_aux n 1
    simp_rw [mul_one] at this
    simp_rw [ofReal_mul, this, â† smul_eq_mul]
    apply Summable.const_smul
    apply Summable.const_smul
    rw [summable_jacobiThetaâ‚‚_term_iff]
    simp
  Â· intro n
    have : n = -1 âˆ¨ n = 0 âˆ¨ n âˆ‰ ({-1, 0} : Set â„¤) := by
      rw [Set.mem_insert_iff, Set.mem_singleton_iff]
      tauto
    rcases this with (rfl | rfl | hn) <;> ring_nf
    Â· simp
    Â· simp
    Â· simp only [hn, not_false_eq_true, Set.indicator_of_notMem]
      apply tendsto_zero_iff_norm_tendsto_zero.mpr
      have hâ‚ (n : â„¤) (z : â„‚) : (Ï€ * I * n * z + Ï€ * I * n ^ 2 * z) = Ï€ * (n + n ^ 2) * z * I := by
        ring_nf
      have h_base' : rexp (-Ï€) ^ ((n : â„) + n ^ 2) < 1 := by
        apply Real.rpow_lt_one
        Â· positivity
        Â· apply Real.exp_lt_one_iff.mpr (by simp; positivity)
        convert_to 0 < ((n * (n + 1) : â„¤) : â„)
        Â· push_cast
          ring_nf
        Â· apply Int.cast_pos.mpr
          by_cases hn' : 0 < n
          Â· apply mul_pos hn' (by omega)
          Â· rw [Set.mem_insert_iff, Set.mem_singleton_iff] at hn
            exact mul_pos_of_neg_of_neg (by omega) (by omega)
      simp_rw [hâ‚, norm_exp_mul_I, mul_assoc, im_ofReal_mul, â† Int.cast_pow, â† Int.cast_add,
        â† ofReal_intCast, im_ofReal_mul, â† mul_assoc, Int.cast_add, Int.cast_pow, â† neg_mul,
        Real.exp_mul, coe_im]
      refine (tendsto_rpow_atTop_of_base_lt_one _ ?_ h_base').comp tendsto_im_atImInfty
      exact neg_one_lt_zero.trans (by positivity)
  Â· rw [eventually_atImInfty]
    use 1
    intro z hz k
    simp_rw [â† Real.exp_add]
    ring_nf
    trans â€–cexp (((Ï€ * k + Ï€ * k ^ 2 : â„) * z) * I)â€–
    Â· apply le_of_eq
      simpa [add_mul] using by ring_nf
    Â· rw [norm_exp_mul_I, im_ofReal_mul]
      have (n : â„¤) : 0 â‰¤ (n : â„) ^ 2 + n := by
        nth_rw 2 [â† mul_one n]
        rw [sq, Int.cast_mul, Int.cast_one, â† mul_add]
        rcases lt_trichotomy (-1) n with (hn | rfl | hn)
        Â· apply mul_nonneg <;> norm_cast; omega
        Â· norm_num
        Â· apply mul_nonneg_of_nonpos_of_nonpos <;> norm_cast <;> omega
      simpa using le_mul_of_one_le_right
        (by rw [â† mul_add, add_comm]; exact mul_nonneg Real.pi_nonneg (this k)) hz

theorem jacobiThetaâ‚‚_zero_apply_tendsto_atImInfty :
    Tendsto (fun x : â„ â†¦ jacobiThetaâ‚‚ 0 x) atImInfty (ğ“ 1) := by
  simp_rw [jacobiThetaâ‚‚, jacobiThetaâ‚‚_term, mul_zero, zero_add]
  convert tendsto_tsum_of_dominated_convergence
    (f := fun (z : â„) (n : â„¤) â†¦ cexp (Ï€ * I * n ^ 2 * z))
    (ğ“• := atImInfty)
    (g := fun k â†¦ if k = 0 then 1 else 0)
    (bound := fun n : â„¤ â†¦ rexp (-Ï€ * n ^ 2)) ?_ ?_ ?_
  Â· simp
  Â· apply summable_ofReal.mp
    have := (summable_jacobiThetaâ‚‚_term_iff 0 I).mpr (by simp)
    rw [â† summable_norm_iff, â† summable_ofReal] at this
    simp_rw [jacobiThetaâ‚‚_term, mul_zero, zero_add, mul_right_comm _ I, mul_assoc, â† sq, I_sq,
      mul_neg_one, norm_exp, re_ofReal_mul, neg_re, mul_neg, â† neg_mul, â† ofReal_intCast,
      â† ofReal_pow, ofReal_re] at this
    exact this
  Â· intro k
    simp only
    split_ifs with hk
    Â· subst hk
      simp
    Â· rw [tendsto_zero_iff_norm_tendsto_zero]
      simp_rw [mul_right_comm _ I, norm_exp_mul_I, mul_assoc, im_ofReal_mul, â† ofReal_intCast,
        â† ofReal_pow, im_ofReal_mul, â† mul_assoc]
      simpa using tendsto_im_atImInfty.const_mul_atTop (by positivity)
  Â· rw [eventually_atImInfty]
    use 1, fun z hz k â†¦ ?_
    simp only
    simp_rw [mul_right_comm _ I, norm_exp_mul_I]
    simpa [â† ofReal_intCast, â† ofReal_pow] using le_mul_of_one_le_right (by positivity) hz

theorem jacobiThetaâ‚‚_half_apply_tendsto_atImInfty :
    Tendsto (fun x : â„ â†¦ jacobiThetaâ‚‚ (1 / 2 : â„‚) x) atImInfty (ğ“ 1) := by
  simp_rw [jacobiThetaâ‚‚, jacobiThetaâ‚‚_term, mul_right_comm _ _ (1 / 2 : â„‚), â† mul_div_assoc,
    mul_one, div_self (Gâ‚€ := â„‚) two_ne_zero, one_mul, exp_add, mul_comm (Ï€ * I), exp_int_mul,
    exp_pi_mul_I, mul_comm, mul_comm I]
  -- I tried converting this to the formula for jacobiThetaâ‚‚ 0 x above, but couldn't
  convert tendsto_tsum_of_dominated_convergence
    (f := fun (z : â„) (n : â„¤) â†¦ (-1) ^ n * cexp (Ï€ * I * n ^ 2 * z))
    (ğ“• := atImInfty)
    (g := fun k â†¦ if k = 0 then 1 else 0)
    (bound := fun n : â„¤ â†¦ rexp (-Ï€ * n ^ 2)) ?_ ?_ ?_
  Â· simp
  Â· apply summable_ofReal.mp
    have := (summable_jacobiThetaâ‚‚_term_iff 0 I).mpr (by simp)
    rw [â† summable_norm_iff, â† summable_ofReal] at this
    simp_rw [jacobiThetaâ‚‚_term, mul_zero, zero_add, mul_right_comm _ I, mul_assoc, â† sq, I_sq,
      mul_neg_one, norm_exp, re_ofReal_mul, neg_re, mul_neg, â† neg_mul, â† ofReal_intCast,
      â† ofReal_pow, ofReal_re] at this
    exact this
  Â· intro k
    simp only
    split_ifs with hk
    Â· subst hk
      simp
    Â· rw [tendsto_zero_iff_norm_tendsto_zero]
      simp_rw [mul_right_comm _ I, norm_mul, norm_zpow, norm_neg, norm_one, one_zpow, one_mul,
        norm_exp_mul_I, mul_assoc, im_ofReal_mul, â† ofReal_intCast, â† ofReal_pow, im_ofReal_mul,
        â† mul_assoc]
      simpa using tendsto_im_atImInfty.const_mul_atTop (by positivity)
  Â· rw [eventually_atImInfty]
    use 1, fun z hz k â†¦ ?_
    simp only
    simp_rw [mul_right_comm _ I, norm_mul, norm_zpow, norm_neg, norm_one, one_zpow, one_mul,
      norm_exp_mul_I]
    simpa [â† ofReal_intCast, â† ofReal_pow] using le_mul_of_one_le_right (by positivity) hz

theorem Î˜â‚‚_tendsto_atImInfty : Tendsto Î˜â‚‚ atImInfty (ğ“ 0) := by
  rw [funext Î˜â‚‚_as_jacobiThetaâ‚‚, â† zero_mul (2 : â„‚)]
  refine Tendsto.mul ?_ jacobiThetaâ‚‚_half_mul_apply_tendsto_atImInfty
  apply tendsto_zero_iff_norm_tendsto_zero.mpr
  -- simp_rw directly below fails
  have (z : â„) : â€–cexp (Ï€ * I * z / 4)â€– = rexp (-Ï€ * z.im / 4) := by
    rw [mul_right_comm, mul_div_right_comm, norm_exp_mul_I]
    simp [neg_div]
  simp_rw [this]
  exact (Real.tendsto_exp_atBot).comp <|
    -- TODO: tendsto_div_const_atBot_of_pos and its friends should be aliased under Tendsto.
    (tendsto_div_const_atBot_of_pos zero_lt_four).mpr
      (tendsto_im_atImInfty.const_mul_atTop_of_neg (neg_lt_zero.mpr Real.pi_pos))

theorem Î˜â‚ƒ_tendsto_atImInfty : Tendsto Î˜â‚ƒ atImInfty (ğ“ 1) := by
  simpa [funext Î˜â‚ƒ_as_jacobiThetaâ‚‚] using jacobiThetaâ‚‚_zero_apply_tendsto_atImInfty

theorem Î˜â‚„_tendsto_atImInfty : Tendsto Î˜â‚„ atImInfty (ğ“ 1) := by
  simpa [funext Î˜â‚„_as_jacobiThetaâ‚‚] using jacobiThetaâ‚‚_half_apply_tendsto_atImInfty

theorem Hâ‚‚_tendsto_atImInfty : Tendsto Hâ‚‚ atImInfty (ğ“ 0) := by
  convert Î˜â‚‚_tendsto_atImInfty.pow 4
  norm_num

theorem Hâ‚ƒ_tendsto_atImInfty : Tendsto Hâ‚ƒ atImInfty (ğ“ 1) := by
  convert Î˜â‚ƒ_tendsto_atImInfty.pow 4
  norm_num

theorem Hâ‚„_tendsto_atImInfty : Tendsto Hâ‚„ atImInfty (ğ“ 1) := by
  convert Î˜â‚„_tendsto_atImInfty.pow 4
  norm_num

/-!
## Jacobi identity asymptotics

We prove that g := Hâ‚‚ + Hâ‚„ - Hâ‚ƒ â†’ 0 at iâˆ, hence f := gÂ² â†’ 0.
Combined with the dimension vanishing for weight 4 cusp forms, this proves jacobi_identity.
-/

/-- The function g := Hâ‚‚ + Hâ‚„ - Hâ‚ƒ tends to 0 at iâˆ.
    Since Hâ‚‚ â†’ 0, Hâ‚ƒ â†’ 1, Hâ‚„ â†’ 1, we have g â†’ 0 + 1 - 1 = 0. -/
theorem jacobi_g_tendsto_atImInfty : Tendsto jacobi_g atImInfty (ğ“ 0) := by
  convert (Hâ‚‚_tendsto_atImInfty.add Hâ‚„_tendsto_atImInfty).sub Hâ‚ƒ_tendsto_atImInfty using 1
  norm_num

/-- The function f := gÂ² tends to 0 at iâˆ. -/
theorem jacobi_f_tendsto_atImInfty : Tendsto jacobi_f atImInfty (ğ“ 0) := by
  convert jacobi_g_tendsto_atImInfty.pow 2 using 1
  norm_num

/-- jacobi_f is bounded at iâˆ (follows from tending to 0) -/
lemma isBoundedAtImInfty_jacobi_f : IsBoundedAtImInfty jacobi_f :=
  IsZeroAtImInfty.isBoundedAtImInfty jacobi_f_tendsto_atImInfty

/-- jacobi_f slash by any SLâ‚‚(â„¤) element equals jacobi_f (for use with bounded_at_cusps) -/
lemma jacobi_f_slash_eq (A' : SL(2, â„¤)) :
    jacobi_f âˆ£[(4 : â„¤)] (SpecialLinearGroup.mapGL â„ A') = jacobi_f := by
  simpa [ModularForm.SL_slash] using jacobi_f_SL2Z_invariant A'

/-- jacobi_f slash by any SLâ‚‚(â„¤) element is bounded at iâˆ -/
lemma isBoundedAtImInfty_jacobi_f_slash :
    âˆ€ A âˆˆ ğ’®â„’, IsBoundedAtImInfty (jacobi_f âˆ£[(4 : â„¤)] (A : GL (Fin 2) â„)) := by
  intro A âŸ¨A', hAâŸ©
  rw [â† hA, jacobi_f_slash_eq A']
  exact isBoundedAtImInfty_jacobi_f

/-- jacobi_f as a ModularForm of weight 4 and level Î“(1) -/
noncomputable def jacobi_f_MF : ModularForm (CongruenceSubgroup.Gamma 1) 4 := {
  jacobi_f_SIF with
  holo' := jacobi_f_SIF_MDifferentiable
  bdd_at_cusps' := fun hc =>
    bounded_at_cusps_of_bounded_at_infty hc isBoundedAtImInfty_jacobi_f_slash
}

/-!
## Completing the Jacobi identity proof

With jacobi_f â†’ 0 at iâˆ, we can show jacobi_f_MF is a cusp form and apply
the dimension vanishing lemma to conclude f = 0, hence g = 0, hence Hâ‚‚ + Hâ‚„ = Hâ‚ƒ.
-/

/-- jacobi_f_MF is a cusp form because it vanishes at iâˆ -/
theorem jacobi_f_MF_IsCuspForm : IsCuspForm (CongruenceSubgroup.Gamma 1) 4 jacobi_f_MF := by
  rw [IsCuspForm_iff_coeffZero_eq_zero, ModularFormClass.qExpansion_coeff]; simp
  exact IsZeroAtImInfty.cuspFunction_apply_zero jacobi_f_tendsto_atImInfty
    (by norm_num : (0 : â„) < 1)

/-- The main dimension vanishing: jacobi_f_MF = 0 -/
theorem jacobi_f_MF_eq_zero : jacobi_f_MF = 0 :=
  IsCuspForm_weight_lt_eq_zero 4 (by norm_num) jacobi_f_MF jacobi_f_MF_IsCuspForm

/-- jacobi_f = 0 as a function -/
theorem jacobi_f_eq_zero : jacobi_f = 0 :=
  congr_arg (Â·.toFun) jacobi_f_MF_eq_zero

/-- jacobi_g = 0 as a function (from gÂ² = 0) -/
theorem jacobi_g_eq_zero : jacobi_g = 0 := by
  ext z
  simpa [jacobi_f] using congr_fun jacobi_f_eq_zero z

-- jacobi_identity is proved in ThetaDerivIdentities.lean using jacobi_g_eq_zero
