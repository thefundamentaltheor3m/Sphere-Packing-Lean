import SpherePacking.ModularForms.Eisenstein

open UpperHalfPlane hiding I
open Real Complex CongruenceSubgroup SlashAction SlashInvariantForm ContinuousMap

open scoped ModularForm MatrixGroups Manifold

/-!
Definition of (Serre) derivative of modular forms.
Prove Ramanujan's formulas on derivatives of Eisenstein series.
-/

noncomputable def D (F : â„ â†’ â„‚) : â„ â†’ â„‚ :=
  fun (z : â„) => (2 * Ï€ * I)â»Â¹ * ((deriv (F âˆ˜ ofComplex)) z)

/--
TODO: Remove this or move this to somewhere more appropriate.
-/
lemma MDifferentiableAt_DifferentiableAt {F : â„ â†’ â„‚} {z : â„}
  (h : MDifferentiableAt ğ“˜(â„‚) ğ“˜(â„‚) F z) :
  DifferentiableAt â„‚ (F âˆ˜ ofComplex) â†‘z := by
  have hâ‚ : DifferentiableWithinAt â„‚ (F âˆ˜ ofComplex) Set.univ â†‘z :=
    by simpa [writtenInExtChartAt, extChartAt, Set.range_id] using
      MDifferentiableWithinAt.differentiableWithinAt_writtenInExtChartAt h
  exact (differentiableWithinAt_univ.1 hâ‚)

/--
The converse direction: `DifferentiableAt` on â„‚ implies `MDifferentiableAt` on â„.
-/
lemma DifferentiableAt_MDifferentiableAt {G : â„‚ â†’ â„‚} {z : â„}
    (h : DifferentiableAt â„‚ G â†‘z) : MDifferentiableAt ğ“˜(â„‚) ğ“˜(â„‚) (G âˆ˜ (â†‘) : â„ â†’ â„‚) z := by
  rw [mdifferentiableAt_iff]
  -- Goal: DifferentiableAt â„‚ ((G âˆ˜ (â†‘)) âˆ˜ ofComplex) â†‘z
  -- The functions ((G âˆ˜ (â†‘)) âˆ˜ ofComplex) and G agree on the upper half-plane
  -- which is a neighborhood of â†‘z
  apply DifferentiableAt.congr_of_eventuallyEq h
  filter_upwards [isOpen_upperHalfPlaneSet.mem_nhds z.im_pos] with w hw
  simp only [Function.comp_apply, ofComplex_apply_of_im_pos hw]
  exact congrArg G (UpperHalfPlane.coe_mk w hw)

/--
The derivative operator `D` preserves MDifferentiability.
If `F : â„ â†’ â„‚` is MDifferentiable, then `D F` is also MDifferentiable.
-/
theorem D_differentiable {F : â„ â†’ â„‚} (hF : MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) F) :
    MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) (D F) := fun z =>
  let hDiffOn : DifferentiableOn â„‚ (F âˆ˜ ofComplex) {z : â„‚ | 0 < z.im} :=
    fun w hw => (MDifferentiableAt_DifferentiableAt (hF âŸ¨w, hwâŸ©)).differentiableWithinAt
  MDifferentiableAt.mul mdifferentiableAt_const <| DifferentiableAt_MDifferentiableAt <|
    (hDiffOn.deriv isOpen_upperHalfPlaneSet).differentiableAt
      (isOpen_upperHalfPlaneSet.mem_nhds z.im_pos)

/--
TODO: Move this to E2.lean.
-/
theorem Eâ‚‚_holo' : MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) Eâ‚‚ := sorry

/--
Basic properties of derivatives: linearity, Leibniz rule, etc.
-/
@[simp]
theorem D_add (F G : â„ â†’ â„‚) (hF : MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) F) (hG : MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) G) :
    D (F + G) = D F + D G := by
  ext z
  have h : deriv ((F âˆ˜ ofComplex) + (G âˆ˜ ofComplex)) z
      = deriv (F âˆ˜ ofComplex) z + deriv (G âˆ˜ ofComplex) z := by
    refine deriv_add ?_ ?_
    Â· exact MDifferentiableAt_DifferentiableAt (hF z)
    Â· exact MDifferentiableAt_DifferentiableAt (hG z)
  calc
    D (F + G) z
    _ = (2 * Ï€ * I)â»Â¹ * deriv ((F âˆ˜ ofComplex) + (G âˆ˜ ofComplex)) z := by rfl
    _ = (2 * Ï€ * I)â»Â¹ * (deriv (F âˆ˜ ofComplex) z + deriv (G âˆ˜ ofComplex) z)
      := by rw [h]
    _ = (2 * Ï€ * I)â»Â¹ * deriv (F âˆ˜ ofComplex) z
        + (2 * Ï€ * I)â»Â¹ * deriv (G âˆ˜ ofComplex) z
      := by simp [mul_add]
    _ = D F z + D G z := by rfl

@[simp]
theorem D_sub (F G : â„ â†’ â„‚) (hF : MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) F) (hG : MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) G)
    : D (F - G) = D F - D G := by
  ext z
  have h : deriv ((F âˆ˜ ofComplex) - (G âˆ˜ ofComplex)) z
      = deriv (F âˆ˜ ofComplex) z - deriv (G âˆ˜ ofComplex) z := by
    refine deriv_sub ?_ ?_
    Â· exact MDifferentiableAt_DifferentiableAt (hF z)
    Â· exact MDifferentiableAt_DifferentiableAt (hG z)
  calc
    D (F - G) z
    _ = (2 * Ï€ * I)â»Â¹ * deriv ((F âˆ˜ ofComplex) - (G âˆ˜ ofComplex)) z := by rfl
    _ = (2 * Ï€ * I)â»Â¹ * (deriv (F âˆ˜ ofComplex) z - deriv (G âˆ˜ ofComplex) z)
      := by rw [h]
    _ = (2 * Ï€ * I)â»Â¹ * deriv (F âˆ˜ ofComplex) z
        - (2 * Ï€ * I)â»Â¹ * deriv (G âˆ˜ ofComplex) z
      := by ring_nf
    _ = D F z - D G z := by rfl

@[simp]
theorem D_smul (c : â„‚) (F : â„ â†’ â„‚) (hF : MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) F)
    : D (c â€¢ F) = c â€¢ D F := by
  ext z
  have h : deriv (c â€¢ (F âˆ˜ ofComplex)) z = c â€¢ deriv (F âˆ˜ ofComplex) z :=
    deriv_const_mul c (MDifferentiableAt_DifferentiableAt (hF z))
  calc
    D (c â€¢ F) z
    _ = (2 * Ï€ * I)â»Â¹ * deriv (c â€¢ (F âˆ˜ ofComplex)) z := by rfl
    _ = (2 * Ï€ * I)â»Â¹ * (c * deriv (F âˆ˜ ofComplex) z) := by rw [h, smul_eq_mul]
    _ = c * ((2 * Ï€ * I)â»Â¹ * deriv (F âˆ˜ ofComplex) z) := by ring_nf
    _ = c * D F z := by rfl

@[simp]
theorem D_mul (F G : â„ â†’ â„‚) (hF : MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) F) (hG : MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) G)
    : D (F * G) = F * D G + D F * G := by
  ext z
  have h : deriv ((F âˆ˜ ofComplex) * (G âˆ˜ ofComplex)) z =
      F z * deriv (G âˆ˜ ofComplex) z + deriv (F âˆ˜ ofComplex) z * G z:= by
    have hFz := MDifferentiableAt_DifferentiableAt (hF z)
    have hGz := MDifferentiableAt_DifferentiableAt (hG z)
    rw [deriv_mul hFz hGz]
    simp only [Function.comp_apply, ofComplex_apply]
    group
  calc
    D (F * G) z
    _ = (2 * Ï€ * I)â»Â¹ * deriv (F âˆ˜ ofComplex * G âˆ˜ ofComplex) z := by rfl
    _ = (2 * Ï€ * I)â»Â¹ * (F z * deriv (G âˆ˜ ofComplex) z + deriv (F âˆ˜ ofComplex) z * G z)
      := by rw [h]
    _ = F z * ((2 * Ï€ * I)â»Â¹ * deriv (G âˆ˜ ofComplex) z) +
        (2 * Ï€ * I)â»Â¹ * deriv (F âˆ˜ ofComplex) z * G z
      := by ring_nf
    _ = F z * D G z + D F z * G z := by rfl

@[simp]
theorem D_sq (F : â„ â†’ â„‚) (hF : MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) F) :
    D (F ^ 2) = 2 * F * D F := by
  calc
    D (F ^ 2) = D (F * F) := by rw [pow_two]
    _ = F * D F + D F * F := by rw [D_mul F F hF hF]
    _ = 2 * F * D F := by ring_nf

@[simp]
theorem D_cube (F : â„ â†’ â„‚) (hF : MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) F) :
    D (F ^ 3) = 3 * F ^ 2 * D F := by
  have hF2 : MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) (F ^ 2) := by rw [pow_two]; exact MDifferentiable.mul hF hF
  calc
    D (F ^ 3) = D (F * F ^ 2) := by ring_nf
    _ = F * D (F ^ 2) + D F * F ^ 2 := by rw [D_mul F (F ^ 2) hF hF2]
    _ = F * (2 * F * D F) + D F * F ^ 2 := by rw [D_sq F hF]
    _ = 3 * F^2 * D F := by ring_nf

@[simp]
theorem D_const (c : â„‚) (z : â„) : D (Function.const _ c) z = 0 := by
  have h : deriv (Function.const _ c âˆ˜ ofComplex) z = 0 := by
    have h' : Function.const _ c âˆ˜ ofComplex = Function.const _ c := by rfl
    rw [h']
    exact deriv_const _ c
  calc
    D (Function.const _ c) z
    _ = (2 * Ï€ * I)â»Â¹ * deriv (Function.const _ c âˆ˜ ofComplex) z := by rfl
    _ = (2 * Ï€ * I)â»Â¹ * 0 := by rw [h]
    _ = 0 := by ring_nf

/--
**Lemma 6.45 (Blueprint)**: The Serre derivative D acts as qÂ·d/dq on q-series.
For a single q-power term: D(aÂ·qâ¿) = nÂ·aÂ·qâ¿ where q = exp(2Ï€iz).

The key calculation:
- d/dz(exp(2Ï€inz)) = 2Ï€inÂ·exp(2Ï€inz)
- D(exp(2Ï€inz)) = (2Ï€i)â»Â¹Â·(2Ï€inÂ·exp(2Ï€inz)) = nÂ·exp(2Ï€inz)
-/
theorem D_qexp_term (n : â„•) (a : â„‚) (z : â„) :
    D (fun w => a * cexp (2 * Ï€ * I * n * w)) z =
      n * a * cexp (2 * Ï€ * I * n * z) := by
  simp only [D]
  -- Compute the derivative of a * exp(2Ï€inz)
  have h_deriv : deriv ((fun w : â„ => a * cexp (2 * Ï€ * I * n * w)) âˆ˜ ofComplex) (z : â„‚) =
      a * (2 * Ï€ * I * n) * cexp (2 * Ï€ * I * n * z) := by
    -- Step 1: Derivative of exp(2Ï€inz) using chain rule
    have h_exp_deriv : HasDerivAt (fun w : â„‚ => a * cexp (2 * Ï€ * I * n * w))
        (a * (2 * Ï€ * I * n) * cexp (2 * Ï€ * I * n * z)) (z : â„‚) := by
      have h_at_arg : HasDerivAt cexp (cexp (2 * Ï€ * I * n * z)) (2 * Ï€ * I * n * z) :=
        Complex.hasDerivAt_exp (2 * Ï€ * I * n * z)
      have h_linear : HasDerivAt (fun w : â„‚ => 2 * Ï€ * I * n * w) (2 * Ï€ * I * n) (z : â„‚) := by
        have h := (hasDerivAt_id (z : â„‚)).const_mul (2 * Ï€ * I * n)
        simp only [mul_one, id] at h
        exact h
      have h_comp := h_at_arg.scomp (z : â„‚) h_linear
      -- Rewrite the scalar multiplication as multiplication
      simp only [smul_eq_mul] at h_comp
      have h_const_mul := h_comp.const_mul a
      convert h_const_mul using 1; ring
    -- Step 2: The composed function equals the â„‚ function in a neighborhood of z
    have h_agree : ((fun w : â„ => a * cexp (2 * Ï€ * I * n * w)) âˆ˜ ofComplex) =á¶ [nhds (z : â„‚)]
        (fun w : â„‚ => a * cexp (2 * Ï€ * I * n * w)) := by
      have him : 0 < (z : â„‚).im := z.2
      have h_open : IsOpen {w : â„‚ | 0 < w.im} := isOpen_lt continuous_const Complex.continuous_im
      filter_upwards [h_open.mem_nhds him] with w hw
      simp only [Function.comp_apply, ofComplex_apply_of_im_pos hw, coe_mk_subtype]
    exact h_agree.deriv_eq.trans h_exp_deriv.deriv
  rw [h_deriv]
  -- Simplify: (2Ï€i)â»Â¹ * a * (2Ï€in) * exp(...) = n * a * exp(...)
  have h_2piI_ne : (2 : â„‚) * Ï€ * I â‰  0 := by
    simp only [ne_eq, mul_eq_zero, OfNat.ofNat_ne_zero, ofReal_eq_zero, pi_ne_zero, I_ne_zero,
      or_self, not_false_eq_true]
  field_simp [h_2piI_ne]

/--
Variant of `D_qexp_term` for integer exponents, covering negative powers in theta series.
D(aÂ·q^n) = nÂ·aÂ·q^n where q = exp(2Ï€iz) and n âˆˆ â„¤.
-/
theorem D_qexp_term_int (n : â„¤) (a : â„‚) (z : â„) :
    D (fun w => a * cexp (2 * Ï€ * I * n * w)) z =
      n * a * cexp (2 * Ï€ * I * n * z) := by
  simp only [D]
  have h_deriv : deriv ((fun w : â„ => a * cexp (2 * Ï€ * I * n * w)) âˆ˜ ofComplex) (z : â„‚) =
      a * (2 * Ï€ * I * n) * cexp (2 * Ï€ * I * n * z) := by
    have h_exp_deriv : HasDerivAt (fun w : â„‚ => a * cexp (2 * Ï€ * I * n * w))
        (a * (2 * Ï€ * I * n) * cexp (2 * Ï€ * I * n * z)) (z : â„‚) := by
      have h_at_arg : HasDerivAt cexp (cexp (2 * Ï€ * I * n * z)) (2 * Ï€ * I * n * z) :=
        Complex.hasDerivAt_exp (2 * Ï€ * I * n * z)
      have h_linear : HasDerivAt (fun w : â„‚ => 2 * Ï€ * I * n * w) (2 * Ï€ * I * n) (z : â„‚) := by
        have h := (hasDerivAt_id (z : â„‚)).const_mul (2 * Ï€ * I * n)
        simp only [mul_one, id] at h
        exact h
      have h_comp := h_at_arg.scomp (z : â„‚) h_linear
      simp only [smul_eq_mul] at h_comp
      have h_const_mul := h_comp.const_mul a
      convert h_const_mul using 1; ring
    have h_agree : ((fun w : â„ => a * cexp (2 * Ï€ * I * n * w)) âˆ˜ ofComplex) =á¶ [nhds (z : â„‚)]
        (fun w : â„‚ => a * cexp (2 * Ï€ * I * n * w)) := by
      have him : 0 < (z : â„‚).im := z.2
      have h_open : IsOpen {w : â„‚ | 0 < w.im} := isOpen_lt continuous_const Complex.continuous_im
      filter_upwards [h_open.mem_nhds him] with w hw
      simp only [Function.comp_apply, ofComplex_apply_of_im_pos hw, coe_mk_subtype]
    exact h_agree.deriv_eq.trans h_exp_deriv.deriv
  rw [h_deriv]
  have h_2piI_ne : (2 : â„‚) * Ï€ * I â‰  0 := by
    simp only [ne_eq, mul_eq_zero, OfNat.ofNat_ne_zero, ofReal_eq_zero, pi_ne_zero, I_ne_zero,
      or_self, not_false_eq_true]
  field_simp [h_2piI_ne]

/--
**Lemma 6.45 (Blueprint)**: D commutes with tsum for q-series.
If F(z) = Î£ a(n)Â·qâ¿ where q = exp(2Ï€iz), then D F(z) = Î£ nÂ·a(n)Â·qâ¿.

More precisely, this lemma shows that for a â„•-indexed q-series with summable coefficients
satisfying appropriate derivative bounds, D acts termwise by multiplying coefficients by n.
-/
theorem D_qexp_tsum (a : â„• â†’ â„‚) (z : â„)
    (_hsum : Summable (fun n => a n * cexp (2 * Ï€ * I * n * z)))
    (hsum_deriv : âˆ€ K : Set â„‚, K âŠ† {w : â„‚ | 0 < w.im} â†’ IsCompact K â†’
        âˆƒ u : â„• â†’ â„, Summable u âˆ§ âˆ€ n (k : K), â€–a n * (2 * Ï€ * I * n) *
          cexp (2 * Ï€ * I * n * k.1)â€– â‰¤ u n) :
    D (fun w => âˆ‘' n, a n * cexp (2 * Ï€ * I * n * w)) z =
      âˆ‘' n : â„•, (n : â„‚) * a n * cexp (2 * Ï€ * I * n * z) := by
  simp only [D]
  -- Use hasDerivAt_tsum_fun on the upper half-plane
  have hs : IsOpen {w : â„‚ | 0 < w.im} := isOpen_lt continuous_const Complex.continuous_im
  have hx : (z : â„‚) âˆˆ {w : â„‚ | 0 < w.im} := z.2
  -- Each term is differentiable
  have hf_diff : âˆ€ n (r : {w : â„‚ | 0 < w.im}), DifferentiableAt â„‚
      (fun w => a n * cexp (2 * Ï€ * I * n * w)) r := fun n r =>
    ((differentiableAt_id.const_mul (2 * Ï€ * I * n)).cexp).const_mul (a n)
  -- Summability at each point
  have hf_sum : âˆ€ y : â„‚, y âˆˆ {w : â„‚ | 0 < w.im} â†’
      Summable (fun n => a n * cexp (2 * Ï€ * I * n * y)) := by
    intro y hy
    -- Singleton {y} is compact
    have hK_compact : IsCompact ({y} : Set â„‚) := isCompact_singleton
    have hK_sub : ({y} : Set â„‚) âŠ† {w : â„‚ | 0 < w.im} := Set.singleton_subset_iff.mpr hy
    -- Apply hsum_deriv to get bound on derivative terms
    obtain âŸ¨u, hu_sum, hu_boundâŸ© := hsum_deriv {y} hK_sub hK_compact
    -- For n â‰¥ 1: â€–a n * exp(...)â€– â‰¤ u n / â€–2Ï€inâ€– â‰¤ u n / (2Ï€)
    -- For n = 0: term is just a(0), handle via cofinite filter (only finitely many exceptions)
    apply Summable.of_norm_bounded_eventually (g := fun n => u n / (2 * Ï€))
        (hu_sum.div_const (2 * Ï€))
    -- Bound holds eventually (i.e., for all but finitely many n)
    -- In fact, it holds for all n â‰¥ 1, so the exception set âŠ† {0}
    rw [Filter.eventually_cofinite]
    apply Set.Finite.subset (Set.finite_singleton 0)
    intro n hn
    simp only [Set.mem_setOf_eq, not_le] at hn
    simp only [Set.mem_singleton_iff]
    by_contra h_ne
    -- n â‰¥ 1, so we can derive the bound and get a contradiction
    have h_deriv_bound := hu_bound n âŸ¨y, Set.mem_singleton yâŸ©
    have h_n_ge_1 : (1 : â„) â‰¤ n := Nat.one_le_cast.mpr (Nat.one_le_iff_ne_zero.mpr h_ne)
    have h_2pi_pos : (0 : â„) < 2 * Ï€ := by positivity
    have hu_nn : 0 â‰¤ u n := le_trans (norm_nonneg _) h_deriv_bound
    -- Key bound: â€–a n * exp(2Ï€iny)â€– â‰¤ u n / (2Ï€n) â‰¤ u n / (2Ï€)
    have h_bound : â€–a n * cexp (2 * Ï€ * I * n * y)â€– â‰¤ u n / (2 * Ï€) := by
      -- â€–a n * exp(...)â€– â‰¤ â€–a n * 2Ï€in * exp(...)â€– / â€–2Ï€inâ€– â‰¤ u n / (2Ï€n)
      have h_factor_pos : (0 : â„) < 2 * Ï€ * n := by positivity
      -- â€–2Ï€inâ€– = 2Ï€n
      have h_norm_2pin : â€–(2 : â„‚) * Ï€ * I * nâ€– = 2 * Ï€ * n := by
        rw [norm_mul, norm_mul, norm_mul, Complex.norm_ofNat, Complex.norm_real,
            Complex.norm_I, mul_one, Complex.norm_natCast, Real.norm_of_nonneg (le_of_lt pi_pos)]
      -- The key algebraic step
      have h_mul_div : â€–a n * cexp (2 * Ï€ * I * n * y)â€– * (2 * Ï€ * n) =
          â€–a n * (2 * Ï€ * I * n) * cexp (2 * Ï€ * I * n * y)â€– := by
        rw [â† h_norm_2pin]
        simp only [norm_mul]
        ring
      calc â€–a n * cexp (2 * Ï€ * I * n * y)â€–
          = â€–a n * cexp (2 * Ï€ * I * n * y)â€– * (2 * Ï€ * n) / (2 * Ï€ * n) := by
            field_simp
        _ = â€–a n * (2 * Ï€ * I * n) * cexp (2 * Ï€ * I * n * y)â€– / (2 * Ï€ * n) := by
            rw [h_mul_div]
        _ â‰¤ u n / (2 * Ï€ * n) := by
            apply div_le_div_of_nonneg_right h_deriv_bound (le_of_lt h_factor_pos)
        _ â‰¤ u n / (2 * Ï€) := by
            apply div_le_div_of_nonneg_left hu_nn h_2pi_pos
            have h2 : 2 * Ï€ * 1 â‰¤ 2 * Ï€ * n := by nlinarith
            linarith
    exact hn.not_ge h_bound
  -- Derivative bound for uniform convergence
  have hu : âˆ€ K âŠ† {w : â„‚ | 0 < w.im}, IsCompact K â†’
      âˆƒ u : â„• â†’ â„, Summable u âˆ§ âˆ€ n (k : K),
        â€–derivWithin (fun w => a n * cexp (2 * Ï€ * I * n * w)) {w : â„‚ | 0 < w.im} kâ€– â‰¤ u n := by
    intro K hK1 hK2
    -- Derivative of a_n * exp(2Ï€inz) is a_n * 2Ï€in * exp(2Ï€inz)
    have h_deriv_eq : âˆ€ n (k : K), derivWithin (fun w => a n * cexp (2 * Ï€ * I * n * w))
        {w : â„‚ | 0 < w.im} k = a n * (2 * Ï€ * I * n) * cexp (2 * Ï€ * I * n * k.1) := by
      intro n k
      have h_chain : HasDerivAt (fun w : â„‚ => a n * cexp (2 * Ï€ * I * n * w))
          (a n * (2 * Ï€ * I * n) * cexp (2 * Ï€ * I * n * k.1)) k.1 := by
        have h_exp := Complex.hasDerivAt_exp (2 * Ï€ * I * n * k.1)
        have h_lin' := (hasDerivAt_id k.1).const_mul (2 * Ï€ * I * n)
        simp only [id, mul_one] at h_lin'
        have h_comp := h_exp.scomp k.1 h_lin'
        simp only [smul_eq_mul] at h_comp
        convert h_comp.const_mul (a n) using 1; ring
      exact (h_chain.hasDerivWithinAt (s := {w : â„‚ | 0 < w.im})).derivWithin
        (hs.uniqueDiffWithinAt (hK1 k.2))
    obtain âŸ¨u, hu_sum, hu_boundâŸ© := hsum_deriv K hK1 hK2
    refine âŸ¨u, hu_sum, fun n k => ?_âŸ©
    rw [h_deriv_eq]
    exact hu_bound n k
  -- Apply termwise differentiation
  have h_tsum_deriv := hasDerivAt_tsum_fun (fun n w => a n * cexp (2 * Ï€ * I * n * w))
    hs (z : â„‚) hx hf_sum hu hf_diff
  -- The composed function with ofComplex equals the â„‚ function in a neighborhood
  have h_agree : ((fun w : â„ => âˆ‘' n, a n * cexp (2 * Ï€ * I * n * w)) âˆ˜ ofComplex) =á¶ [nhds (z : â„‚)]
      (fun w => âˆ‘' n, a n * cexp (2 * Ï€ * I * n * w)) := by
    filter_upwards [hs.mem_nhds hx] with w hw
    simp only [Function.comp_apply, ofComplex_apply_of_im_pos hw, coe_mk_subtype]
  rw [h_agree.deriv_eq, h_tsum_deriv.deriv]
  -- Simplify: derivWithin equals 2Ï€in * (term) on open set, factor out (2Ï€i)â»Â¹
  have h_deriv_simp : âˆ€ n, derivWithin (fun w => a n * cexp (2 * Ï€ * I * n * w))
      {w : â„‚ | 0 < w.im} (z : â„‚) = a n * (2 * Ï€ * I * n) * cexp (2 * Ï€ * I * n * z) := by
    intro n
    have h_chain : HasDerivAt (fun w : â„‚ => a n * cexp (2 * Ï€ * I * n * w))
        (a n * (2 * Ï€ * I * n) * cexp (2 * Ï€ * I * n * z)) (z : â„‚) := by
      have h_exp := Complex.hasDerivAt_exp (2 * Ï€ * I * n * z)
      have h_lin' := (hasDerivAt_id (z : â„‚)).const_mul (2 * Ï€ * I * n)
      simp only [id, mul_one] at h_lin'
      have h_comp := h_exp.scomp (z : â„‚) h_lin'
      simp only [smul_eq_mul] at h_comp
      convert h_comp.const_mul (a n) using 1; ring
    exact (h_chain.hasDerivWithinAt (s := {w : â„‚ | 0 < w.im})).derivWithin
      (hs.uniqueDiffWithinAt hx)
  simp_rw [h_deriv_simp]
  -- Factor out (2Ï€i)â»Â¹ from the tsum
  have h_2piI_ne : (2 : â„‚) * Ï€ * I â‰  0 := by
    simp only [ne_eq, mul_eq_zero, OfNat.ofNat_ne_zero, ofReal_eq_zero, pi_ne_zero, I_ne_zero,
      or_self, not_false_eq_true]
  rw [â† tsum_mul_left]
  congr 1
  funext n
  field_simp [h_2piI_ne]

/--
Simplified version of `D_qexp_tsum` for â„•+-indexed series (starting from n=1).
This is the form most commonly used for Eisenstein series q-expansions.
-/
theorem D_qexp_tsum_pnat (a : â„•+ â†’ â„‚) (z : â„)
    (_hsum : Summable (fun n : â„•+ => a n * cexp (2 * Ï€ * I * n * z)))
    (hsum_deriv : âˆ€ K : Set â„‚, K âŠ† {w : â„‚ | 0 < w.im} â†’ IsCompact K â†’
        âˆƒ u : â„•+ â†’ â„, Summable u âˆ§ âˆ€ n (k : K), â€–a n * (2 * Ï€ * I * n) *
          cexp (2 * Ï€ * I * n * k.1)â€– â‰¤ u n) :
    D (fun w => âˆ‘' n : â„•+, a n * cexp (2 * Ï€ * I * n * w)) z =
      âˆ‘' n : â„•+, (n : â„‚) * a n * cexp (2 * Ï€ * I * n * z) := by
  simp only [D]
  -- Use hasDerivAt_tsum_fun on the upper half-plane
  have hs : IsOpen {w : â„‚ | 0 < w.im} := isOpen_lt continuous_const Complex.continuous_im
  have hx : (z : â„‚) âˆˆ {w : â„‚ | 0 < w.im} := z.2
  -- Each term is differentiable
  have hf_diff : âˆ€ (n : â„•+) (r : {w : â„‚ | 0 < w.im}), DifferentiableAt â„‚
      (fun w => a n * cexp (2 * Ï€ * I * n * w)) r := fun n r =>
    ((differentiableAt_id.const_mul (2 * Ï€ * I * n)).cexp).const_mul (a n)
  -- Summability at each point - simpler than â„• case since all n â‰¥ 1
  have hf_sum : âˆ€ y : â„‚, y âˆˆ {w : â„‚ | 0 < w.im} â†’
      Summable (fun n : â„•+ => a n * cexp (2 * Ï€ * I * n * y)) := by
    intro y hy
    have hK_compact : IsCompact ({y} : Set â„‚) := isCompact_singleton
    have hK_sub : ({y} : Set â„‚) âŠ† {w : â„‚ | 0 < w.im} := Set.singleton_subset_iff.mpr hy
    obtain âŸ¨u, hu_sum, hu_boundâŸ© := hsum_deriv {y} hK_sub hK_compact
    -- For â„•+, every n â‰¥ 1, so bound holds uniformly
    apply Summable.of_norm_bounded (g := fun n => u n / (2 * Ï€)) (hu_sum.div_const (2 * Ï€))
    intro n
    have h_deriv_bound := hu_bound n âŸ¨y, Set.mem_singleton yâŸ©
    have h_n_ge_1 : (1 : â„) â‰¤ n := by exact_mod_cast n.one_le
    have h_2pi_pos : (0 : â„) < 2 * Ï€ := by positivity
    have hu_nn : 0 â‰¤ u n := le_trans (norm_nonneg _) h_deriv_bound
    -- Key bound: â€–a n * exp(2Ï€iny)â€– â‰¤ u n / (2Ï€n) â‰¤ u n / (2Ï€)
    have h_factor_pos : (0 : â„) < 2 * Ï€ * n := by positivity
    have h_norm_2pin : â€–(2 : â„‚) * Ï€ * I * nâ€– = 2 * Ï€ * n := by
      rw [norm_mul, norm_mul, norm_mul, Complex.norm_ofNat, Complex.norm_real,
          Complex.norm_I, mul_one, Complex.norm_natCast,
          Real.norm_of_nonneg (le_of_lt pi_pos)]
    have h_mul_div : â€–a n * cexp (2 * Ï€ * I * n * y)â€– * (2 * Ï€ * n) =
        â€–a n * (2 * Ï€ * I * n) * cexp (2 * Ï€ * I * n * y)â€– := by
      rw [â† h_norm_2pin]
      simp only [norm_mul]
      ring
    calc â€–a n * cexp (2 * Ï€ * I * n * y)â€–
        = â€–a n * cexp (2 * Ï€ * I * n * y)â€– * (2 * Ï€ * n) / (2 * Ï€ * n) := by field_simp
      _ = â€–a n * (2 * Ï€ * I * n) * cexp (2 * Ï€ * I * n * y)â€– / (2 * Ï€ * n) := by rw [h_mul_div]
      _ â‰¤ u n / (2 * Ï€ * n) := by
          apply div_le_div_of_nonneg_right h_deriv_bound (le_of_lt h_factor_pos)
      _ â‰¤ u n / (2 * Ï€) := by
          apply div_le_div_of_nonneg_left hu_nn h_2pi_pos
          have h2 : 2 * Ï€ * 1 â‰¤ 2 * Ï€ * n := by nlinarith
          linarith
  -- Derivative bound for uniform convergence
  have hu : âˆ€ K âŠ† {w : â„‚ | 0 < w.im}, IsCompact K â†’
      âˆƒ u : â„•+ â†’ â„, Summable u âˆ§ âˆ€ n (k : K),
        â€–derivWithin (fun w => a n * cexp (2 * Ï€ * I * n * w)) {w : â„‚ | 0 < w.im} kâ€– â‰¤ u n := by
    intro K hK1 hK2
    have h_deriv_eq : âˆ€ n (k : K), derivWithin (fun w => a n * cexp (2 * Ï€ * I * n * w))
        {w : â„‚ | 0 < w.im} k = a n * (2 * Ï€ * I * n) * cexp (2 * Ï€ * I * n * k.1) := by
      intro n k
      have h_chain : HasDerivAt (fun w : â„‚ => a n * cexp (2 * Ï€ * I * n * w))
          (a n * (2 * Ï€ * I * n) * cexp (2 * Ï€ * I * n * k.1)) k.1 := by
        have h_exp := Complex.hasDerivAt_exp (2 * Ï€ * I * n * k.1)
        have h_lin' := (hasDerivAt_id k.1).const_mul (2 * Ï€ * I * n)
        simp only [id, mul_one] at h_lin'
        have h_comp := h_exp.scomp k.1 h_lin'
        simp only [smul_eq_mul] at h_comp
        convert h_comp.const_mul (a n) using 1; ring
      exact (h_chain.hasDerivWithinAt (s := {w : â„‚ | 0 < w.im})).derivWithin
        (hs.uniqueDiffWithinAt (hK1 k.2))
    obtain âŸ¨u, hu_sum, hu_boundâŸ© := hsum_deriv K hK1 hK2
    refine âŸ¨u, hu_sum, fun n k => ?_âŸ©
    rw [h_deriv_eq]
    exact hu_bound n k
  -- Apply termwise differentiation
  have h_tsum_deriv := hasDerivAt_tsum_fun (fun n w => a n * cexp (2 * Ï€ * I * n * w))
    hs (z : â„‚) hx hf_sum hu hf_diff
  -- The composed function with ofComplex equals the â„‚ function in a neighborhood
  have h_agree : ((fun w : â„ => âˆ‘' n : â„•+, a n * cexp (2 * Ï€ * I * n * w)) âˆ˜ ofComplex)
      =á¶ [nhds (z : â„‚)] (fun w => âˆ‘' n : â„•+, a n * cexp (2 * Ï€ * I * n * w)) := by
    filter_upwards [hs.mem_nhds hx] with w hw
    simp only [Function.comp_apply, ofComplex_apply_of_im_pos hw, coe_mk_subtype]
  rw [h_agree.deriv_eq, h_tsum_deriv.deriv]
  -- Simplify: derivWithin equals 2Ï€in * (term) on open set, factor out (2Ï€i)â»Â¹
  have h_deriv_simp : âˆ€ n : â„•+, derivWithin (fun w => a n * cexp (2 * Ï€ * I * n * w))
      {w : â„‚ | 0 < w.im} (z : â„‚) = a n * (2 * Ï€ * I * n) * cexp (2 * Ï€ * I * n * z) := by
    intro n
    have h_chain : HasDerivAt (fun w : â„‚ => a n * cexp (2 * Ï€ * I * n * w))
        (a n * (2 * Ï€ * I * n) * cexp (2 * Ï€ * I * n * z)) (z : â„‚) := by
      have h_exp := Complex.hasDerivAt_exp (2 * Ï€ * I * n * z)
      have h_lin' := (hasDerivAt_id (z : â„‚)).const_mul (2 * Ï€ * I * n)
      simp only [id, mul_one] at h_lin'
      have h_comp := h_exp.scomp (z : â„‚) h_lin'
      simp only [smul_eq_mul] at h_comp
      convert h_comp.const_mul (a n) using 1; ring
    exact (h_chain.hasDerivWithinAt (s := {w : â„‚ | 0 < w.im})).derivWithin
      (hs.uniqueDiffWithinAt hx)
  simp_rw [h_deriv_simp]
  -- Factor out (2Ï€i)â»Â¹ from the tsum
  have h_2piI_ne : (2 : â„‚) * Ï€ * I â‰  0 := by
    simp only [ne_eq, mul_eq_zero, OfNat.ofNat_ne_zero, ofReal_eq_zero, pi_ne_zero, I_ne_zero,
      or_self, not_false_eq_true]
  rw [â† tsum_mul_left]
  congr 1
  funext n
  field_simp [h_2piI_ne]

/--
Serre derivative of weight $k$.
Note that the definition makes sense for any analytic function $F : \mathbb{H} \to \mathbb{C}$.
-/
noncomputable def serre_D (k : â„‚) : (â„ â†’ â„‚) â†’ (â„ â†’ â„‚) :=
  fun (F : â„ â†’ â„‚) => (fun z => D F z - k * 12â»Â¹ * Eâ‚‚ z * F z)

/--
Basic properties of Serre derivative: linearity, Leibniz rule, etc.
-/
theorem serre_D_add (k : â„¤) (F G : â„ â†’ â„‚) (hF : MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) F)
    (hG : MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) G) : serre_D k (F + G) = serre_D k F + serre_D k G := by
  ext z
  simp only [serre_D, Pi.add_apply, D_add F G hF hG]
  ring_nf

theorem serre_D_smul (k : â„¤) (c : â„‚) (F : â„ â†’ â„‚) (hF : MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) F) (z : â„) :
    serre_D k (c â€¢ F) z = c * serre_D k F z := by
  simp only [serre_D, D_smul c F hF]
  simp
  ring_nf

theorem serre_D_mul (kâ‚ kâ‚‚ : â„¤) (F G : â„ â†’ â„‚) (hF : MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) F)
    (hG : MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) G) (z : â„) :
    serre_D (kâ‚ + kâ‚‚) (F * G) z = F z * serre_D kâ‚ G z + G z * serre_D kâ‚‚ F z := by
  simp only [serre_D, D_mul F G hF hG]
  simp
  ring_nf

/--
The Serre derivative preserves MDifferentiability.
If `F : â„ â†’ â„‚` is MDifferentiable, then `serre_D k F` is also MDifferentiable.
-/
theorem serre_D_differentiable {F : â„ â†’ â„‚} {k : â„‚}
    (hF : MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) F) :
    MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) (serre_D k F) := by
  -- serre_D k F = D F - k * 12â»Â¹ * Eâ‚‚ * F
  have h_term : MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) (fun z => k * 12â»Â¹ * Eâ‚‚ z * F z) := by
    have h1 : MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) (fun z => (k * 12â»Â¹) * (Eâ‚‚ z * F z)) :=
      MDifferentiable.mul mdifferentiable_const (Eâ‚‚_holo'.mul hF)
    convert h1 using 1; ext z; simp only [mul_assoc]
  exact (D_differentiable hF).sub h_term

/--
Serre derivative is equivariant under the slash action. More precisely, if `F` is invariant
under the slash action of weight `k`, then `serre_D k F` is invariant under the slash action
of weight `k + 2`.
-/
theorem serre_D_slash_equivariant (k : â„¤) (F : â„ â†’ â„‚) (hF : MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) F) :
    âˆ€ Î³ : SL(2, â„¤), serre_D k F âˆ£[k + 2] Î³ = serre_D k (F âˆ£[k] Î³) := by sorry

theorem serre_D_slash_invariant (k : â„¤) (F : â„ â†’ â„‚) (hF : MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) F)
    (Î³ : SL(2, â„¤)) (h : F âˆ£[k] Î³ = F) :
    serre_D k F âˆ£[k + 2] Î³ = serre_D k F := by
  rw [serre_D_slash_equivariant, h]
  exact hF

/--
Serre derivative of Eisenstein series. Use `serre_D_slash_invariant` and compare constant terms.
Note that the dimensions of the spaces of modular forms are all 1.
-/
theorem ramanujan_Eâ‚‚' : serre_D 1 Eâ‚‚ = - 12â»Â¹ * Eâ‚„.toFun := by sorry

theorem ramanujan_Eâ‚„' : serre_D 4 Eâ‚„.toFun = - 3â»Â¹ * Eâ‚†.toFun := by sorry

theorem ramanujan_Eâ‚†' : serre_D 6 Eâ‚†.toFun = - 2â»Â¹ * Eâ‚„.toFun * Eâ‚„.toFun := by sorry

@[simp]
theorem ramanujan_Eâ‚‚ : D Eâ‚‚ = 12â»Â¹ * (Eâ‚‚ * Eâ‚‚ - Eâ‚„.toFun) := by
  ext z
  have h := ramanujan_Eâ‚‚'
  unfold serre_D at h
  have h1 := congrFun h z
  simp [field]
  field_simp at h1
  simpa [add_comm, sub_eq_iff_eq_add] using h1

@[simp]
theorem ramanujan_Eâ‚„ : D Eâ‚„.toFun = 3â»Â¹ * (Eâ‚‚ * Eâ‚„.toFun - Eâ‚†.toFun) := by
  ext z
  have h := ramanujan_Eâ‚„'
  unfold serre_D at h
  have h1 := congrFun h z
  simp [field]
  simp [field] at h1
  ring_nf
  ring_nf at h1
  have hc : (12 : â„‚) â‰  0 := by norm_num
  apply (mul_right_inj' hc).mp
  ring_nf
  simpa [add_comm, sub_eq_iff_eq_add] using h1

@[simp]
theorem ramanujan_Eâ‚† : D Eâ‚†.toFun = 2â»Â¹ * (Eâ‚‚ * Eâ‚†.toFun - Eâ‚„.toFun * Eâ‚„.toFun) := by
  ext z
  have h := ramanujan_Eâ‚†'
  unfold serre_D at h
  have h1 := congrFun h z
  simp [field]
  simp [field] at h1
  ring_nf
  ring_nf at h1
  have hc : (12 : â„‚) â‰  0 := by norm_num
  apply (mul_right_inj' hc).mp
  ring_nf
  simpa [add_comm, sub_eq_iff_eq_add] using h1


/--
Prove modular linear differential equation satisfied by $F$.
-/
noncomputable def Xâ‚„â‚‚ := 288â»Â¹ * (Eâ‚„.toFun - Eâ‚‚ * Eâ‚‚)

noncomputable def Î”_fun := 1728â»Â¹ * (Eâ‚„.toFun ^ 3 - Eâ‚†.toFun ^ 2)

noncomputable def F := (Eâ‚‚ * Eâ‚„.toFun - Eâ‚†.toFun) ^ 2

theorem F_aux : D F = 5 * 6â»Â¹ * Eâ‚‚ ^ 3 * Eâ‚„.toFun ^ 2 - 5 * 2â»Â¹ * Eâ‚‚ ^ 2 * Eâ‚„.toFun * Eâ‚†.toFun
    + 5 * 6â»Â¹ * Eâ‚‚ * Eâ‚„.toFun ^ 3 + 5 * 3â»Â¹ * Eâ‚‚ * Eâ‚†.toFun ^ 2 - 5 * 6â»Â¹ * Eâ‚„.toFun^2 * Eâ‚†.toFun
    := by
  rw [F, D_sq, D_sub, D_mul]
  Â· ring_nf
    rw [ramanujan_Eâ‚‚, ramanujan_Eâ‚„, ramanujan_Eâ‚†]
    ext z
    simp
    ring_nf
  -- Holomorphicity of the terms
  Â· exact Eâ‚‚_holo'
  Â· exact Eâ‚„.holo'
  Â· exact MDifferentiable.mul Eâ‚‚_holo' Eâ‚„.holo'
  Â· exact Eâ‚†.holo'
  have h24 := MDifferentiable.mul Eâ‚‚_holo' Eâ‚„.holo'
  exact MDifferentiable.sub h24 Eâ‚†.holo'


/--
Modular linear differential equation satisfied by `F`.
TODO: Move this to a more appropriate place.
-/
theorem MLDE_F : serre_D 12 (serre_D 10 F) = 5 * 6â»Â¹ * F + 172800 * Î”_fun * Xâ‚„â‚‚ := by
  ext x
  rw [Xâ‚„â‚‚, Î”_fun, serre_D, serre_D, F_aux]
  unfold serre_D
  rw [F_aux]
  sorry

example : D (Eâ‚„.toFun * Eâ‚„.toFun) = 2 * 3â»Â¹ * Eâ‚„.toFun * (Eâ‚‚ * Eâ‚„.toFun - Eâ‚†.toFun) :=
  by
  rw [D_mul Eâ‚„.toFun Eâ‚„.toFun]
  Â· simp only [ramanujan_Eâ‚„]
    ring_nf
  Â· exact Eâ‚„.holo'
  Â· exact Eâ‚„.holo'

/-
Interaction between (Serre) derivative and restriction to the imaginary axis.
-/

/--
Chain rule for restriction to imaginary axis: `d/dt F(it) = -2Ï€ * (D F)(it)`.

This connects the real derivative along the imaginary axis to the normalized derivative D.
The key computation is:
- The imaginary axis is parametrized by g(t) = I * t
- By chain rule: d/dt F(it) = (dF/dz)(it) Â· (d/dt)(it) = F'(it) Â· I
- Since D = (2Ï€i)â»Â¹ Â· d/dz, we have F' = 2Ï€i Â· D F
- So d/dt F(it) = 2Ï€i Â· D F(it) Â· I = -2Ï€ Â· D F(it)
-/
theorem deriv_resToImagAxis_eq (F : â„ â†’ â„‚) (hF : MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) F)
    {t : â„} (ht : 0 < t) :
    deriv F.resToImagAxis t = -2 * Ï€ * (D F).resToImagAxis t := by
  let z : â„ := âŸ¨I * t, by simp [ht]âŸ©
  let g : â„ â†’ â„‚ := (I * Â·)
  have h_eq : F.resToImagAxis =á¶ [nhds t] ((F âˆ˜ ofComplex) âˆ˜ g) := by
    filter_upwards [lt_mem_nhds ht] with s hs
    have him : 0 < (g s).im := by simp [g, hs]
    simp [Function.resToImagAxis_apply, ResToImagAxis, hs, Function.comp_apply, g,
      ofComplex_apply_of_im_pos him]
  rw [h_eq.deriv_eq]
  have hg : HasDerivAt g I t := by simpa using ofRealCLM.hasDerivAt.const_mul I
  have hF' := (MDifferentiableAt_DifferentiableAt (hF z)).hasDerivAt
  rw [(hF'.scomp t hg).deriv]
  have hD : deriv (F âˆ˜ ofComplex) z = 2 * Ï€ * I * D F z := by simp only [D]; field_simp
  simp only [hD, Function.resToImagAxis_apply, ResToImagAxis, dif_pos ht, z, smul_eq_mul]
  ring_nf; simp only [I_sq]; ring

/--
If $F$ is a modular form where $F(it)$ is positive for sufficiently large $t$ (i.e. constant term
is positive) and the derivative is positive, then $F$ is also positive.
-/
theorem antiDerPos {F : â„ â†’ â„‚} {k : â„¤} (hF : ResToImagAxis.EventuallyPos F)
    (hDF : ResToImagAxis.Pos (D F)) : ResToImagAxis.Pos F := by
  sorry

/--
Let $F : \mathbb{H} \to \mathbb{C}$ be a holomorphic function where $F(it)$ is real for all $t > 0$.
Assume that Serre derivative $\partial_k F$ is positive on the imaginary axis.
If $F(it)$ is positive for sufficiently large $t$, then $F(it)$ is positive for all $t > 0$.
-/
theorem antiSerreDerPos {F : â„ â†’ â„‚} {k : â„¤} (hSDF : ResToImagAxis.Pos (serre_D k F))
    (hF : ResToImagAxis.EventuallyPos F) : ResToImagAxis.Pos F := by
  sorry
