module
public import Mathlib.Data.Rat.Star
public import Mathlib.LinearAlgebra.Dimension.Localization
public import Mathlib.LinearAlgebra.Dimension.Free
public import Mathlib.NumberTheory.ModularForms.LevelOne
public import Mathlib.Algebra.Group.Int.Even
public import SpherePacking.ModularForms.EisensteinBase
public import SpherePacking.ModularForms.DimGenCongLevels.NormTransfer
public import SpherePacking.ModularForms.CuspFormIsoModforms
public import SpherePacking.ModularForms.Eisenstein

/-!
# Dimension Formulas

This file proves identities for `Delta` in terms of `E‚ÇÑ`/`E‚ÇÜ` and derives rank/dimension statements
for level-one modular forms.
-/

open scoped Interval Real NNReal ENNReal Topology BigOperators Nat
  Real MatrixGroups CongruenceSubgroup ArithmeticFunction.sigma

open ModularForm EisensteinSeries UpperHalfPlane TopologicalSpace Set MeasureTheory intervalIntegral
  Metric Filter Function Complex MatrixGroups SlashInvariantFormClass ModularFormClass

noncomputable section

def mul_Delta_map (k : ‚Ñ§) (f : ModularForm (CongruenceSubgroup.Gamma 1) (k - 12)) :
    ModularForm (CongruenceSubgroup.Gamma 1) k := by
  have := (f.mul (ModForm_mk _ 12 Delta))
  have hk : k - 12 + 12 = k := by ring
  exact ModularForm.mcast hk this

lemma mcast_apply {a b : ‚Ñ§} {Œì : Subgroup SL(2, ‚Ñ§)} (h : a = b) (f : ModularForm Œì a) (z : ‚Ñç) :
  (ModularForm.mcast h f) z = f z := by
  rfl

lemma mul_Delta_map_eq (k : ‚Ñ§) (f : ModularForm (CongruenceSubgroup.Gamma 1) (k - 12)) (z : ‚Ñç) :
  (mul_Delta_map k f) z = f z * Delta z := by
  rw [mul_Delta_map, mcast_apply ]
  rfl

lemma mul_Delta_map_eq_mul (k : ‚Ñ§) (f : ModularForm (CongruenceSubgroup.Gamma 1) (k - 12)) :
  ((mul_Delta_map k f) : ‚Ñç ‚Üí ‚ÑÇ) = (f.mul (ModForm_mk _ 12 Delta)) := by
  ext z
  rw [mul_Delta_map, mcast_apply ]

/-
lemma mul_Delta_IsCuspForm (k : ‚Ñ§) (f : ModularForm (CongruenceSubgroup.Gamma 1) (k - 12)) :
  IsCuspForm (CongruenceSubgroup.Gamma 1) k (mul_Delta_map k f) := by
  rw [IsCuspForm_iff_coeffZero_eq_zero]
  rw [qExpansion_ext2 _ _ (mul_Delta_map_eq_mul k f)]
  rw [‚Üê Nat.cast_one (R := ‚Ñù), qExpansion_mul_coeff_zero]
  simp only [mul_eq_zero]
  right
  rw [Nat.cast_one, ‚Üê IsCuspForm_iff_coeffZero_eq_zero]
  rw [IsCuspForm, CuspFormSubmodule, CuspForm_to_ModularForm]
  simp

def Modform_mul_Delta' (k : ‚Ñ§) (f : ModularForm (CongruenceSubgroup.Gamma 1) (k - 12)) :
    CuspForm (CongruenceSubgroup.Gamma 1) k :=
  IsCuspForm_to_CuspForm _ k (mul_Delta_map k f) (mul_Delta_IsCuspForm k f)

theorem mul_apply {k‚ÇÅ k‚ÇÇ : ‚Ñ§} {Œì : Subgroup SL(2, ‚Ñ§)} (f : SlashInvariantForm Œì k‚ÇÅ)
    (g : SlashInvariantForm Œì k‚ÇÇ) (z : ‚Ñç) : (f.mul g) z = f z * g z := rfl

lemma Modform_mul_Delta_apply (k : ‚Ñ§) (f : ModularForm (CongruenceSubgroup.Gamma 1) (k - 12))
    (z : ‚Ñç) : (Modform_mul_Delta' k f) z = f z * Delta z := by
  rw [Modform_mul_Delta']
  have := congr_fun
    (CuspForm_to_ModularForm_Fun_coe _ _ (mul_Delta_map k f) (mul_Delta_IsCuspForm k f)) z
  simp at *
  rw [mul_Delta_map_eq] at this
  exact this

def CuspForms_iso_Modforms (k : ‚Ñ§) : CuspForm (CongruenceSubgroup.Gamma 1) k ‚âÉ‚Çó[‚ÑÇ]
    ModularForm (CongruenceSubgroup.Gamma 1) (k - 12) where
      toFun f := CuspForm_div_Discriminant k f
      map_add' a b := CuspForm_div_Discriminant_Add k a b
      map_smul' := by
        intro m a
        ext z
        simp only [CuspForm_div_Discriminant_apply, CuspForm.IsGLPos.smul_apply, smul_eq_mul,
          RingHom.id_apply, IsGLPos.smul_apply]
        exact mul_div_assoc m (a z) (Œî z)
      invFun := Modform_mul_Delta' k
      left_inv := by
        intro f
        ext z
        simp [Modform_mul_Delta_apply, CuspForm_div_Discriminant_apply ]
        rw [Delta_apply]
        rw [div_mul_cancel‚ÇÄ ]
        apply Œî_ne_zero
      right_inv := by
        intro f
        ext z
        simp [Modform_mul_Delta_apply, CuspForm_div_Discriminant_apply ]
        rw [Delta_apply]
        rw [mul_div_cancel_right‚ÇÄ]
        apply Œî_ne_zero
-/

lemma delta_eq_E4E6_const : ‚àÉ (c : ‚ÑÇ), (c ‚Ä¢ Delta) = Delta_E4_E6_aux := by
  have hr : Module.finrank ‚ÑÇ (CuspForm Œì(1) 12) = 1 :=
    Module.finrank_eq_of_rank_eq <| by
      simpa [LinearEquiv.rank_eq (CuspForms_iso_Modforms 12)] using
        ModularForm.levelOne_weight_zero_rank_one
  exact (finrank_eq_one_iff_of_nonzero' Delta Delta_ne_zero).1 hr Delta_E4_E6_aux

lemma cuspform_weight_lt_12_zero (k : ‚Ñ§) (hk : k < 12) : Module.rank ‚ÑÇ (CuspForm Œì(1) k) = 0 := by
  have := CuspForms_iso_Modforms k
  --apply Module.finrank_eq_of_rank_eq
  rw [LinearEquiv.rank_eq this]
  apply ModularForm.levelOne_neg_weight_rank_zero
  linarith

/-
lemma IsCuspForm_weight_lt_eq_zero (k : ‚Ñ§) (hk : k < 12) (f : ModularForm Œì(1) k)
    (hf : IsCuspForm Œì(1) k f) : f = 0 := by
  have hfc2:= CuspForm_to_ModularForm_coe _ _ f hf
  ext z
  simp only [ModularForm.zero_apply] at *
  have hy := congr_arg (fun x ‚Ü¶ x.1) hfc2
  have hz := congr_fun hy z
  simp only [SlashInvariantForm.toFun_eq_coe, CuspForm.toSlashInvariantForm_coe,
  toSlashInvariantForm_coe] at hz
  rw [‚Üê hz]
  have := rank_zero_iff_forall_zero.mp (cuspform_weight_lt_12_zero k hk)
    (IsCuspForm_to_CuspForm Œì(1) k f hf)
  rw [this]
  simp only [CuspForm.zero_apply]
-/

/-- The discriminant cusp form as a scaled version of `E‚ÇÑ^3 - E‚ÇÜ^2`. -/
public lemma Delta_E4_E6_eq : ModForm_mk _ _ Delta_E4_E6_aux =
  ((1/ 1728 : ‚ÑÇ) ‚Ä¢ (((DirectSum.of _ 4 E‚ÇÑ)^3 - (DirectSum.of _ 6 E‚ÇÜ)^2) 12 )) := by
  ext z
  simpa [ModForm_mk, Delta_E4_E6_aux, one_div, IsGLPos.smul_apply, sub_apply, smul_eq_mul] using
    congrArg (fun F => (F z : ‚ÑÇ))
      (CuspForm_to_ModularForm_Fun_coe _ _
        ((1 / 1728 : ‚ÑÇ) ‚Ä¢
          (((DirectSum.of _ 4 E‚ÇÑ) ^ 3 - (DirectSum.of _ 6 E‚ÇÜ) ^ 2) 12)) (by
          rw [IsCuspForm_iff_coeffZero_eq_zero]
          exact E4E6_coeff_zero_eq_zero))

private lemma qExpansion_Delta_E4_E6_aux_eq :
    qExpansion 1 Delta_E4_E6_aux = qExpansion 1 (ModForm_mk Œì(1) 12 Delta_E4_E6_aux) := by
  simpa [ModForm_mk] using qExpansion_ext2 Delta_E4_E6_aux
    (ModForm_mk Œì(1) 12 Delta_E4_E6_aux) rfl

lemma Delta_E4_E6_aux_q_one_term : (qExpansion 1 Delta_E4_E6_aux).coeff 1 = 1 := by
  rw [qExpansion_Delta_E4_E6_aux_eq, Delta_E4_E6_eq]
  -- Coefficient `q` of `E‚ÇÑ^3 - E‚ÇÜ^2` is `1728`, so scaling by `1/1728` gives `1`.
  simp only [one_div, DirectSum.sub_apply, ModularForm.IsGLPos.coe_smul, ModularForm.coe_sub]
  have hsmul :=
    (by
      simpa using qExpansion_smul2 (n := 1) (a := (1728‚Åª¬π : ‚ÑÇ))
        (f := (((DirectSum.of (ModularForm Œì(1)) 4) E‚ÇÑ) ^ 3) 12 -
          (((DirectSum.of (ModularForm Œì(1)) 6) E‚ÇÜ) ^ 2) 12))
  rw [‚Üê hsmul]
  simp only [qExpansion_sub1, map_smul, map_sub, smul_eq_mul]
  have h4 := qExpansion_pow E‚ÇÑ 3
  have h6 := qExpansion_pow E‚ÇÜ 2
  simp only [Nat.cast_ofNat, Int.reduceMul] at h4 h6
  rw [h4, h6]
  have hE4c : PowerSeries.constantCoeff (qExpansion 1 E‚ÇÑ) = (1 : ‚ÑÇ) := by
    simpa [PowerSeries.coeff_zero_eq_constantCoeff_apply] using
      (E4_q_exp_zero : (qExpansion 1 E‚ÇÑ).coeff 0 = 1)
  have hE6c : PowerSeries.constantCoeff (qExpansion 1 E‚ÇÜ) = (1 : ‚ÑÇ) := by
    simpa [PowerSeries.coeff_zero_eq_constantCoeff_apply] using
      (E6_q_exp_zero : (qExpansion 1 E‚ÇÜ).coeff 0 = 1)
  have hcoeff :
      (((qExpansion 1 E‚ÇÑ) ^ 3 - (qExpansion 1 E‚ÇÜ) ^ 2) : PowerSeries ‚ÑÇ).coeff 1 = 1728 := by
    simp [map_sub, PowerSeries.coeff_one_pow, hE4c, hE6c, E4_q_exp_one, E6_q_exp_one]
    norm_num
  have h1728 : (1728 : ‚ÑÇ) ‚â† 0 := by norm_num
  have hcoeff' :
      ((qExpansion 1 E‚ÇÑ ^ 3).coeff 1 - (qExpansion 1 E‚ÇÜ ^ 2).coeff 1) = (1728 : ‚ÑÇ) := by
    simpa [map_sub] using hcoeff
  rw [hcoeff']
  simp [h1728]

/-- Identify `Delta` with the auxiliary cusp form `Delta_E4_E6_aux`. -/
public theorem Delta_E4_eqn : Delta = Delta_E4_E6_aux := by
  ext z
  obtain ‚ü®c, H‚ü© := delta_eq_E4E6_const
  have h1 := Delta_q_one_term
  have h2 := Delta_E4_E6_aux_q_one_term
  have hc : c = 1 := by
    have hsmul : (qExpansion 1 (c ‚Ä¢ Delta)).coeff 1 = c * (qExpansion 1 Delta).coeff 1 := by
      simpa [smul_eq_mul, CuspForm.coe_smul] using
        congrArg (fun p : PowerSeries ‚ÑÇ => p.coeff 1)
          (by simpa using (qExpansion_smul2 (n := 1) (a := c)
            (f := ModForm_mk (CongruenceSubgroup.Gamma 1) 12 Delta)).symm)
    -- Compare `q`-coefficients at `1`.
    have h2' : (qExpansion 1 (c ‚Ä¢ Delta)).coeff 1 = 1 := by simpa [‚Üê H] using h2
    have : c * (qExpansion 1 Delta).coeff 1 = 1 := by simpa [hsmul] using h2'
    simpa [h1] using this
  simpa [hc] using congrArg (fun f => (f z : ‚ÑÇ)) H

/-- The pointwise formula `Delta(z) = (1/1728) * (E‚ÇÑ(z)^3 - E‚ÇÜ(z)^2)`. -/
public lemma Delta_apply_eq_one_div_1728_mul_E4_pow_three_sub_E6_sq (z : ‚Ñç) :
    (Delta z : ‚ÑÇ) = (1 / 1728 : ‚ÑÇ) * ((E‚ÇÑ z) ^ (3 : ‚Ñï) - (E‚ÇÜ z) ^ (2 : ‚Ñï)) := by
  have hŒî : (Delta z : ‚ÑÇ) = (Delta_E4_E6_aux z : ‚ÑÇ) := by
    simpa using congrArg (fun f => (f z : ‚ÑÇ)) Delta_E4_eqn
  have hE : (Delta_E4_E6_aux z : ‚ÑÇ) =
      (1 / 1728 : ‚ÑÇ) * ((E‚ÇÑ z) ^ (3 : ‚Ñï) - (E‚ÇÜ z) ^ (2 : ‚Ñï)) := by
    have h := congrArg (fun F : ModularForm Œì(1) 12 => (F z : ‚ÑÇ)) Delta_E4_E6_eq
    simpa [ModForm_mk, one_div, IsGLPos.smul_apply, sub_apply, DirectSum.sub_apply, smul_eq_mul,
      pow_two, pow_three, DirectSum.of_mul_of, mul_assoc] using h
  exact hŒî.trans hE

/-- The second `q`-coefficient of `Delta` is `-24`. -/
public lemma Delta_q_exp_two : (qExpansion 1 Delta).coeff 2 = (-24 : ‚ÑÇ) := by
  rw [Delta_E4_eqn]
  rw [qExpansion_Delta_E4_E6_aux_eq, Delta_E4_E6_eq]
  simp only [one_div, DirectSum.sub_apply, ModularForm.IsGLPos.coe_smul, ModularForm.coe_sub]
  have hsmul :=
    (by
      simpa using qExpansion_smul2 (n := 1) (a := (1728‚Åª¬π : ‚ÑÇ))
        (f := (((DirectSum.of (ModularForm Œì(1)) 4) E‚ÇÑ) ^ 3) 12 -
          (((DirectSum.of (ModularForm Œì(1)) 6) E‚ÇÜ) ^ 2) 12))
  rw [‚Üê hsmul]
  simp only [qExpansion_sub1, map_smul, map_sub, smul_eq_mul]
  have h4 := qExpansion_pow E‚ÇÑ 3
  have h6 := qExpansion_pow E‚ÇÜ 2
  simp only [Nat.cast_ofNat, Int.reduceMul] at h4 h6
  rw [h4, h6]
  have hœÉ3 : (œÉ 3 2 : ‚Ñï) = 9 := by decide
  have hœÉ5 : (œÉ 5 2 : ‚Ñï) = 33 := by decide
  have hE4_2 : (qExpansion 1 E‚ÇÑ).coeff 2 = (240 : ‚ÑÇ) * (9 : ‚ÑÇ) := by
    simpa [hœÉ3] using congr_fun E4_q_exp 2
  have hE6_2 : (qExpansion 1 E‚ÇÜ).coeff 2 = (-(504 : ‚ÑÇ)) * (33 : ‚ÑÇ) := by
    simpa [hœÉ5] using congr_fun E6_q_exp 2
  have hanti2 : Finset.antidiagonal 2 = {(0, 2), (1, 1), (2, 0)} := by decide
  suffices h :
      1728‚Åª¬π *
          (240 * 9 + (240 * 240 + 240 * 9) +
                (240 *
                      (‚àë p ‚àà Finset.antidiagonal 1,
                          (qExpansion 1 E‚ÇÑ).coeff p.1 * (qExpansion 1 E‚ÇÑ).coeff p.2) +
                    240 * 9) -
              (-(504 * 33) + (504 * 504 + -(504 * 33)))) = (-24 : ‚ÑÇ) by
    simpa [pow_three, pow_two, PowerSeries.coeff_mul, hanti2, E4_q_exp_zero, E4_q_exp_one, hE4_2,
      E6_q_exp_zero, E6_q_exp_one, hE6_2] using h
  have hanti1 : Finset.antidiagonal 1 = {(0, 1), (1, 0)} := by decide
  have hs :
      (‚àë x ‚àà Finset.antidiagonal 1, (qExpansion 1 E‚ÇÑ).coeff x.1 * (qExpansion 1 E‚ÇÑ).coeff x.2) =
        (480 : ‚ÑÇ) := by
    rw [hanti1]
    suffices h : (240 : ‚ÑÇ) + 240 = 480 by
      simpa [E4_q_exp_zero, E4_q_exp_one] using h
    norm_num
  simp [hs]
  have h1728 : (1728 : ‚ÑÇ) ‚â† 0 := by norm_num
  field_simp [h1728]
  norm_num

private lemma qExpansion_coeff_zero_ne_zero_of_not_cusp {k : ‚Ñ§} (f : ModularForm Œì(1) k)
    (hf : ¬¨ IsCuspForm Œì(1) k f) : (qExpansion 1 f).coeff 0 ‚â† 0 := by
  exact fun h0 => hf ((IsCuspForm_iff_coeffZero_eq_zero k f).2 h0)

private lemma isCuspForm_sub_normalise {k : ‚Ñ§} (E f : ModularForm Œì(1) k)
    (hf : ¬¨ IsCuspForm Œì(1) k f) (hE0 : (qExpansion 1 E).coeff 0 = 1) :
    IsCuspForm Œì(1) k (E - ((qExpansion 1 f).coeff 0)‚Åª¬π ‚Ä¢ f) := by
  rw [IsCuspForm_iff_coeffZero_eq_zero]
  have hnorm : (qExpansion 1 (((qExpansion 1 f).coeff 0)‚Åª¬π ‚Ä¢ f)).coeff 0 = 1 :=
    modularForm_normalise (k := k) f hf
  have hE0' : PowerSeries.constantCoeff (qExpansion 1 E) = 1 := by simpa using hE0
  have hnorm' :
      PowerSeries.constantCoeff
          (qExpansion 1 ((PowerSeries.constantCoeff (qExpansion 1 f))‚Åª¬π ‚Ä¢ f)) = 1 := by
    simpa using hnorm
  simpa [map_sub, hE0', hnorm'] using
    congrArg (fun p : PowerSeries ‚ÑÇ => p.coeff 0)
      (qExpansion_sub1 (f := E) (g := ((qExpansion 1 f).coeff 0)‚Åª¬π ‚Ä¢ f))

private lemma weight_lt_twelve_one_dimensional {k : ‚Ñ§} (E : ModularForm Œì(1) k)
    (hEne : E ‚â† 0) (hE0 : (qExpansion 1 E).coeff 0 = 1) (hk : k < 12) :
    Module.rank ‚ÑÇ (ModularForm Œì(1) k) = 1 := by
  rw [rank_eq_one_iff]
  refine ‚ü®E, hEne, fun f => ?_‚ü©
  by_cases hf : IsCuspForm Œì(1) k f
  ¬∑ refine ‚ü®0, ?_‚ü©
    simp [IsCuspForm_weight_lt_eq_zero k (by simpa using hk) f hf]
  ¬∑ set c : ‚ÑÇ := (qExpansion 1 f).coeff 0
    have hc : c ‚â† 0 := by
      simpa [c] using qExpansion_coeff_zero_ne_zero_of_not_cusp (k := k) f hf
    have hcusp : IsCuspForm Œì(1) k (E - c‚Åª¬π ‚Ä¢ f) := by
      simpa [c] using isCuspForm_sub_normalise (k := k) (E := E) (f := f) hf hE0
    have hEq : E = c‚Åª¬π ‚Ä¢ f := by
      simpa [sub_eq_zero] using
        IsCuspForm_weight_lt_eq_zero k (by simpa using hk) (E - c‚Åª¬π ‚Ä¢ f) hcusp
    refine ‚ü®c, by simp [hEq, smul_smul, hc]‚ü©

lemma weight_six_one_dimensional : Module.rank ‚ÑÇ (ModularForm Œì(1) 6) = 1 := by
  simpa using
    weight_lt_twelve_one_dimensional (k := 6) (E := E‚ÇÜ) E6_ne_zero E6_q_exp_zero (by norm_num)

lemma weight_four_one_dimensional : Module.rank ‚ÑÇ (ModularForm Œì(1) 4) = 1 := by
  simpa using
    weight_lt_twelve_one_dimensional (k := 4) (E := E‚ÇÑ) E4_ne_zero E4_q_exp_zero (by norm_num)

lemma weight_eight_one_dimensional (k : ‚Ñï) (hk : 3 ‚â§ (k : ‚Ñ§)) (hk2 : Even k) (hk3 : k < 12) :
    Module.rank ‚ÑÇ (ModularForm Œì(1) k) = 1 := by
  simpa using
    weight_lt_twelve_one_dimensional (k := (k : ‚Ñ§)) (E := E k hk) (Ek_ne_zero k hk hk2)
      (Ek_q_exp_zero k hk hk2) (by simpa using hk3)

lemma weight_two_zero (f : ModularForm (CongruenceSubgroup.Gamma 1) 2) : f = 0 := by
-- Let `a` be the constant term; then `f^2 = a^2 E‚ÇÑ` and `f^3 = a^3 E‚ÇÜ`, forcing `a = 0` or `Œî = 0`.
  by_cases hf : IsCuspForm (CongruenceSubgroup.Gamma 1) 2 f
  ¬∑ exact IsCuspForm_weight_lt_eq_zero 2 (by norm_num) f hf
  ¬∑ have hc1 : (qExpansion 1 f).coeff 0 ‚â† 0 :=
      fun h => hf ((IsCuspForm_iff_coeffZero_eq_zero 2 f).2 h)
    set a : ‚ÑÇ := (qExpansion 1 f).coeff 0 with ha_def
    have ha : a ‚â† 0 := by simpa [ha_def] using hc1
    have r6 : ‚àÄ g : ModularForm Œì(1) 6, ‚àÉ c : ‚ÑÇ, c ‚Ä¢ E‚ÇÜ = g :=
      (finrank_eq_one_iff_of_nonzero' E‚ÇÜ E6_ne_zero).1
        ((Module.rank_eq_one_iff_finrank_eq_one).1 weight_six_one_dimensional)
    rcases r6 ((f.mul f).mul f) with ‚ü®c6, hc6‚ü©
    have hc6e : c6 = a ^ 3 := by
      have := qExpansion_mul_coeff 1 4 2 (f.mul f) f
      rw [‚Üê hc6] at this; simp only [ModularForm.IsGLPos.coe_smul] at this
      rw [‚Üê qExpansion_smul2 1 c6, qExpansion_mul_coeff 1 2 2 f f] at this
      have hh := congr_arg (fun x => x.coeff 0) this
      simp only [map_smul, smul_eq_mul] at hh
      rw [Nat.cast_one, E6_q_exp_zero] at hh
      simp only [PowerSeries.coeff_zero_eq_constantCoeff, mul_one, map_mul] at *
      simpa [a, pow_three, mul_assoc] using hh
    have r4 : ‚àÄ g : ModularForm Œì(1) 4, ‚àÉ c : ‚ÑÇ, c ‚Ä¢ E‚ÇÑ = g :=
      (finrank_eq_one_iff_of_nonzero' E‚ÇÑ E4_ne_zero).1
        ((Module.rank_eq_one_iff_finrank_eq_one).1 weight_four_one_dimensional)
    rcases r4 (f.mul f) with ‚ü®c4, hc4‚ü©
    have hc4e : c4 = a ^ 2 := by
      have := qExpansion_mul_coeff 1 2 2 f f
      rw [‚Üê hc4] at this; simp only [ModularForm.IsGLPos.coe_smul] at this
      rw [‚Üê qExpansion_smul2 1 c4] at this
      have hh := congr_arg (fun x => x.coeff 0) this
      simp only [map_smul, smul_eq_mul] at hh
      rw [Nat.cast_one, E4_q_exp_zero] at hh
      simpa [a, pow_two] using hh
    exfalso
    let F := DirectSum.of _ 2 f
    let D : ModularForm Œì(1) 12 := ModForm_mk Œì(1) 12 Delta
    have HF2 : (F^2) = c4 ‚Ä¢ (DirectSum.of _ 4 E‚ÇÑ) := by
      rw [‚Üê DirectSum.of_smul, hc4]
      dsimp [F]
      rw [pow_two, DirectSum.of_mul_of]
      rfl
    have HF3 : (F^3) = c6 ‚Ä¢ (DirectSum.of _ 6 E‚ÇÜ) := by
      rw [‚Üê DirectSum.of_smul, hc6]
      dsimp [F]
      rw [pow_three, ‚Üê mul_assoc, DirectSum.of_mul_of, DirectSum.of_mul_of]
      rfl
    have HF12 : (((F^2)^3) 12) = a ^ 6 ‚Ä¢ (E‚ÇÑ.mul (E‚ÇÑ.mul E‚ÇÑ)) := by
      rw [HF2, pow_three]
      simp [DirectSum.smul_apply, DirectSum.of_mul_of, hc4e, smul_smul]
      ring_nf
      rfl
    have hF2 : (((F^3)^2) 12) = a ^ 6 ‚Ä¢ (E‚ÇÜ.mul E‚ÇÜ) := by
      rw [HF3, pow_two]
      simp [DirectSum.smul_apply, DirectSum.of_mul_of, hc6e, smul_smul]
      ring_nf
      rfl
    have V : (1 / 1728 : ‚ÑÇ) ‚Ä¢ ((((F^2)^3) 12) - (((F^3)^2) 12)) = a ^ 6 ‚Ä¢ D
      := by
      rw [HF12, hF2]
      simp only [one_div, Int.reduceAdd, D]
      rw [Delta_E4_eqn, Delta_E4_E6_eq, pow_two, pow_three, DirectSum.of_mul_of,
        DirectSum.of_mul_of,DirectSum.of_mul_of]
      simp only [one_div, Int.reduceAdd, DirectSum.sub_apply, DirectSum.of_eq_same]
      ext y
      simp only [IsGLPos.smul_apply, sub_apply, Int.reduceAdd, smul_eq_mul]
      ring_nf
      rfl
    have ht : (1 / 1728 : ‚ÑÇ) ‚Ä¢ ((((F^2)^3) 12) - (((F^3)^2) 12)) = 0 := by
      ext y
      simp only [one_div, IsGLPos.smul_apply, sub_apply, smul_eq_mul, zero_apply, mul_eq_zero,
        inv_eq_zero, OfNat.ofNat_ne_zero, false_or, F]
      ring_nf
    rw [ht] at V
    have hr := congrArg (fun g : ModularForm Œì(1) 12 => g UpperHalfPlane.I) V
    simp only [D, ModForm_mk, IsGLPos.smul_apply, smul_eq_mul, zero_apply, zero_eq_mul, ne_eq,
      OfNat.ofNat_ne_zero, not_false_eq_true, pow_eq_zero_iff] at hr
    rcases hr with h | h
    ¬∑ exact ha h
    ¬∑ have hDelta : Delta UpperHalfPlane.I ‚â† 0 := by
        simpa [Delta_apply] using Œî_ne_zero UpperHalfPlane.I
      exact hDelta h

lemma dim_modforms_eq_one_add_dim_cuspforms (k : ‚Ñï) (hk : 3 ‚â§ (k : ‚Ñ§)) (hk2 : Even k) :
    Module.rank ‚ÑÇ (ModularForm (CongruenceSubgroup.Gamma 1) k) =
    1 + Module.rank ‚ÑÇ (CuspForm (CongruenceSubgroup.Gamma 1) k) := by
    have h1 : Module.rank ‚ÑÇ (CuspFormSubmodule (CongruenceSubgroup.Gamma 1) k) =
      Module.rank ‚ÑÇ (CuspForm (CongruenceSubgroup.Gamma 1) k) := by
      apply LinearEquiv.rank_eq
      have := CuspForm_iso_CuspFormSubmodule Œì(1) k
      exact id this.symm
    rw [‚Üê h1, ‚Üê Submodule.rank_quotient_add_rank (CuspFormSubmodule (CongruenceSubgroup.Gamma 1) k)]
    congr
    rw [rank_eq_one_iff ]
    refine ‚ü®Submodule.Quotient.mk (E k (by linarith)), ?_, ?_‚ü©
    ¬∑ intro hq
      rw [Submodule.Quotient.mk_eq_zero] at hq
      have := IsCuspForm_iff_coeffZero_eq_zero k (E k (by linarith))
      rw [IsCuspForm] at this
      rw [this, Ek_q_exp_zero k hk hk2] at hq
      aesop
    intro v
    rcases Quotient.exists_rep v with ‚ü®f, rfl‚ü©
    refine ‚ü®(qExpansion 1 f).coeff 0, ?_‚ü©
    rw [‚Üê Submodule.Quotient.mk_smul]
    change
      Submodule.Quotient.mk ((qExpansion 1 f).coeff 0 ‚Ä¢ E k (by linarith)) =
        Submodule.Quotient.mk f
    rw [Submodule.Quotient.eq, CuspFormSubmodule_mem_iff_coeffZero_eq_zero]
    -- Reduce to the constant coefficient computation.
    simp only [ModularForm.coe_sub]
    rw [qExpansion_sub1]
    simp only [ModularForm.IsGLPos.coe_smul]
    rw [‚Üê Nat.cast_one (R := ‚Ñù), ‚Üê qExpansion_smul2, Nat.cast_one]
    have hc := Ek_q_exp_zero k hk hk2
    simp only [PowerSeries.coeff_zero_eq_constantCoeff, map_sub, PowerSeries.constantCoeff_smul,
      smul_eq_mul] at *
    rw [hc]
    ring

theorem dim_weight_two : Module.rank ‚ÑÇ (ModularForm Œì(1) ‚Üë2) = 0 := by
  rw [@rank_zero_iff_forall_zero]
  exact weight_two_zero

lemma floor_lem1 (k a : ‚Ñö) (ha : 0 < a) (hak : a ‚â§ k) :
    1 + Nat.floor ((k - a) / a) = Nat.floor (k / a) := by
  rw [div_sub_same (Ne.symm (ne_of_lt ha))]
  have := Nat.floor_sub_one (k/a)
  norm_cast at *
  rw [this]
  refine Nat.add_sub_cancel' (Nat.le_floor <| (le_div_iff‚ÇÄ ha).2 ?_)
  simpa using hak

public lemma ModularForm.dimension_level_one (k : ‚Ñï) (hk : 3 ‚â§ (k : ‚Ñ§)) (hk2 : Even k) :
    Module.rank ‚ÑÇ (ModularForm (CongruenceSubgroup.Gamma 1) (k)) = if 12 ‚à£ ((k) : ‚Ñ§) - 2 then
    Nat.floor ((k : ‚Ñö)/ 12) else Nat.floor ((k : ‚Ñö) / 12) + 1 := by
  induction k using Nat.strong_induction_on with | _ k ihn =>
  rw [dim_modforms_eq_one_add_dim_cuspforms (k) (by omega) hk2 ,
    LinearEquiv.rank_eq (CuspForms_iso_Modforms (k : ‚Ñ§))]
  by_cases HK : (3 : ‚Ñ§) ‚â§ (((k : ‚Ñ§) - 12))
  ¬∑ have iH := ihn (k - 12) (by omega) ?_ ?_
    ¬∑ have hk12 : (((k - 12) : ‚Ñï) : ‚Ñ§) = k - 12 := by
        norm_cast
        refine Eq.symm (Int.subNatNat_of_le ?_)
        omega
      rw [hk12] at iH
      have : ((k - 12) : ‚Ñï) = (k : ‚Ñö) - 12 := by
        norm_cast
      rw [iH, this]
      by_cases h12 : 12 ‚à£ ((k) : ‚Ñ§) - 2
      ¬∑ have h12k : 12 ‚à£ (k : ‚Ñ§) -12 - 2 := by
          omega
        simp only [h12k, ‚ÜìreduceIte, h12]
        have := floor_lem1 k 12 (by norm_num)
        norm_cast at *
        apply this
        omega
      ¬∑ have h12k : ¬¨ 12 ‚à£ (k : ‚Ñ§) -12 - 2 := by
          omega
        simp only [h12k, ‚ÜìreduceIte, Nat.cast_add, Nat.cast_one, h12]
        have := floor_lem1 k 12 (by norm_num)
        norm_cast at *
        rw [‚Üê add_assoc, this]
        omega
    ¬∑ omega
    ¬∑ refine (Nat.even_sub ?_).mpr ?_
      ¬∑ omega
      simp only [hk2, true_iff]
      decide
  ¬∑ simp only [not_le] at HK
    have hkop : k ‚àà Finset.filter Even (Finset.Icc 3 14) := by
      simp only [Finset.mem_filter, Finset.mem_Icc, hk2, and_true]
      omega
    have : Finset.filter Even (Finset.Icc 3 14) = ({4,6,8,10,12, 14} : Finset ‚Ñï) := by
        decide
    rw [this] at hkop
    fin_cases hkop
    ¬∑ simp only [Nat.cast_ofNat, Int.reduceSub, Int.reduceNeg, Nat.cast_ite]
      have h8 : -8 < 0 := by norm_num
      rw [ModularForm.levelOne_neg_weight_rank_zero h8]
      norm_cast
    ¬∑ simp only [Nat.cast_ofNat, Int.reduceSub, Int.reduceNeg, Nat.cast_ite]
      have h8 : -6 < 0 := by norm_num
      rw [ModularForm.levelOne_neg_weight_rank_zero h8]
      norm_cast
    ¬∑ simp only [Nat.cast_ofNat, Int.reduceSub, Int.reduceNeg, Nat.cast_ite]
      have h8 : -4 < 0 := by norm_num
      rw [ModularForm.levelOne_neg_weight_rank_zero h8]
      norm_cast
    ¬∑ simp only [Nat.cast_ofNat, Int.reduceSub, Int.reduceNeg, Nat.cast_ite]
      have h8 : -2 < 0 := by norm_num
      rw [ModularForm.levelOne_neg_weight_rank_zero h8]
      norm_cast
    ¬∑ simp only [Nat.cast_ofNat, Int.reduceSub, ne_eq, OfNat.ofNat_ne_zero, not_false_eq_true,
      div_self, Nat.floor_one, Nat.reduceAdd, Nat.cast_ite, Nat.cast_one]
      rw [ModularForm.levelOne_weight_zero_rank_one]
      norm_cast
    ¬∑ simp only [Nat.cast_ofNat, Int.reduceSub, dim_weight_two, add_zero, dvd_refl, ‚ÜìreduceIte]
      norm_cast

lemma finiteDimensional_of_subsingleton (V : Type*) [AddCommGroup V] [Module ‚ÑÇ V] [Subsingleton V] :
    FiniteDimensional ‚ÑÇ V := by
  refine (Module.finite_def).2 ?_
  simpa [Submodule.eq_bot_of_subsingleton (p := (‚ä§ : Submodule ‚ÑÇ V))] using
    (Submodule.fg_bot : (‚ä• : Submodule ‚ÑÇ V).FG)

lemma finiteDimensional_of_rank_lt_aleph0 (V : Type*) [AddCommGroup V] [Module ‚ÑÇ V]
    (h : Module.rank ‚ÑÇ V < Cardinal.aleph0) : FiniteDimensional ‚ÑÇ V := by
  haveI : Module.Free ‚ÑÇ V := by infer_instance
  simpa using (Module.rank_lt_aleph0_iff (R := ‚ÑÇ) (M := V)).1 h

/-- Level-one modular forms of odd weight are identically zero, via invariance under `-I`. -/
lemma ModularForm.levelOne_eq_zero_of_odd_weight {k : ‚Ñ§} (hk : Odd k) (f : ModularForm Œì(1) k) :
    f = 0 := by
  ext z
  have h' : f z = -f z := by
    have h : f z = (-1 : ‚ÑÇ) ^ k * f z := by
      simpa [denom, show (-1 : SL(2, ‚Ñ§)) ‚Ä¢ z = z by simp] using
        (SlashInvariantForm.slash_action_eqn_SL'' (f := f) (Œ≥ := (-1 : SL(2, ‚Ñ§)))
          (hŒ≥ := CongruenceSubgroup.mem_Gamma_one (-1 : SL(2, ‚Ñ§))) z)
    simpa [hk.neg_one_zpow, neg_one_mul] using h
  simpa using (CharZero.eq_neg_self_iff (a := f z)).1 h'

lemma finiteDimensional_modularForm_level_one (k : ‚Ñ§) :
    FiniteDimensional ‚ÑÇ (ModularForm Œì(1) k) := by
  by_cases hkneg : k < 0
  ¬∑ have hr : Module.rank ‚ÑÇ (ModularForm Œì(1) k) = 0 :=
      ModularForm.levelOne_neg_weight_rank_zero (k := k) hkneg
    have hr' : Module.rank ‚ÑÇ (ModularForm Œì(1) k) < Cardinal.aleph0 := by
      simpa [hr] using Cardinal.aleph0_pos
    exact finiteDimensional_of_rank_lt_aleph0 (V := ModularForm Œì(1) k) hr'
  ¬∑ have hk0le : 0 ‚â§ k := le_of_not_gt hkneg
    by_cases hk0 : k = 0
    ¬∑ subst hk0
      refine finiteDimensional_of_rank_lt_aleph0 (V := ModularForm Œì(1) (0 : ‚Ñ§)) ?_
      simp [ModularForm.levelOne_weight_zero_rank_one, Cardinal.one_lt_aleph0]
    ¬∑ have hkpos : 0 < k := lt_of_le_of_ne hk0le (Ne.symm hk0)
      rcases Int.even_or_odd k with hk2 | hk2
      ¬∑ set kN : ‚Ñï := Int.toNat k
        have hkNat : (kN : ‚Ñ§) = k := by
          simpa [kN] using (Int.toNat_of_nonneg hk0le)
        have hk2Nat : Even (Int.toNat k) := by
          have : Even (kN : ‚Ñ§) := by simpa [hkNat, kN] using hk2
          simpa [kN] using (Int.even_coe_nat kN).1 this
        by_cases hk2' : k = 2
        ¬∑ subst hk2'
          have hr : Module.rank ‚ÑÇ (ModularForm Œì(1) (2 : ‚Ñ§)) = 0 := by
            simpa using dim_weight_two
          refine finiteDimensional_of_rank_lt_aleph0 (V := ModularForm Œì(1) (2 : ‚Ñ§)) ?_
          simpa [hr] using (Cardinal.natCast_lt_aleph0 (n := 0))
        ¬∑ have hkNat_ge3 : (3 : ‚Ñ§) ‚â§ (Int.toNat k : ‚Ñ§) := by
            rcases hk2 with ‚ü®t, rfl‚ü©
            have htpos : 0 < t := by
              have : 0 < t + t := by simpa using hkpos
              linarith
            have htne1 : t ‚â† 1 := by
              intro ht1
              apply hk2'
              simp [ht1]
            have htge2 : (2 : ‚Ñ§) ‚â§ t := by
              have htge1 : (1 : ‚Ñ§) ‚â§ t := by omega
              have htgt1 : (1 : ‚Ñ§) < t := lt_of_le_of_ne htge1 (Ne.symm htne1)
              omega
            have hkge4 : (4 : ‚Ñ§) ‚â§ t + t := by linarith
            have hkge3 : (3 : ‚Ñ§) ‚â§ t + t := le_trans (by norm_num) hkge4
            have hkNat' : (kN : ‚Ñ§) = (t + t) := by
              have hnn : 0 ‚â§ t + t := le_of_lt (show 0 < t + t from by linarith [htpos])
              simp [kN, Int.toNat_of_nonneg hnn]
            simpa [hkNat', kN] using hkge3
          have hr :
              Module.rank ‚ÑÇ (ModularForm (CongruenceSubgroup.Gamma 1) kN) =
                if 12 ‚à£ ((kN : ‚Ñ§) - 2) then Nat.floor ((kN : ‚Ñö) / 12) else
                  Nat.floor ((kN : ‚Ñö) / 12) + 1 := by
            simpa [kN] using ModularForm.dimension_level_one (k := kN) hkNat_ge3 hk2Nat
          have hr' :
              Module.rank ‚ÑÇ (ModularForm (CongruenceSubgroup.Gamma 1) kN) < Cardinal.aleph0 := by
            by_cases hdiv : 12 ‚à£ ((kN : ‚Ñ§) - 2)
            ¬∑ simp [hr, hdiv]
            ¬∑ simpa [hr, hdiv] using
                (Cardinal.add_lt_aleph0
                  (Cardinal.natCast_lt_aleph0 (n := Nat.floor ((kN : ‚Ñö) / 12)))
                  Cardinal.one_lt_aleph0)
          haveI : FiniteDimensional ‚ÑÇ (ModularForm (CongruenceSubgroup.Gamma 1) (kN : ‚Ñ§)) :=
            finiteDimensional_of_rank_lt_aleph0
              (V := ModularForm (CongruenceSubgroup.Gamma 1) (kN : ‚Ñ§)) hr'
          haveI : FiniteDimensional ‚ÑÇ (ModularForm Œì(1) (kN : ‚Ñ§)) := by infer_instance
          exact hkNat ‚ñ∏ (show FiniteDimensional ‚ÑÇ (ModularForm Œì(1) (kN : ‚Ñ§)) by infer_instance)
      ¬∑ have hz : ‚àÄ f : ModularForm Œì(1) k, f = 0 := fun f =>
          ModularForm.levelOne_eq_zero_of_odd_weight (k := k) hk2 f
        haveI : Subsingleton (ModularForm Œì(1) k) := by
          refine ‚ü®fun f g => ?_‚ü©
          calc
            f = 0 := hz f
            _ = g := by simp [hz g]
        exact finiteDimensional_of_subsingleton (V := ModularForm Œì(1) k)

lemma finiteDimensional_modularForm_congr {k : ‚Ñ§} {H K : Subgroup (GL (Fin 2) ‚Ñù)}
    (h : H = K) [H.HasDetOne] [K.HasDetOne] (hH : FiniteDimensional ‚ÑÇ (ModularForm H k)) :
    FiniteDimensional ‚ÑÇ (ModularForm K k) := by
  cases h; simpa using hH

lemma finiteDimensional_modularForm_SL2Z (k : ‚Ñ§) : FiniteDimensional ‚ÑÇ (ModularForm ùíÆ‚Ñí k) := by
  let f : SL(2, ‚Ñ§) ‚Üí* GL (Fin 2) ‚Ñù := Matrix.SpecialLinearGroup.mapGL ‚Ñù
  change FiniteDimensional ‚ÑÇ (ModularForm f.range k)
  have hŒì1 : FiniteDimensional ‚ÑÇ (ModularForm (Subgroup.map f (Œì(1) : Subgroup SL(2, ‚Ñ§))) k) := by
    simpa [f] using (finiteDimensional_modularForm_level_one (k := k))
  have htop : FiniteDimensional ‚ÑÇ (ModularForm (Subgroup.map f (‚ä§ : Subgroup SL(2, ‚Ñ§))) k) := by
    have hŒì : (Œì(1) : Subgroup SL(2, ‚Ñ§)) = ‚ä§ := by
      simpa using (CongruenceSubgroup.Gamma_one_top : CongruenceSubgroup.Gamma 1 = ‚ä§)
    have hmap :
        Subgroup.map f (Œì(1) : Subgroup SL(2, ‚Ñ§)) = Subgroup.map f (‚ä§ : Subgroup SL(2, ‚Ñ§)) :=
      congrArg (fun H : Subgroup SL(2, ‚Ñ§) => Subgroup.map f H) hŒì
    exact finiteDimensional_modularForm_congr (k := k) hmap hŒì1
  have hrange : f.range = Subgroup.map f (‚ä§ : Subgroup SL(2, ‚Ñ§)) := by
    simpa [f] using (MonoidHom.range_eq_map f)
  exact finiteDimensional_modularForm_congr (k := k) hrange.symm htop

open SpherePacking.ModularForms.NormReduction

public lemma dim_gen_cong_levels (k : ‚Ñ§) (Œì : Subgroup SL(2, ‚Ñ§)) (hŒì : Subgroup.index Œì ‚â† 0) :
    FiniteDimensional ‚ÑÇ (ModularForm Œì k) := by
  by_cases hkneg : k < 0
  ¬∑ exact SpherePacking.ModularForms.finiteDimensional_modularForm_neg_weight Œì hŒì k hkneg
  ¬∑ have hk0le : 0 ‚â§ k := le_of_not_gt hkneg
    by_cases hk0 : k = 0
    ¬∑ subst hk0
      exact SpherePacking.ModularForms.finiteDimensional_modularForm_weight_zero Œì hŒì
    ¬∑ haveI : Œì.FiniteIndex := ‚ü®hŒì‚ü©
      let GŒì : Subgroup (GL (Fin 2) ‚Ñù) := SpherePacking.ModularForms.NormReduction.G Œì
      let h : ‚Ñù := SpherePacking.ModularForms.NormReduction.cuspWidth (Œì := Œì)
      have hh : 0 < h := SpherePacking.ModularForms.NormReduction.cuspWidth_pos (Œì := Œì) hŒì
      have hperŒì : h ‚àà GŒì.strictPeriods := by
        simpa [h] using
          SpherePacking.ModularForms.NormReduction.cuspWidth_mem_strictPeriods (Œì := Œì)
      have hperSL : h ‚àà (ùíÆ‚Ñí : Subgroup (GL (Fin 2) ‚Ñù)).strictPeriods := by
        simpa [h] using
          SpherePacking.ModularForms.NormReduction.cuspWidth_mem_strictPeriods_levelOne (Œì := Œì)
      haveI : GŒì.IsArithmetic :=
        SpherePacking.ModularForms.NormReduction.instIsArithmetic (Œì := Œì) hŒì
      haveI : GŒì.IsFiniteRelIndex ùíÆ‚Ñí :=
        Subgroup.IsArithmetic.isFiniteRelIndexSL
          (ùí¢ := GŒì)
      let w : ‚Ñ§ := k * Nat.card (SpherePacking.ModularForms.NormReduction.Q Œì)
      haveI : FiniteDimensional ‚ÑÇ (ModularForm ùíÆ‚Ñí w) := by
        simpa [w] using (finiteDimensional_modularForm_SL2Z (k := w))
      obtain ‚ü®N, hNinj‚ü© :=
        SpherePacking.ModularForms.exists_qCoeff_injective
          (Œì := (ùíÆ‚Ñí : Subgroup (GL (Fin 2) ‚Ñù))) (k := w) (h := h) hh hperSL
      let trunc : ModularForm GŒì k ‚Üí‚Çó[‚ÑÇ] (Fin N ‚Üí ‚ÑÇ) :=
      { toFun := fun f => fun n => (qExpansion h f).coeff n
        map_add' := by
          intro f g
          ext n
          simp [qExpansion_add hh hperŒì f g]
        map_smul' := by
          intro a f
          ext n
          simp [qExpansion_smul hh hperŒì a f] }
      have htrunc_inj : Function.Injective trunc := by
        intro f g hfg
        have hcoeff : ‚àÄ m < N, (qExpansion h (f - g)).coeff m = 0 := by
          intro m hm
          have hsub : trunc (f - g) = 0 := by
            have hmap : trunc (f - g) = trunc f - trunc g := trunc.map_sub f g
            have hdiff : trunc f - trunc g = 0 := by simp [hfg]
            simp [hmap, hdiff]
          have := congrArg (fun t : Fin N ‚Üí ‚ÑÇ => t ‚ü®m, hm‚ü©) hsub
          simpa [trunc] using this
        have hcoeff_norm : ‚àÄ m < N,
            (qExpansion h (ModularForm.norm ùíÆ‚Ñí (f - g))).coeff m = 0 := by
          have qCoeffNorm :=
            qExpansion_coeff_eq_zero_norm_of_qExpansion_coeff_eq_zero (Œì := Œì) (k := k)
          intro m hm
          exact qCoeffNorm (f := (f - g)) (N := N) (n := m) hm hcoeff
        have hfun :
            (fun n : Fin N => (qExpansion h (ModularForm.norm ùíÆ‚Ñí (f - g))).coeff n) =
          fun n : Fin N => (qExpansion h (0 : ModularForm ùíÆ‚Ñí w)).coeff n := by
          ext n
          simpa [qExpansion_zero h] using hcoeff_norm (n : ‚Ñï) n.isLt
        have hnorm : ModularForm.norm ùíÆ‚Ñí (f - g) = (0 : ModularForm ùíÆ‚Ñí w) :=
          hNinj hfun
        have : (f - g : ModularForm GŒì k) = 0 := by
          have := (ModularForm.norm_eq_zero_iff (‚Ñã := ùíÆ‚Ñí) (f := (f - g)) (k := k))
          have hf0 :
              ((f - g : ModularForm GŒì k) : ‚Ñç ‚Üí ‚ÑÇ) = 0 :=
            this.1 (by simpa using hnorm)
          ext x
          have hx := congrArg (fun g : ‚Ñç ‚Üí ‚ÑÇ => g x) hf0
          simpa using hx
        simpa [sub_eq_zero] using this
      haveI : FiniteDimensional ‚ÑÇ (Fin N ‚Üí ‚ÑÇ) := by infer_instance
      simpa using (FiniteDimensional.of_injective trunc htrunc_inj)
