module
public import Mathlib.Analysis.Normed.Group.Tannery
public import SpherePacking.ModularForms.JacobiTheta
public import SpherePacking.ForMathlib.AtImInfty

/-!
# Limits at infinity

In this file we establish basic results about q-expansions. The results are put under the `QExp`
namespace.
-/

namespace QExp

open scoped Real

open UpperHalfPlane hiding I
open Complex Asymptotics Topology Filter

private lemma norm_term_nat (a : ‚Ñï ‚Üí ‚ÑÇ) (z : ‚Ñç) (n : ‚Ñï) :
    ‚Äña n * cexp (2 * œÄ * I * z * n)‚Äñ = ‚Äña n‚Äñ * rexp (z.im * (-2 * œÄ * n)) := by
  simp_rw [norm_mul, mul_right_comm _ I, norm_exp_mul_I]
  simp [mul_assoc, mul_left_comm, mul_comm]

private lemma tendsto_term_nat_of_ne_zero (a : ‚Ñï ‚Üí ‚ÑÇ) {k : ‚Ñï} (hk : k ‚â† 0) :
    Tendsto (fun z : ‚Ñç ‚Ü¶ a k * cexp (2 * œÄ * I * z * k)) atImInfty (ùìù 0) := by
  apply tendsto_zero_iff_norm_tendsto_zero.2
  simp_rw [norm_term_nat (a := a) (n := k)]
  have hkneg : (-2 * œÄ * (k : ‚Ñù)) < 0 := by
    nlinarith [Real.pi_pos, (show (0 : ‚Ñù) < (k : ‚Ñù) by exact_mod_cast Nat.pos_of_ne_zero hk)]
  simpa [mul_assoc, mul_left_comm, mul_comm] using
    (tendsto_const_nhds.mul <| (Real.tendsto_exp_atBot).comp
      ((tendsto_mul_const_atBot_of_neg hkneg).2 tendsto_im_atImInfty))

private lemma norm_term_bound_nat (a : ‚Ñï ‚Üí ‚ÑÇ) {z : ‚Ñç} (hz : (1 : ‚Ñù) ‚â§ z.im) (k : ‚Ñï) :
    ‚Äña k * cexp (2 * œÄ * I * z * k)‚Äñ ‚â§ ‚Äña k‚Äñ * rexp (-2 * œÄ * k) := by
  simp_rw [norm_term_nat (a := a) (z := z) (n := k)]
  refine mul_le_mul_of_nonneg_left ?_ (norm_nonneg (a k))
  have ht : (-2 * œÄ * (k : ‚Ñù)) ‚â§ 0 := by
    nlinarith [Real.pi_pos, (show (0 : ‚Ñù) ‚â§ (k : ‚Ñù) by positivity)]
  simpa [mul_assoc, mul_left_comm, mul_comm] using
    Real.exp_le_exp.2 (mul_le_mul_of_nonpos_right hz ht)

/-- A dominated-convergence lemma for `‚Ñï`-indexed `q`-expansions at the cusp `‚àû`. -/
public lemma tendsto_nat (a : ‚Ñï ‚Üí ‚ÑÇ) (ha : Summable fun n : ‚Ñï ‚Ü¶ ‚Äña n‚Äñ * rexp (-2 * œÄ * n)) :
    Tendsto (fun z : ‚Ñç ‚Ü¶ ‚àë' n, a n * cexp (2 * œÄ * I * z * n)) atImInfty (ùìù (a 0)) := by
  simpa using
    (tendsto_tsum_of_dominated_convergence
      (f := fun z n ‚Ü¶ a n * cexp (2 * œÄ * I * z * n))
      (ùìï := atImInfty)
      (g := Set.indicator ({0} : Set ‚Ñï) (fun _ ‚Ü¶ a 0))
      ha
      (by
        intro k
        rcases eq_or_ne k 0 with (rfl | hk)
        ¬∑ simp
        ¬∑ simpa [hk] using tendsto_term_nat_of_ne_zero (a := a) hk)
      (by
        rw [eventually_atImInfty]
        refine ‚ü®1, fun z hz k ‚Ü¶ ?_‚ü©
        simpa using norm_term_bound_nat (a := a) (z := z) hz k))

lemma tendsto_int (a : ‚Ñ§ ‚Üí ‚ÑÇ) (ha : Summable fun n : ‚Ñ§ ‚Ü¶ ‚Äña n‚Äñ * rexp (-2 * œÄ * n))
    (ha' : ‚àÄ n, n < 0 ‚Üí a n = 0) :
    Tendsto (fun z : ‚Ñç ‚Ü¶ ‚àë' n, a n * cexp (2 * œÄ * I * z * n)) atImInfty (ùìù (a 0)) := by
  -- ‚àë' (n : ‚Ñï), f ‚Üën + ‚àë' (n : ‚Ñï), f (-(‚Üën + 1))
  have : Tendsto
    (fun z : ‚Ñç ‚Ü¶ (‚àë' n : ‚Ñï, (a n * cexp (2 * œÄ * I * z * n)
      + a (-(n + 1 : ‚Ñ§)) * cexp (2 * œÄ * I * z * (-(n + 1) : ‚Ñ§))))) atImInfty (ùìù (a 0)) := by
    have := tendsto_nat (fun n ‚Ü¶ a n) ?_
    ¬∑ apply this.congr
      exact fun _ ‚Ü¶ tsum_congr (by simpa using fun _ ‚Ü¶ ha' _ (by omega))
    ¬∑ exact (summable_int_iff_summable_nat_and_neg.mp ha).left
  apply this.congr'
  rw [EventuallyEq, eventually_atImInfty]
  use 1, fun z hz ‚Ü¶ ?_
  rw [‚Üê tsum_nat_add_neg_add_one]
  ¬∑ rfl
  rw [‚Üê summable_norm_iff]
  convert_to Summable fun n ‚Ü¶ ‚Äña n‚Äñ * rexp (z.im * -2 * œÄ * n)
  ¬∑ ext n
    rw [norm_mul, mul_right_comm _ I, mul_right_comm _ I, norm_exp_mul_I]
    simp
    ring_nf
    simp
  ¬∑ apply ha.of_nonneg_of_le (fun _ ‚Ü¶ by positivity) fun b ‚Ü¶ ?_
    by_cases hb : 0 ‚â§ b
    ¬∑ have : z.im * -2 * œÄ * b ‚â§ -2 * œÄ * b := by
        gcongr
        simp [hz]
      gcongr
    ¬∑ norm_num at hb
      simp [ha' _ hb]

end QExp
