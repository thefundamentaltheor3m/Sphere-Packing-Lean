/-
Copyright (c) 2024 Sidharth Hariharan. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sidharth Hariharan
-/
import SpherePacking.CohnElkies.Prereqs
import SpherePacking.ForMathlib.VolumeOfBalls
import SpherePacking.Basic.PeriodicPacking
import Mathlib.Logic.IsEmpty
import Mathlib.MeasureTheory.Integral.SetIntegral
import Mathlib.Analysis.Complex.Basic

open scoped FourierTransform ENNReal SchwartzMap
open SpherePacking Metric BigOperators Pointwise Filter MeasureTheory Complex Real Zspan Bornology

variable {d : â„•} [instPosDim : Fact (0 < d)] -- Is `Fact` right here?

/-
# Potential Design Complications:

* What we have in Mathlib on Fourier Transforms seems to deal with complex-valued functions. I've
  dealt with it for now by giving an assumption that the imaginary part of `f` is always zero and
  stating everything else in terms of the real part of `f`. The real-valuedness may not even be
  necessary, as we could simply apply the Cohn-Elkies theorem to the real part of any complex-valued
  function whose real part satisfies the Cohn-Elkies Conditions `hCohnElkiesâ‚` and `hCohnElkiesâ‚‚`.
  If the hypothesis does go unused (as I expect it will), I will remove it.
* As mentioned in `section theorem_2_2` of `SpherePacking/Basic/PeriodicPacking.lean`, we have to
  use a hack for fundamental domains by supplying the two necessary assumptions ourselves. One day,
  when it's a bit better developed in Mathlib, we can either modify our file or let people feed in
  those assumptions as inputs.

# TODOs:

* Everything in `Prereqs.lean` is either a TODO or has already been done (eg. in #25) (to reflect
  which the corresponding refs must be updated).
* Add some lemmas about when the set of centres of a sphere packing is empty. Then, do cases here
  and remove the `Nonempty` instance in the assumptions.
-/

-- Once we sort out the whole 'including variables' thing, we should remove all the variables from
-- the various lemmas and leave these as they are. Else, we should remove these and keep those.
variable {f : ğ“¢(EuclideanSpace â„ (Fin d), â„‚)} (hne_zero : f â‰  0)
-- We need `f` to be real-valued for Cohn-Elkies, but do we need that for the PSF-L as well?
variable (hReal : âˆ€ x : EuclideanSpace â„ (Fin d), â†‘(f x).re = (f x))
-- I'm not sure if `hCohnElkiesâ‚‚` can replace this, because of the 5th step in `calc_steps`.
-- (The blueprint says that ğ“• f x â‰¥Â 0, ie, ğ“• f âˆˆ [0, âˆ) âŠ† â„, for all x âˆˆ â„áµˆ)
-- We can't simply replace ğ“• f with its real part everywhere because the PSF-L involves ğ“• f.
variable (hRealFourier : âˆ€ x : EuclideanSpace â„ (Fin d), â†‘(ğ“• f x).re = (ğ“• f x))
-- The Cohn-Elkies conditions:
variable (hCohnElkiesâ‚ : âˆ€ x : EuclideanSpace â„ (Fin d), â€–xâ€– â‰¥ 1 â†’ (f x).re â‰¤ 0)
variable (hCohnElkiesâ‚‚ : âˆ€ x : EuclideanSpace â„ (Fin d), (ğ“• f x).re â‰¥ 0)

-- We (locally) denote the Complex Conjugate of some `z : â„‚` by `conj z`
local notation "conj" => starRingEnd â„‚

section Complex_Function_Helpers

private theorem helper (g : EuclideanSpace â„ (Fin d) â†’ â„‚) :
  (âˆ€ x : EuclideanSpace â„ (Fin d), â†‘(g x).re = (g x)) â†’
  (âˆ€ x : EuclideanSpace â„ (Fin d), (g x).im = 0) := by
  intro hIsReal x
  specialize hIsReal x
  rw [â† hIsReal, ofReal_im]

include hReal in
private theorem hImZero : âˆ€ x : EuclideanSpace â„ (Fin d), (f x).im = 0 :=
  helper f hReal

include hRealFourier in
private theorem hFourierImZero : âˆ€ x : EuclideanSpace â„ (Fin d), (ğ“• f x).im = 0 :=
  helper (ğ“• f) hRealFourier

end Complex_Function_Helpers

section Nonnegativity

private theorem hIntegrable : MeasureTheory.Integrable (ğ“• f) :=
    ((SchwartzMap.fourierTransformCLE â„) f).integrable

include hne_zero

theorem fourier_ne_zero : ğ“• f â‰  0 := by
  rw [â† SchwartzMap.fourierTransformCLE_apply â„ f]
  intro hFourierZero
  apply hne_zero
  rw [â† ContinuousLinearEquiv.map_eq_zero_iff (SchwartzMap.fourierTransformCLE â„)]
  exact Eq.symm (SchwartzMap.ext (congrFun (id (Eq.symm hFourierZero))))

include hReal hRealFourier hCohnElkiesâ‚‚

theorem f_nonneg_at_zero : 0 â‰¤ (f 0).re := by
  -- Building off the previous one, f(0) is an integral of a nonneg function, and hence, also nonneg
  rw [â† f.fourierInversion â„, fourierIntegralInv_eq]
  simp only [inner_zero_right, AddChar.map_zero_eq_one, one_smul]
  have hcalcâ‚ :
    (âˆ« (v : EuclideanSpace â„ (Fin d)), ğ“• (â‡‘f) v).re =
    âˆ« (v : EuclideanSpace â„ (Fin d)), (ğ“• (â‡‘f) v).re := by
    rw [â† RCLike.re_eq_complex_re, â† integral_re hIntegrable]
  rw [hcalcâ‚]
  exact integral_nonneg hCohnElkiesâ‚‚

theorem f_zero_pos : 0 < (f 0).re := by
  -- We know from previous that f(0) is nonneg. If zero, then the integral of ğ“• f is zero, making
  -- ğ“• f zero (it's continuous and nonneg: if it's pos anywhere, it's pos on a nbhd, and hence the
  -- integral must be pos too, but it's zero, contra). By Schwartz, f is identically zero iff ğ“• f
  -- is (ğ“• is a linear iso). But ğ“• f is zero while f is not, contra! So f(0) is positive.
  -- apply ne_of_gt
  have hauxâ‚ : f 0 = ğ“•â» (ğ“• f) 0 := by rw [f.fourierInversion â„]
  rw [fourierIntegralInv_eq] at hauxâ‚
  simp only [inner_zero_right, AddChar.map_zero_eq_one, one_smul] at hauxâ‚
  -- We need to take real parts at hauxâ‚
  rw [â† re_add_im (f 0), hImZero hReal, ofReal_zero, zero_mul, add_zero] at hauxâ‚
  -- We need to take real and imaginary parts inside the integral.
  have hauxâ‚‚ : âˆ« (v : EuclideanSpace â„ (Fin d)), ğ“• (â‡‘f) v =
    âˆ« (v : EuclideanSpace â„ (Fin d)), (ğ“• (â‡‘f) v).re :=
    calc âˆ« (v : EuclideanSpace â„ (Fin d)), ğ“• (â‡‘f) v
    _ = â†‘(âˆ« (v : EuclideanSpace â„ (Fin d)), (ğ“• (â‡‘f) v).re) +
    (âˆ« (v : EuclideanSpace â„ (Fin d)), (ğ“• (â‡‘f) v).im) * I
      := by
         rw [â† re_add_im (âˆ« (v : EuclideanSpace â„ (Fin d)), ğ“• (â‡‘f) v)]
         rw [â† RCLike.re_eq_complex_re, â† integral_re hIntegrable, RCLike.re_eq_complex_re]
         rw [â† RCLike.im_eq_complex_im, â† integral_im hIntegrable, RCLike.im_eq_complex_im]
    _ = âˆ« (v : EuclideanSpace â„ (Fin d)), (ğ“• (â‡‘f) v).re
      := by
         rw [add_right_eq_self]
         suffices hwhat : âˆ€ v : EuclideanSpace â„ (Fin d), (ğ“• (â‡‘f) v).im = 0
         Â· simp only [hwhat, ofReal_zero, zero_mul, integral_zero]
         exact hFourierImZero hRealFourier
  rw [hauxâ‚‚] at hauxâ‚
  norm_cast at hauxâ‚
  rw [hauxâ‚, lt_iff_not_ge]
  by_contra hantisymmâ‚
  have hantisymmâ‚‚ : 0 â‰¤ âˆ« (v : EuclideanSpace â„ (Fin d)), (ğ“• (â‡‘f) v).re := integral_nonneg hCohnElkiesâ‚‚
  have hintzero : 0 = âˆ« (v : EuclideanSpace â„ (Fin d)), (ğ“• (â‡‘f) v).re := by
    rw [ge_iff_le] at hantisymmâ‚
    exact antisymm' hantisymmâ‚ hantisymmâ‚‚
  have hğ“•frezero : âˆ€ x, (ğ“• f x).re = 0 := by
    -- Integral of a nonneg continuous function is zero iff the function is zero
    suffices hfun : (fun x => (ğ“• f x).re) = 0
    -- (This is the function actually being integrated)
    Â· intro x
      calc (ğ“• (â‡‘f) x).re
      _ = (fun x => (ğ“• f x).re) x := rfl
      _ = (0 : (EuclideanSpace â„ (Fin d)) â†’ â„) x := by rw [hfun]
      _ = 0 := by rw [Pi.zero_apply]
    have hcont : Continuous (fun x â†¦ (ğ“• (â‡‘f) x).re) := by
      rw [â† SchwartzMap.fourierTransformCLE_apply â„ f]
      exact Continuous.comp' continuous_re ((SchwartzMap.fourierTransformCLE â„) f).continuous
    refine (Continuous.integral_zero_iff_zero_of_nonneg hcont ?_ hCohnElkiesâ‚‚).mp hintzero.symm
    rw [â† RCLike.re_eq_complex_re]
    refine MeasureTheory.Integrable.re ?_
    rw [â† SchwartzMap.fourierTransformCLE_apply â„ f]
    exact ((SchwartzMap.fourierTransformCLE â„) f).integrable
  have hğ“•fzero : ğ“• f = 0 := by
    ext x
    rw [â† re_add_im (ğ“• f x), hFourierImZero hRealFourier, ofReal_zero, zero_mul,
        add_zero, Pi.zero_apply, ofReal_eq_zero]
    exact hğ“•frezero x
  exact fourier_ne_zero hne_zero hğ“•fzero

end Nonnegativity

section Fundamental_Domain_Dependent

include d instPosDim f hne_zero hReal hRealFourier hCohnElkiesâ‚ hCohnElkiesâ‚‚

variable {P : PeriodicSpherePacking d} (hP : P.separation = 1) [Nonempty P.centers]
variable {D : Set (EuclideanSpace â„ (Fin d))} (hD_isBounded : IsBounded D)
variable (hD_unique_covers : âˆ€ x, âˆƒ! g : P.lattice, g +áµ¥ x âˆˆ D) (hD_measurable : MeasurableSet D)

/-
In this section, we will prove that the density of every periodic sphere packing of separation 1 is
bounded above by the Cohn-Elkies bound.
-/

include hP
private theorem calc_aux_1 :
  âˆ‘' x : P.centers, âˆ‘' y : â†‘(P.centers âˆ© D), (f (x - â†‘y)).re
  â‰¤ â†‘(P.numReps' Fact.out hD_isBounded) * (f 0).re := calc
  âˆ‘' x : P.centers, âˆ‘' y : â†‘(P.centers âˆ© D), (f (x - â†‘y)).re
  _ = (âˆ‘' (x : P.centers) (y : â†‘(P.centers âˆ© D)),
      if h : x - (y : EuclideanSpace â„ (Fin d)) = 0 then 0 else (f (x - â†‘y)).re) +
      (âˆ‘' (x : â†‘(P.centers âˆ© D)), (f (0 : EuclideanSpace â„ (Fin d))).re)
        := by
            -- First, we need to un-distribute the tsums on the RHS.
            -- Then, we need to use some sort of `tsum_ite_eq`.
            -- Both of the above require some summability stuff.
            sorry
  _ â‰¤ âˆ‘' (x : â†‘(P.centers âˆ© D)), (f (0 : EuclideanSpace â„ (Fin d))).re
        := by
            rw [â† tsub_nonpos]
            rw [add_sub_cancel_right]
            apply tsum_nonpos
            intro x
            apply tsum_nonpos
            intro y
            cases eq_or_ne ((x : EuclideanSpace â„ (Fin d)) - y) (0 : EuclideanSpace â„ (Fin d))
            Â· case inl h =>
              simp only [h, â†“reduceDIte, le_refl]
            Â· case inr h =>
              simp only [h, â†“reduceDIte]
              apply hCohnElkiesâ‚ (x - y)
              -- Both `x` and `y` are in `P.centers` and are distinct. `hP` then implies the result.
              rw [â† hP]
              apply P.centers_dist'
              Â· exact Subtype.mem x
              Â· obtain âŸ¨hyâ‚, hyâ‚‚âŸ© := Subtype.mem y
                exact hyâ‚
              Â· exact sub_ne_zero.mp h
    -- _ = âˆ‘' (y : â†‘(P.centers âˆ© D)), (f (y - â†‘y)).re
    --     := by simp only [sub_self]
    _ = â†‘(P.numReps' Fact.out hD_isBounded) * (f 0).re
        := by
            simp only [tsum_const, nsmul_eq_mul, mul_eq_mul_right_iff, Nat.cast_inj]
            cases eq_or_ne (f 0).re 0
            Â· case inl h =>
              right
              rw [h]
            Â· case inr h =>
              left
              let myInstFintype := P.instFintypeNumReps' Fact.out hD_isBounded
              rw [PeriodicSpherePacking.numReps']
              exact Nat.card_eq_fintype_card

-- # NOTE:
-- There are several summability results stated as intermediate `have`s in the following theorem.
-- I think their proofs should follow from whatever we define `PSF_Conditions` to be.
-- If there are assumptions needed beyond PSF, we should require them here, not in `PSF_Conditions`.
set_option maxHeartbeats 200000
private theorem calc_steps :
  â†‘(P.numReps' Fact.out hD_isBounded) * (f 0).re â‰¥ â†‘(P.numReps' Fact.out hD_isBounded) ^ 2 *
  (ğ“• f 0).re / Zlattice.covolume P.lattice := calc
  â†‘(P.numReps' Fact.out hD_isBounded) * (f 0).re
  _ â‰¥ âˆ‘' (x : P.centers) (y : â†‘(P.centers âˆ© D)),
      (f (x - â†‘y)).re
        := by
            rw [ge_iff_le]
            exact calc_aux_1 hne_zero hReal hRealFourier hCohnElkiesâ‚ hCohnElkiesâ‚‚ hP
              hD_isBounded
  _ = âˆ‘' (x : â†‘(P.centers âˆ© D)) (y : â†‘(P.centers âˆ© D)) (â„“ : P.lattice),
      (f (â†‘x - â†‘y + â†‘â„“)).re
        :=  by
              -- We need to use `PeriodocSpherePacking.unique_covers_of_centers` to split up the
              -- `tsum` in `x` by writing `P.centers` as a union of translates of `P.centers âˆ© D`.
              -- We'd need disjointedness so we can apply `tsum_finset_bUnion_disjoint`.
              -- Some summability stuff might be necessary as well...

              sorry
  -- We now take the real part out so we can apply the PSF-L to the stuff inside.
  -- The idea would be to say, in subsequent lines, that "it suffices to show that the numbers
  -- whose real parts we're taking are equal as complex numbers" and then apply the PSF-L and
  -- other complex-valued stuff.
  _ = (âˆ‘' (x : â†‘(P.centers âˆ© D)) (y : â†‘(P.centers âˆ© D)) (â„“ : P.lattice),
      f (â†‘x - â†‘y + â†‘â„“)).re
        := by
            -- rw [re_tsum hPSF.1] -- Needs some sort of summability over subsets...?
            sorry
  _ = (âˆ‘' x : â†‘(P.centers âˆ© D),
      âˆ‘' y : â†‘(P.centers âˆ© D), (1 / Zlattice.covolume P.lattice) *
      âˆ‘' m : DualLattice P.lattice, (ğ“• f m) *
      exp (2 * Ï€ * I * âŸªâ†‘x - â†‘y, (m : EuclideanSpace â„ (Fin d))âŸ«_â„)).re
        := by
            -- First, we apply the fact that two sides are equal if they're equal in â„‚.
            apply congrArg re
            -- Next, we apply the fact that two sums are equal if their summands are.
            apply congrArg _ _
            ext x
            apply congrArg _ _
            ext y
            -- Now that we've isolated the innermost sum, we can use the PSF-L.
            exact SchwartzMap.PoissonSummation_Lattices P.lattice f (x - â†‘y)
  _ = ((1 / Zlattice.covolume P.lattice) * âˆ‘' m : DualLattice P.lattice, (ğ“• f m).re * (
      âˆ‘' (x : â†‘(P.centers âˆ© D)) (y : â†‘(P.centers âˆ© D)),
      exp (2 * Ï€ * I * âŸªâ†‘x - â†‘y, (m : EuclideanSpace â„ (Fin d))âŸ«_â„))).re
        := by
            apply congrArg re
            simp only [tsum_mul_left]
            apply congrArg _ _
            simp only [â† tsum_mul_left]
            -- We want to apply `tsum_comm`, which requires some summability conditions.
            have hSummableâ‚ : Summable (Function.uncurry fun
            (m : â†¥(DualLattice P.lattice)) (x : â†‘(P.centers âˆ© D)) â†¦
            âˆ‘' (x_1 : â†‘(P.centers âˆ© D)), â†‘(ğ“• f â†‘m).re * exp (2 * â†‘Ï€ * I *
            â†‘âŸª(x : EuclideanSpace â„ (Fin d)) - â†‘x_1, â†‘mâŸ«_â„)) := by
              sorry
            rw [â† tsum_comm hSummableâ‚]
            apply congrArg _ _
            ext x
            have hSummableâ‚‚ : Summable (Function.uncurry fun
            (m : â†¥(DualLattice P.lattice)) (x_1 : â†‘(P.centers âˆ© D)) â†¦
            â†‘(ğ“• f â†‘m).re * exp (2 * â†‘Ï€ * I * â†‘âŸª(x : EuclideanSpace â„ (Fin d)) - â†‘x_1, â†‘mâŸ«_â„)) := by
              sorry
            rw [â† tsum_comm hSummableâ‚‚]
            apply congrArg _ _
            ext y
            apply congrArg _ _
            ext m
            refine (IsUnit.mul_left_inj ?h.h).mpr ?h.a
            Â· rw [isUnit_iff_ne_zero]
              exact Complex.exp_ne_zero _
            Â· exact (hRealFourier (m : EuclideanSpace â„ (Fin d))).symm
  _ = ((1 / Zlattice.covolume P.lattice) * âˆ‘' m : DualLattice P.lattice, (ğ“• f m).re * (
      âˆ‘' (x : â†‘(P.centers âˆ© D)) (y : â†‘(P.centers âˆ© D)),
      exp (2 * Ï€ * I * âŸªâ†‘x, (m : EuclideanSpace â„ (Fin d))âŸ«_â„) *
      exp (2 * Ï€ * I * âŸª-â†‘y, (m : EuclideanSpace â„ (Fin d))âŸ«_â„))).re
        := by
            -- As before, we have to go through a bunch of `congrArg`s to isolate the expressions we
            -- are really trying to show are equal.
            apply congrArg re
            apply congrArg _ _
            apply congrArg _ _
            ext m
            apply congrArg _ _
            apply congrArg _ _
            ext x
            apply congrArg _ _
            ext y
            rw [sub_eq_neg_add, inner_add_left]
            push_cast  -- Can this be condensed into a rw so that there's just a bunch of rws?
            rw [mul_add, Complex.exp_add, mul_comm]
  _ = ((1 / Zlattice.covolume P.lattice) * âˆ‘' m : DualLattice P.lattice, (ğ“• f m).re *
      (âˆ‘' x : â†‘(P.centers âˆ© D),
      exp (2 * Ï€ * I * âŸªâ†‘x, (m : EuclideanSpace â„ (Fin d))âŸ«_â„)) *
      (âˆ‘' y : â†‘(P.centers âˆ© D),
      exp (-(2 * Ï€ * I * âŸªâ†‘y, (m : EuclideanSpace â„ (Fin d))âŸ«_â„)))).re
        := by
            apply congrArg re
            apply congrArg _ _
            apply congrArg _ _
            ext m
            simp only [mul_assoc]
            apply congrArg _ _
            rw [â† tsum_mul_right]
            apply congrArg _ _
            ext x
            rw [â† tsum_mul_left]
            apply congrArg _ _
            ext y
            simp only [inner_neg_left, ofReal_neg, mul_neg]
  _ = ((1 / Zlattice.covolume P.lattice) * âˆ‘' m : DualLattice P.lattice, (ğ“• f m).re *
      (âˆ‘' x : â†‘(P.centers âˆ© D),
      exp (2 * Ï€ * I * âŸªâ†‘x, (m : EuclideanSpace â„ (Fin d))âŸ«_â„)) *
      conj (âˆ‘' x : â†‘(P.centers âˆ© D),
      exp (2 * Ï€ * I * âŸªâ†‘x, (m : EuclideanSpace â„ (Fin d))âŸ«_â„)) -- Need its complex conjugate
      ).re
        := by
            apply congrArg re
            apply congrArg _ _
            apply congrArg _ _
            ext m
            apply congrArg _ _
            rw [conj_tsum]
            apply congrArg _ _
            ext x
            exact Complex.exp_neg_real_I_eq_conj (x : EuclideanSpace â„ (Fin d)) m
  _ = (1 / Zlattice.covolume P.lattice) * âˆ‘' m : DualLattice P.lattice, (ğ“• f m).re *
      (Complex.abs (âˆ‘' x : â†‘(P.centers âˆ© D),
      exp (2 * Ï€ * I * âŸªâ†‘x, (m : EuclideanSpace â„ (Fin d))âŸ«_â„)) ^ 2)
        := by
            -- Need to turn the RHS into the real part of a complex number
            rw [â† ofReal_re (1 / Zlattice.covolume P.lattice volume *
                               âˆ‘' (m : â†¥(DualLattice P.lattice)),
                               (ğ“• f â†‘m).re * Complex.abs (âˆ‘' (x : â†‘(P.centers âˆ© D)),
                               cexp (2 * â†‘Ï€ * I * â†‘âŸª(x : EuclideanSpace â„ (Fin d)), â†‘mâŸ«_â„)) ^ 2)]
            -- Now we can apply the fact that the real parts of both expressions are equal if they
            -- are equal in â„‚.
            apply congrArg re
            push_cast
            apply congrArg _ _
            apply congrArg _ _
            ext m
            rw [mul_assoc]
            apply congrArg _ _
            rw [mul_conj, normSq_eq_abs]
            norm_cast
  -- We split the sum up into the `m = 0` and `m â‰  0` parts.
  _ = (1 / Zlattice.covolume P.lattice) * (
      (âˆ‘' (m : DualLattice P.lattice), if hm : m = (0 : EuclideanSpace â„ (Fin d)) then 0 else
      (ğ“• f m).re * (Complex.abs (âˆ‘' x : â†‘(P.centers âˆ© D),
      exp (2 * Ï€ * I * âŸªâ†‘x, (m : EuclideanSpace â„ (Fin d))âŸ«_â„)) ^ 2))
      +
      (ğ“• f (0 : EuclideanSpace â„ (Fin d))).re *
      (Complex.abs (âˆ‘' x : â†‘(P.centers âˆ© D),
      exp (2 * Ï€ * I * âŸªâ†‘x, (0 : EuclideanSpace â„ (Fin d))âŸ«_â„)) ^ 2))
        := by
            apply congrArg _ _
            rw [add_comm]
            have hSummable : Summable (fun (m : â†¥(DualLattice P.lattice)) =>
              (ğ“• f m).re * (Complex.abs (âˆ‘' x : â†‘(P.centers âˆ© D),
              exp (2 * Ï€ * I * âŸªâ†‘x, (m : EuclideanSpace â„ (Fin d))âŸ«_â„)) ^ 2)) := by
              sorry
            rw [tsum_eq_add_tsum_ite hSummable (0 : â†¥(DualLattice P.lattice))]
            simp only [ZeroMemClass.coe_zero, ZeroMemClass.coe_eq_zero, dite_eq_ite]
  _ â‰¥ (1 / Zlattice.covolume P.lattice) * (ğ“• f (0 : EuclideanSpace â„ (Fin d))).re *
      (Complex.abs (âˆ‘' x : â†‘(P.centers âˆ© D),
      exp (2 * Ï€ * I * âŸªâ†‘x, (0 : EuclideanSpace â„ (Fin d))âŸ«_â„)) ^ 2)
        := by
            -- We need to show that the `m â‰  0` part is nonpositive.
            -- We begin by subtracting both sides, and thereby, isolating the `m â‰  0` part.
            rw [ge_iff_le, â† tsub_nonpos, mul_assoc,
                â† mul_sub (1 / Zlattice.covolume P.lattice volume) _ _]
            simp only [ZeroMemClass.coe_eq_zero, dite_eq_ite, sub_add_cancel_right, mul_neg,
              Left.neg_nonpos_iff]
            -- We now get rid of the `1 / Zlattice.covolume P.lattice volume` factor.
            apply mul_nonneg
            Â· refine one_div_nonneg.mpr ?ha.a
              rw [Zlattice.covolume]
              exact ENNReal.toReal_nonneg
            Â· -- We now show that the `m â‰  0` sum is nonpositive by showing that each term is.
              apply tsum_nonneg
              intro m
              cases eq_or_ne m 0
              Â· case inl h =>
                simp only [h, â†“reduceIte, le_refl]
              Â· case inr h =>
                simp only [h, â†“reduceIte]
                apply mul_nonneg
                Â· rw [â† ge_iff_le]
                  exact hCohnElkiesâ‚‚ m
                Â· -- Providing an explicit argument below gives a deterministic timeout...
                  exact sq_nonneg _
  _ = (1 / Zlattice.covolume P.lattice) * (ğ“• f (0 : EuclideanSpace â„ (Fin d))).re *
      â†‘(P.numReps' Fact.out hD_isBounded) ^ 2
        := by
            apply congrArg _ _
            let myInstFintype := P.instFintypeNumReps' Fact.out hD_isBounded
            simp only [inner_zero_right, zero_mul, ofReal_zero, mul_zero, Complex.exp_zero,
                       tsum_const, nsmul_eq_mul, mul_one, abs_natCast, Nat.cast_nonneg, ne_eq,
                       not_false_eq_true, pow_left_inj, Nat.cast_inj,
                       PeriodicSpherePacking.numReps', Set.toFinset_card] -- â†‘(P.centers âˆ© D)]
            exact Nat.card_eq_fintype_card
  _ = â†‘(P.numReps' Fact.out hD_isBounded) ^ 2 * (ğ“• f 0).re / Zlattice.covolume P.lattice volume
        := by simp only [div_eq_mul_inv, one_div, mul_comm, one_mul, â† mul_assoc]


-- And now, the main result of this section:
include hP hD_isBounded hD_unique_covers hD_measurable

-- Temporary hack, must be deleted
-- instance : HMul â„ â„â‰¥0âˆ â„â‰¥0âˆ := sorry

/-
I think the only sustainable fix is to show that the volume of a sphere is finite and then turn
`SpherePacking.density` into an element of `â„` instead of `â„â‰¥0âˆ`.
-/

end Fundamental_Domain_Dependent

section Main_Theorem_For_One_Packing

variable {P : PeriodicSpherePacking d} (hP : P.separation = 1) [Nonempty P.centers]
variable {D : Set (EuclideanSpace â„ (Fin d))} (hD_isBounded : IsBounded D)
variable (hD_unique_covers : âˆ€ x, âˆƒ! g : P.lattice, g +áµ¥ x âˆˆ D)

include d instPosDim f hne_zero hReal hRealFourier hCohnElkiesâ‚ hCohnElkiesâ‚‚ P hP D hD_isBounded hD_unique_covers

theorem LinearProgrammingBound' :
  P.density â‰¤ (f 0).re.toNNReal / (ğ“• f 0).re.toNNReal *
  volume (ball (0 : EuclideanSpace â„ (Fin d)) (1 / 2)) := by
  -- HUGE TODO: Get the periodic density formula in terms of some `D`.
  rw [P.density_eq' Fact.out]
  suffices hCalc : (P.numReps' Fact.out hD_isBounded) * (f 0).re â‰¥
    (P.numReps' Fact.out hD_isBounded)^2 * (ğ“• f 0).re / Zlattice.covolume P.lattice
  Â· rw [hP]
    rw [ge_iff_le] at hCalc
    have vol_pos := EuclideanSpace.volume_ball_pos (0 : EuclideanSpace â„ (Fin d)) one_half_pos
    have vol_ne_zero : volume (ball (0 : EuclideanSpace â„ (Fin d)) (1 / 2)) â‰  0 :=
      Ne.symm (ne_of_lt vol_pos)
    have vol_ne_top : volume (ball (0 : EuclideanSpace â„ (Fin d)) (1 / 2)) â‰  âˆ := by
      rw [â† lt_top_iff_ne_top]
      exact EuclideanSpace.volume_ball_lt_top 0
    cases eq_or_ne (ğ“• f 0) 0
    Â· case inl hğ“•f =>
      rw [hğ“•f, zero_re]
      -- For `ENNReal.div_zero`, we need `f 0 â‰  0`. This can be deduced from the fact that
      -- `ğ“• f â‰¥ 0` and `f â‰ Â 0`.
      have ne_zero_at_zero : ((f 0).re.toNNReal : ENNReal) â‰  0 :=
        ENNReal.coe_ne_zero.mpr (Ne.symm (ne_of_lt (toNNReal_pos.mpr
        (f_zero_pos hne_zero hReal hRealFourier hCohnElkiesâ‚‚))))
      -- Now we can safely divide by zero!
      rw [ENat.toENNReal_coe, toNNReal_zero, ENNReal.coe_zero, ENNReal.div_zero ne_zero_at_zero]
      -- We now need to multiply by âŠ¤.
      rw [ENNReal.top_mul vol_ne_zero]
      exact le_top
    Â· case inr hğ“•f =>
      -- First, we shift things around and cancel volumes on the right
      rw [ENat.toENNReal_coe, mul_div_assoc, div_eq_mul_inv (volume _), mul_comm (volume _),
          â† mul_assoc, ENNReal.mul_le_mul_right vol_ne_zero vol_ne_top]
      -- Next, we simplify `hCalc` by replacing `numReps'` with `numReps`
      rw [â† P.numReps_eq_numReps' Fact.out hD_isBounded hD_unique_covers] at hCalc
      -- Next, we multiply both sides by `(ğ“• (â‡‘f) 0).re.toNNReal`, cancelling accordingly.
      have hfouauxâ‚ : ((ğ“• (â‡‘f) 0).re.toNNReal : ENNReal) â‰  0 := by
        intro hContra
        apply hğ“•f
        simp only [ENNReal.coe_eq_zero, toNNReal_eq_zero] at hContra
        specialize hCohnElkiesâ‚‚ 0
        rw [ge_iff_le] at hCohnElkiesâ‚‚
        -- We can't simply do antisymm because we have an equality in â„‚, not â„!
        rw [â† re_add_im (ğ“• (â‡‘f) 0), le_antisymm hContra hCohnElkiesâ‚‚,
            hFourierImZero hRealFourier 0, ofReal_zero, zero_mul, add_zero]
      have hfouauxâ‚‚ : ((ğ“• (â‡‘f) 0).re.toNNReal : ENNReal) â‰  âŠ¤ := ENNReal.coe_ne_top
      rw [â† ENNReal.mul_le_mul_right hfouauxâ‚ hfouauxâ‚‚,
          div_eq_mul_inv ((f 0).re.toNNReal : ENNReal) _,
          mul_assoc ((f 0).re.toNNReal : ENNReal) _ _, ENNReal.inv_mul_cancel hfouauxâ‚ hfouauxâ‚‚]
      -- We put it in a more desirable form and consolidate.
      rw [mul_one, mul_assoc, â† ENNReal.div_eq_inv_mul]
      -- Next, we multiply both sides on the left by `â†‘P.numReps`.
      have hnRauxâ‚ : ENat.toENNReal (P.numReps : ENat) â‰  0 := by
        rw [ENat.toENNReal_coe, ne_eq, Nat.cast_eq_zero, â† ne_eq]
        -- intro hContra
        -- rw [â† P.card_centers_inter_isFundamentalDomain D hD_isBounded hD_unique_covers Fact.out]
        unfold PeriodicSpherePacking.numReps
        haveI : Nonempty (Quotient (AddAction.orbitRel â†¥P.lattice â†‘P.centers)) := by
          rw [nonempty_quotient_iff]
          assumption
        exact Fintype.card_ne_zero
      have hnRauxâ‚‚ : ENat.toENNReal (P.numReps : ENat) â‰  âŠ¤ := Ne.symm (ne_of_beq_false rfl)
      rw [â† ENNReal.mul_le_mul_left hnRauxâ‚ hnRauxâ‚‚]
      -- We put it in a more desirable form and consolidate.
      rw [ENat.toENNReal_coe, â† mul_assoc, â† pow_two, â† mul_div_assoc]
      -- Now, we use the nonnegativity of... everything... to get the `toNNReal`s to the outside.
      have hRHSCast : (P.numReps : ENNReal) * â†‘(f 0).re.toNNReal = (P.numReps * (f 0).re).toNNReal
      := by
        -- rw [ENNReal.coe_mul, ENNReal.coe_natCast]
        norm_cast
        refine NNReal.eq ?_
        have hauxâ‚ : 0 â‰¤ â†‘P.numReps * (f 0).re := mul_nonneg (Nat.cast_nonneg' P.numReps)
          (f_nonneg_at_zero hne_zero hReal hRealFourier hCohnElkiesâ‚‚)
        rw [Real.toNNReal_of_nonneg (f_nonneg_at_zero hne_zero hReal hRealFourier hCohnElkiesâ‚‚),
            Real.toNNReal_of_nonneg hauxâ‚]
        push_cast
        rfl
      have hLHSCast : (P.numReps : ENNReal) ^ 2 * ((ğ“• (â‡‘f) 0).re.toNNReal : ENNReal) / ((Zlattice.covolume P.lattice volume).toNNReal : ENNReal) = ((P.numReps) ^ 2 * (ğ“• (â‡‘f) 0).re / Zlattice.covolume P.lattice volume).toNNReal := by
        simp only [mul_div_assoc, div_eq_mul_inv]
        have hauxâ‚ : 0 â‰¤ â†‘P.numReps ^ 2 * (ğ“• (â‡‘f) 0).re * (Zlattice.covolume P.lattice volume)â»Â¹ := by
          refine mul_nonneg (mul_nonneg (sq_nonneg (P.numReps : â„)) (hCohnElkiesâ‚‚ 0)) ?_
          rw [inv_nonneg]
          exact LT.lt.le (Zlattice.covolume_pos P.lattice volume)
        rw [Real.toNNReal_of_nonneg hauxâ‚]
        have hauxâ‚‚ : (Zlattice.covolume P.lattice volume).toNNReal â‰  0 := by
          apply LT.lt.ne'
          rw [Real.toNNReal_pos]
          exact Zlattice.covolume_pos P.lattice volume
        rw [â† ENNReal.coe_inv hauxâ‚‚]
        norm_cast
        rw [Real.toNNReal_of_nonneg (hCohnElkiesâ‚‚ 0),
            Real.toNNReal_of_nonneg (LT.lt.le (Zlattice.covolume_pos P.lattice volume))]
        refine NNReal.eq ?_
        push_cast
        rfl
      -- We can now get rid of the `toNNReal`s and use `hCalc` to finish the proof!
      rw [hRHSCast, hLHSCast, ENNReal.coe_le_coe]
      exact Real.toNNReal_le_toNNReal hCalc
  exact calc_steps hne_zero hReal hRealFourier hCohnElkiesâ‚ hCohnElkiesâ‚‚ hP hD_isBounded

end Main_Theorem_For_One_Packing

section Main_Theorem

include d instPosDim f hne_zero hReal hRealFourier hCohnElkiesâ‚ hCohnElkiesâ‚‚

theorem LinearProgrammingBound : SpherePackingConstant d â‰¤
  (f 0).re.toNNReal / (ğ“• f 0).re.toNNReal * volume (ball (0 : EuclideanSpace â„ (Fin d)) (1 / 2)) := by
  rw [â† periodic_constant_eq_constant (Fact.out),
    periodic_constant_eq_periodic_constant_normalized (Fact.out)]
  apply iSup_le
  intro P
  rw [iSup_le_iff]
  intro hP
  cases isEmpty_or_nonempty â†‘P.centers
  Â· case inl instEmpty =>
    rw [P.density_of_centers_empty Fact.out]
    exact zero_le _
  Â· case inr instNonempty =>
    let b : Basis (Fin d) â„¤ â†¥P.lattice := ((Zlattice.module_free â„ P.lattice).chooseBasis).reindex
      (P.basis_index_equiv)
    exact LinearProgrammingBound' hne_zero hReal hRealFourier hCohnElkiesâ‚ hCohnElkiesâ‚‚ hP
      (fundamentalDomain_isBounded (Basis.ofZlatticeBasis â„ P.lattice b))
      (P.fundamental_domain_unique_covers b)

end Main_Theorem
