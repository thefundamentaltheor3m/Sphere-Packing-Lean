/-
Copyright (c) 2024 Sidharth Hariharan. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sidharth Hariharan, Gareth Ma
-/
import Mathlib
import SpherePacking.ForMathlib.VolumeOfBalls
import SpherePacking.ForMathlib.Real

open BigOperators MeasureTheory Metric

/-!
# Density of Sphere Packings

Let `X ‚äÜ ‚Ñù^d` be a set of points such that distinct points are at least distance `r` apart. Putting
a ball of radius `r / 2` around each point, we have a configuration of *sphere packing*. We call `X`
the sphere packing centers.

We also define the *density* of the configuration.
-/

open scoped ENNReal
open Metric BigOperators Pointwise Filter MeasureTheory

section Definitions

structure SpherePacking (d : ‚Ñï) where
  centers : Set (EuclideanSpace ‚Ñù (Fin d))
  separation : ‚Ñù
  separation_pos : 0 < separation := by positivity
  centers_dist : Pairwise (separation ‚â§ dist ¬∑ ¬∑ : centers ‚Üí centers ‚Üí Prop)

structure PeriodicSpherePacking (d : ‚Ñï) extends SpherePacking d where
  Œõ : AddSubgroup (EuclideanSpace ‚Ñù (Fin d))
  -- Note that an AddAction here is not enough, because
  Œõ_action : ‚àÄ ‚¶Éx y‚¶Ñ, x ‚àà Œõ ‚Üí y ‚àà centers ‚Üí x + y ‚àà centers
  Œõ_discrete : DiscreteTopology Œõ := by infer_instance
  Œõ_lattice : IsZlattice ‚Ñù Œõ := by infer_instance

variable {d : ‚Ñï}

noncomputable instance PeriodicSpherePacking.instAddAction (S : PeriodicSpherePacking d) :
    AddAction S.Œõ S.centers where
  vadd x y := ‚ü®‚Üëx + ‚Üëy, S.Œõ_action x.prop y.prop‚ü©
  zero_vadd := by
    intro ‚ü®v, hv‚ü©
    apply Subtype.ext
    exact zero_add v
  add_vadd := by
    intro ‚ü®u, hu‚ü© ‚ü®v, hv‚ü© ‚ü®p, hp‚ü©
    apply Subtype.ext
    exact add_assoc u v p

abbrev SpherePacking.balls (S : SpherePacking d) : Set (EuclideanSpace ‚Ñù (Fin d)) :=
  ‚ãÉ x ‚àà S.centers, ball x (S.separation / 2)

noncomputable def SpherePacking.finiteDensity (S : SpherePacking d) (R : ‚Ñù) : ‚Ñù‚â•0‚àû :=
  volume (S.balls ‚à© ball 0 R) / (volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) R))

noncomputable def SpherePacking.density (S : SpherePacking d) : ‚Ñù‚â•0‚àû :=
  limsup S.finiteDensity atTop

end Definitions

section Instances
variable {d : ‚Ñï}
open Real

-- Unfortunately I can't define a SMul ‚Ñù (SpherePacking d) because we require 0 < c
def SpherePacking.scale (S : SpherePacking d) {c : ‚Ñù} (hc : 0 < c) : SpherePacking d where
  centers := c ‚Ä¢ S.centers
  separation := c * S.separation
  separation_pos := Real.mul_pos hc S.separation_pos
  centers_dist := fun ‚ü®x, hx‚ü© ‚ü®y, hy‚ü© _ ‚Ü¶ by
    change c * S.separation ‚â§ ‚Äñx - y‚Äñ
    obtain ‚ü®x', ‚ü®hx', rfl‚ü©‚ü© := Set.mem_smul_set.mp hx
    obtain ‚ü®y', ‚ü®hy', rfl‚ü©‚ü© := Set.mem_smul_set.mp hy
    rw [‚Üê smul_sub, norm_smul, norm_eq_abs, abs_eq_self.mpr hc.le]
    rw [ne_eq, Subtype.mk.injEq] at *
    have : x' ‚â† y' := by rintro rfl; tauto
    have : (‚ü®x', hx'‚ü© : S.centers) ‚â† ‚ü®y', hy'‚ü© := by simp [this]
    have := S.centers_dist this
    exact (mul_le_mul_left hc).mpr this

noncomputable def PeriodicSpherePacking.scale (S : PeriodicSpherePacking d) {c : ‚Ñù} (hc : 0 < c) :
    PeriodicSpherePacking d := {
  SpherePacking.scale S.toSpherePacking hc with
  Œõ := c ‚Ä¢ S.Œõ
  Œõ_action := fun x y hx hy ‚Ü¶ by
    simp_all only [SpherePacking.scale, Set.mem_smul_set, AddSubgroup.mem_smul_pointwise_iff_exists]
    obtain ‚ü®x, hx, rfl‚ü© := hx
    obtain ‚ü®y, hy, rfl‚ü© := hy
    use x + y, S.Œõ_action hx hy, smul_add ..
  Œõ_discrete := by
    have := S.Œõ_discrete
    rw [discreteTopology_iff_isOpen_singleton_zero, Metric.isOpen_singleton_iff] at this ‚ä¢
    obtain ‚ü®Œµ, hŒµ, hŒµ'‚ü© := this
    use c * Œµ, Real.mul_pos hc hŒµ
    simp_rw [dist_zero_right, AddSubgroup.coe_norm, AddSubgroup.coe_pointwise_smul,
      Subtype.forall, AddSubmonoid.mk_eq_zero, AddSubgroup.mem_smul_pointwise_iff_exists] at hŒµ' ‚ä¢
    rintro x ‚ü®x, hx, rfl‚ü© hx'
    rw [norm_smul, norm_eq_abs, abs_eq_self.mpr hc.le, mul_lt_mul_left hc] at hx'
    rw [hŒµ' x hx hx', smul_zero]
  Œõ_lattice := by
    haveI := S.Œõ_discrete
    haveI := S.Œõ_lattice
    use ?_
    rw [‚Üê S.Œõ_lattice.span_top]
    ext v
    simp_rw [Submodule.mem_span]
    constructor <;> intro h p hp
    ¬∑ specialize h (c ‚Ä¢ p) ?_
      ¬∑ rw [AddSubgroup.coe_pointwise_smul, Submodule.coe_pointwise_smul]
        exact Set.smul_set_mono hp
      ¬∑ have : c ‚Ä¢ v ‚àà c ‚Ä¢ p := Submodule.smul_mem _ _ h
        have := Submodule.smul_mem_pointwise_smul _ c‚Åª¬π _ this
        simpa [smul_smul, inv_mul_cancel hc.ne.symm, one_smul]
    ¬∑ specialize h (c‚Åª¬π ‚Ä¢ p) ?_
      ¬∑ rw [AddSubgroup.coe_pointwise_smul, Submodule.coe_pointwise_smul] at *
        have := Set.smul_set_mono (a := c‚Åª¬π) hp
        rwa [smul_smul, inv_mul_cancel hc.ne.symm, one_smul] at this
      ¬∑ have : c‚Åª¬π ‚Ä¢ v ‚àà c‚Åª¬π ‚Ä¢ p := Submodule.smul_mem _ _ h
        have := Submodule.smul_mem_pointwise_smul _ c _ this
        simpa [smul_smul, mul_inv_cancel hc.ne.symm, one_smul]
}

end Instances

noncomputable section Density

variable {d : ‚Ñï} (S : SpherePacking d)

/-- The `PeriodicSpherePackingConstant` in dimension d is the supremum of the density of all
periodic packings. See also `<TODO>` for specifying the separation radius of the packings. -/
def PeriodicSpherePackingConstant (d : ‚Ñï) : ‚Ñù‚â•0‚àû :=
  ‚®Ü S : PeriodicSpherePacking d, S.density

/-- The `SpherePackingConstant` in dimension d is the supremum of the density of all packings. See
also `<TODO>` for specifying the separation radius of the packings. -/
def SpherePackingConstant (d : ‚Ñï) : ‚Ñù‚â•0‚àû :=
  ‚®Ü S : SpherePacking d, S.density

end Density

section DensityLemmas
open scoped NNReal
namespace SpherePacking

variable {d : ‚Ñï} (S : SpherePacking d) {c : ‚Ñù‚â•0}

lemma finiteDensity_le_one (R : ‚Ñù) : S.finiteDensity R ‚â§ 1 := by
  rw [finiteDensity]
  apply ENNReal.div_le_of_le_mul
  rw [one_mul]
  exact volume.mono Set.inter_subset_right

lemma density_le_one : S.density ‚â§ 1 := by
  rw [density]
  apply limsup_le_iSup.trans
  apply iSup_le
  intro
  exact finiteDensity_le_one _ _

theorem EuclideanSpace.volume_ball_mul {Œπ : Type*} [Nonempty Œπ] [Fintype Œπ]
    {x : EuclideanSpace ‚Ñù Œπ} {c : ‚Ñù} (hc : 0 ‚â§ c) (R : ‚Ñù) :
      volume (ball x (c * R)) = ENNReal.ofReal c ^ Fintype.card Œπ * volume (ball x R) := by
  simp_rw [EuclideanSpace.volume_ball]
  simp only [‚Üê mul_assoc]
  rw [ENNReal.ofReal_mul hc, mul_pow]

/-- Finite density of a scaled packing. -/
lemma scale_finiteDensity (hd : 0 < d) (S : SpherePacking d) {c : ‚Ñù} (hc : 0 < c) (R : ‚Ñù) :
    (S.scale hc).finiteDensity (c * R) = S.finiteDensity R := by
  haveI : Nonempty (Fin d) := Fin.pos_iff_nonempty.mp hd
  dsimp [finiteDensity, balls, scale]
  rw [EuclideanSpace.volume_ball_mul hc.le]

  sorry

/-- Density of a scaled packing. -/
lemma scale_density (hd : 0 < d) (S : SpherePacking d) {c : ‚Ñù} (hc : 0 < c) :
    (S.scale hc).density = S.density := by
  -- Proving this would be a good practice for limsup API
  dsimp [density, finiteDensity]

  sorry

-- TODO: Rename
theorem SpherePackingConstant_aux (hd : 0 < d) :
    SpherePackingConstant d = ‚®Ü (S : SpherePacking d) (_ : S.separation = 1), S.density := by
  rw [iSup_subtype', SpherePackingConstant]
  apply le_antisymm
  ¬∑ apply iSup_le
    intro S
    have h := inv_mul_cancel S.separation_pos.ne.symm
    have := le_iSup (fun S : { S : SpherePacking d // S.separation = 1 } ‚Ü¶ S.val.density)
        ‚ü®S.scale (inv_pos.mpr S.separation_pos), h‚ü©
    simpa only [scale_density hd]
  ¬∑ apply iSup_le
    intro ‚ü®S, _‚ü©
    exact le_iSup density S

end SpherePacking
end DensityLemmas

section PeriodicDensity

/- In this subsection, we prove that PeriodicDensity is equivalent to Density. This would allow us
to compute density of a periodic sphere packing easier. -/

variable (d : ‚Ñï)
local notation "V" => EuclideanSpace ‚Ñù (Fin d)
local notation "V" d => EuclideanSpace ‚Ñù (Fin d)

variable
  (X : Set (V d)) (r : ‚Ñù) [DiscreteTopology X] [SpherePackingCentres d X r]
  (Œõ : AddSubgroup (V d)) [DiscreteTopology Œõ] [IsZlattice ‚Ñù Œõ] [PeriodicPackingCentres d X r Œõ]
  {F : Set (V d)} (hF : IsAddFundamentalDomain Œõ F volume)

theorem Main : Density d X r = PeriodicDensity d X r Œõ hF := by
  sorry

end PeriodicDensity

section BasicResults

variable {d : ‚Ñï}
local notation "V" => EuclideanSpace ‚Ñù (Fin d)
local notation "V" d => EuclideanSpace ‚Ñù (Fin d)

open scoped ENNReal
open EuclideanSpace

variable (X : Set (V d)) (r R : ‚Ñù) [DiscreteTopology X] [hX : SpherePackingCentres d X r]

instance : Countable X := countable_of_Lindelof_of_discrete

/- In this section we establish basic results about FiniteDensity and Density of different types of
packings. -/

def instDiscreteX (hr : 0 < r) : DiscreteTopology X := by
  simp_rw [‚Üê singletons_open_iff_discrete, Metric.isOpen_iff]
  intro ‚ü®u, hu‚ü© ‚ü®v, hv‚ü© huv
  simp only [Set.mem_singleton_iff, Subtype.mk.injEq, Set.subset_singleton_iff, mem_ball,
    Subtype.forall] at huv ‚ä¢
  subst huv
  use r, hr
  intro a ha ha_dist
  have hX_dist := hX.nonoverlapping a v ha hv
  contrapose! hX_dist
  use hX_dist, ha_dist

theorem biUnion_inter_balls_subset_biUnion_balls_inter (r R : ‚Ñù) :
    ‚ãÉ x ‚àà X ‚à© ball 0 R, ball x r ‚äÜ (‚ãÉ x ‚àà X, ball x r) ‚à© ball 0 (R + r) := by
  intro x hx
  simp at hx ‚ä¢
  obtain ‚ü®y, ‚ü®hy‚ÇÅ, hy‚ÇÇ‚ü©‚ü© := hx
  use ‚ü®y, ‚ü®hy‚ÇÅ.left, hy‚ÇÇ‚ü©‚ü©
  apply lt_of_le_of_lt <| norm_le_norm_add_norm_sub' x y
  gcongr <;> tauto

theorem biUnion_balls_inter_subset_biUnion_inter_balls (r R : ‚Ñù) :
    (‚ãÉ x ‚àà X, ball x r) ‚à© ball 0 (R - r) ‚äÜ ‚ãÉ x ‚àà X ‚à© ball 0 R, ball x r := by
  intro x hx
  simp at hx ‚ä¢
  obtain ‚ü®‚ü®y, ‚ü®hy‚ÇÅ, hy‚ÇÇ‚ü©‚ü©, hx‚ü© := hx
  use y, ‚ü®hy‚ÇÅ, ?_‚ü©, hy‚ÇÇ
  calc
    ‚Äñy‚Äñ ‚â§ ‚Äñx‚Äñ + ‚Äñy - x‚Äñ := norm_le_norm_add_norm_sub' y x
    _ = ‚Äñx‚Äñ + dist x y := by rw [dist_comm]; rfl
    _ < R - r + r := by gcongr
    _ = R := by ring

theorem volume_iUnion_balls_eq_tsum (hr : 0 < r) (R : ‚Ñù) {r' : ‚Ñù} (hr' : r' ‚â§ r / 2) :
    volume (‚ãÉ x : ‚Üë(X ‚à© ball 0 R), ball (x : EuclideanSpace ‚Ñù (Fin d)) r')
      = ‚àë' x : ‚Üë(X ‚à© ball 0 R), volume (ball (x : EuclideanSpace ‚Ñù (Fin d)) r') := by
  have : DiscreteTopology X := instDiscreteX _ _ hr
  have : Countable X := countable_of_Lindelof_of_discrete
  have : Countable ‚Üë(X ‚à© ball 0 R) := Set.Countable.mono (Set.inter_subset_left) this
  apply measure_iUnion ?_ (fun _ ‚Ü¶ measurableSet_ball)
  intro ‚ü®x, hx‚ü© ‚ü®y, hy‚ü© h
  apply ball_disjoint_ball
  simp_rw [ne_eq, Subtype.mk.injEq] at h ‚ä¢
  change _ ‚â§ ‚Äñx - y‚Äñ
  linarith [hX.nonoverlapping x y hx.left hy.left h]

-- https://github.com/leanprover-community/mathlib4/pull/15214/files
-- Putting it as axioms so that #print axioms will show that this should be removed
-- TODO: remove when merged
axiom ENNReal.tsum_const_eq' {Œ± : Type*} (s : Set Œ±) (c : ENNReal) :
    ‚àë' (_ : s), c = s.encard * c

/-- This gives an upper bound on the number of points in the sphere packing X with norm less than R.
-/
theorem inter_ball_encard_le (hd : 0 < d) (hr : 0 < r) (R : ‚Ñù) :
    (X ‚à© ball 0 R).encard ‚â§
      volume ((‚ãÉ (x : X), ball (x : V) (r / 2)) ‚à© ball 0 (R + r / 2))
        / volume (ball (0 : V) (r / 2)) := by
  have h := volume.mono <| biUnion_inter_balls_subset_biUnion_balls_inter X (r / 2) R
  change volume _ ‚â§ volume _ at h
  simp_rw [Set.biUnion_eq_iUnion, volume_iUnion_balls_eq_tsum X r hr _ (le_refl _),
    Measure.addHaar_ball_center, ENNReal.tsum_const_eq'] at h
  haveI : Nonempty (Fin d) := Fin.pos_iff_nonempty.mp hd
  rwa [‚Üê ENNReal.le_div_iff_mul_le] at h <;> left
  ¬∑ exact (volume_ball_pos _ (by linarith)).ne.symm
  ¬∑ exact (volume_ball_lt_top _).ne

/-- This gives an upper bound on the number of points in the sphere packing X with norm less than R.
-/
theorem inter_ball_encard_ge (hd : 0 < d) (hr : 0 < r) (R : ‚Ñù) :
    (X ‚à© ball 0 R).encard ‚â•
      volume ((‚ãÉ (x : X), ball (x : V) (r / 2)) ‚à© ball 0 (R - r / 2))
        / volume (ball (0 : V) (r / 2)) := by
  have h := volume.mono <| biUnion_balls_inter_subset_biUnion_inter_balls X (r / 2) R
  change volume _ ‚â§ volume _ at h
  simp_rw [Set.biUnion_eq_iUnion, volume_iUnion_balls_eq_tsum X r hr _ (le_refl _),
    Measure.addHaar_ball_center, ENNReal.tsum_const_eq'] at h
  haveI : Nonempty (Fin d) := Fin.pos_iff_nonempty.mp hd
  rwa [‚Üê ENNReal.div_le_iff_le_mul] at h <;> left
  ¬∑ exact (volume_ball_pos _ (by linarith)).ne.symm
  ¬∑ exact (volume_ball_lt_top _).ne

theorem aux6 (hr : 0 < r) (R : ‚Ñù) : Finite ‚Üë(X ‚à© ball 0 R) := by
  apply Set.encard_lt_top_iff.mp
  by_cases hd : 0 < d
  ¬∑ haveI : Nonempty (Fin d) := Fin.pos_iff_nonempty.mp hd
    apply ENat.toENNReal_lt.mp
    apply lt_of_le_of_lt (inter_ball_encard_le X r hd hr R)
    apply ENNReal.div_lt_top ?_ (volume_ball_pos _ (by linarith)).ne.symm
    rw [‚Üê lt_top_iff_ne_top]
    calc
      _ ‚â§ volume (ball 0 (R + r / 2)) := volume.mono Set.inter_subset_right
      _ < ‚ä§ := EuclideanSpace.volume_ball_lt_top _
  ¬∑ rw [not_lt, nonpos_iff_eq_zero] at hd
    have : (ball (0 : EuclideanSpace ‚Ñù (Fin 0)) R).encard ‚â§ 1 := by
      rw [‚Üê Set.Finite.cast_ncard_eq (Set.toFinite _), Nat.cast_le_one]
      exact Set.ncard_le_one_of_subsingleton _
    subst hd
    apply lt_of_le_of_lt (Set.encard_mono inf_le_right)
    apply lt_of_le_of_lt this (by decide)

theorem finite_density_lower_bound [DiscreteTopology X] (hd : 0 < d) (hr : 0 < r) :
    FiniteDensity d X r R
      ‚â• (X ‚à© ball 0 (R - r / 2)).encard * volume (ball (0 : V) (r / 2))
        / volume (ball (0 : V) R) := by
  haveI : Nonempty (Fin d) := Fin.pos_iff_nonempty.mp hd
  rw [FiniteDensity, Packing_of_Centres, Set.biUnion_eq_iUnion]
  apply ENNReal.div_le_div_right
  rw [‚Üê ENNReal.le_div_iff_mul_le] <;> try left
  ¬∑ have := inter_ball_encard_le X _ hd hr (R - r / 2)
    rwa [sub_add_cancel] at this
  ¬∑ exact (volume_ball_pos _ (by linarith)).ne.symm
  ¬∑ exact (volume_ball_lt_top _).ne

theorem finite_density_upper_bound [DiscreteTopology X] (hd : 0 < d) (hr : 0 < r) :
    FiniteDensity d X r R
      ‚â§ (X ‚à© ball 0 (R + r / 2)).encard * volume (ball (0 : V) (r / 2))
        / volume (ball (0 : V) R) := by
  haveI : Nonempty (Fin d) := Fin.pos_iff_nonempty.mp hd
  rw [FiniteDensity, Packing_of_Centres, Set.biUnion_eq_iUnion]
  apply ENNReal.div_le_div_right
  rw [‚Üê ENNReal.div_le_iff_le_mul] <;> try left
  ¬∑ have := inter_ball_encard_ge X _ hd hr (R + r / 2)
    rwa [add_sub_cancel_right] at this
  ¬∑ exact (volume_ball_pos _ (by linarith)).ne.symm
  ¬∑ exact (volume_ball_lt_top _).ne

example : volume (ball (0 : EuclideanSpace ‚Ñù (Fin 8)) (‚àö2 / 2))
    = ENNReal.ofReal (Real.pi ^ 4 / 384) := by
  have h‚ÇÅ : ‚àö2 ^ 8 = 16 := by
    trans (‚àö2 ^ 2) ^ 4
    ¬∑ rw [‚Üê pow_mul]
    ¬∑ norm_num
  have h‚ÇÇ : ‚àöReal.pi ^ 8 = Real.pi ^ 4 := by
    trans (‚àöReal.pi ^ 2) ^ 4
    ¬∑ rw [‚Üê pow_mul]
    ¬∑ rw [Real.sq_sqrt Real.pi_nonneg]
  have h‚ÇÉ : Nat.factorial 4 = 24 := by
    decide
  rw [volume_ball, ‚Üê ENNReal.ofReal_pow, ‚Üê ENNReal.ofReal_mul] <;> try positivity
  norm_num
  rw [h‚ÇÅ, h‚ÇÇ, h‚ÇÉ]
  congr 1
  ring_nf

open scoped Topology NNReal
open Asymptotics Filter ENNReal

private lemma aux {Œµ : ‚Ñù‚â•0‚àû} (hŒµ : 0 < Œµ) (hd : 0 < d) :
    ‚àÉ k : ‚Ñù, k ‚â• 0 ‚àß ‚àÄ k' ‚â• k, ENNReal.ofReal ((k' / (k' + 1)) ^ d) ‚àà Set.Icc (1 - Œµ) (1 + Œµ) := by
  -- wtf
  by_cases hŒµ' : Œµ = ‚ä§
  ¬∑ use 0
    subst hŒµ'
    simp
  ¬∑ have : ‚àÉ t : ‚Ñù, 0 < t ‚àß Œµ = ENNReal.ofReal t := by
      obtain ‚ü®‚ü®t, ht_nonneg‚ü©, rfl‚ü© := Option.ne_none_iff_exists'.mp hŒµ'
      rw [ENNReal.some_eq_coe, ENNReal.coe_pos] at hŒµ
      use t, hŒµ, (ENNReal.ofReal_eq_coe_nnreal ht_nonneg).symm
    obtain ‚ü®t, ht_pos, rfl‚ü© := this
    by_cases ht : t ‚â§ 1
    ¬∑ have hd' : (d : ‚Ñù) ‚â† 0 := by rw [ne_eq, Nat.cast_eq_zero]; exact Nat.not_eq_zero_of_lt hd
      let K : ‚Ñù := 1 / (1 - (1 - t) ^ (1 / (d : ‚Ñù))) - 1
      have hK : 0 ‚â§ K := by
        simp_rw [K]
        apply sub_nonneg.mpr
        apply one_le_one_div
        ¬∑ rw [sub_pos]
          apply Real.rpow_lt_one
          ¬∑ linarith
          ¬∑ linarith
          ¬∑ exact one_div_pos.mpr <| Nat.cast_pos'.mpr hd
        ¬∑ rw [sub_le_self_iff]
          apply Real.rpow_nonneg
          linarith
      use K, hK
      intro k' hk'
      have : 1 - 1 / (k' + 1) ‚â• 1 - 1 / (K + 1) := by
        gcongr
      have hK' : (k' / (k' + 1)) ^ d ‚â• 1 - t := calc
        -- (K / (K + 1)) ^ d = (1 - 1 / (K + 1)) ^ d := by
        (k' / (k' + 1)) ^ d = (1 - 1 / (k' + 1)) ^ d := by
          congr
          rw [eq_sub_iff_add_eq, div_add_div_same, div_self]
          linarith
        _ ‚â• (1 - 1 / (K + 1)) ^ d := by
          gcongr
          rw [sub_nonneg, one_div_le, div_one]
          ¬∑ linarith
          ¬∑ linarith
          ¬∑ linarith
        _ = ((1 - t) ^ (1 / (d : ‚Ñù))) ^ d := by simp [K]
        _ = 1 - t := by
          rw [‚Üê Real.rpow_mul_natCast (by linarith), one_div_mul_cancel hd', Real.rpow_one]
      rw [Set.mem_Icc, tsub_le_iff_right, ‚Üê ENNReal.ofReal_add]
      ¬∑ constructor
        ¬∑ apply ENNReal.one_le_ofReal.mpr
          linarith
        ¬∑ trans 1
          ¬∑ apply ENNReal.ofReal_le_one.mpr
            apply pow_le_one
            ¬∑ apply div_nonneg
              ¬∑ linarith
              ¬∑ linarith
            ¬∑ apply (div_le_one _).mpr
              ¬∑ linarith
              ¬∑ linarith
          ¬∑ exact le_self_add
      ¬∑ linarith
      ¬∑ linarith
    ¬∑ use 0, le_refl 0
      intro k' hk'
      have : 0 ‚â§ k' ^ d / (k' + 1) ^ d := by
        apply div_nonneg
        ¬∑ apply pow_nonneg
          linarith
        ¬∑ apply pow_nonneg
          linarith
      have : k' ^ d / (k' + 1) ^ d ‚â§ 1 := by
        apply (div_le_one _).mpr
        ¬∑ apply pow_le_pow_left
          ¬∑ linarith
          ¬∑ linarith
        ¬∑ apply pow_pos
          linarith
      rw [not_le] at ht
      rw [div_pow, Set.mem_Icc, tsub_le_iff_right]
      constructor
      ¬∑ rw [‚Üê ENNReal.ofReal_add, ENNReal.one_le_ofReal]
        ¬∑ linarith
        ¬∑ linarith
        ¬∑ linarith
      ¬∑ trans 1
        ¬∑ exact ENNReal.ofReal_le_one.mpr this
        ¬∑ apply le_self_add

theorem volume_ball_ratio_tendsto_nhds_one {C : ‚Ñù} (hd : 0 < d) (hC : 0 < C) :
    Tendsto (fun R ‚Ü¶ volume (ball (0 : V d) R) / volume (ball (0 : V d) (R + C))) atTop (ùìù 1) := by
  haveI : Nonempty (Fin d) := Fin.pos_iff_nonempty.mp hd
  have (R : ‚Ñù) (hR : 0 ‚â§ R) : volume (ball (0 : V d) R) / volume (ball (0 : V d) (R + C))
      = ENNReal.ofReal (R ^ d / (R + C) ^ d) := by
    rw [volume_ball, volume_ball, Fintype.card_fin, ‚Üê ENNReal.ofReal_pow, ‚Üê ENNReal.ofReal_mul,
      ‚Üê ENNReal.ofReal_pow, ‚Üê ENNReal.ofReal_mul, ‚Üê ENNReal.ofReal_div_of_pos, mul_div_mul_right]
    <;> positivity
  rw [ENNReal.tendsto_atTop (by decide)]
  intro Œµ hŒµ
  obtain ‚ü®k, ‚ü®hk‚ÇÅ, hk‚ÇÇ‚ü©‚ü© := aux hŒµ hd
  use k * C
  intro n hn
  specialize hk‚ÇÇ (n / C) ((le_div_iff hC).mpr hn)
  -- boring
  rw [this]
  ¬∑ convert hk‚ÇÇ
    rw [‚Üê div_pow]
    congr 1
    rw [div_eq_div_iff]
    ¬∑ rw [mul_add, mul_add, ‚Üê mul_div_assoc, mul_one, div_mul_cancel‚ÇÄ, mul_div_right_comm]
      exact hC.ne.symm
    ¬∑ apply ne_of_gt
      calc
        n + C ‚â• k * C + C := by gcongr
        _ > 0 := by positivity
    ¬∑ apply ne_of_gt
      calc
        n / C + 1 ‚â• k * C / C + 1 := by gcongr
        _ = k + 1 := by rw [mul_div_cancel_right‚ÇÄ _ hC.ne.symm]
        _ > 0 := by linarith
  ¬∑ exact (by positivity : 0 ‚â§ k * C).trans hn

end BasicResults
