import Mathlib.Data.Set.Card
import SpherePacking.Basic.SpherePacking

import SpherePacking.ForMathlib.Cardinal
import SpherePacking.ForMathlib.Encard
import SpherePacking.ForMathlib.Zlattice
import SpherePacking.ForMathlib.Bornology

/- In this file, we establish results about density of periodic packings. This roughly corresponds
to Section 2.2, "Bounds on Finite Density of Periodic Packing". -/

/-#
TODO:

* Write the docstrings properly
* Rename lemmas

Vocabulary:

* `Quotient S.addAction.orbitRel`: the type of *representatives* of S.centers ‚ß∏ S.lattice

-/

open scoped ENNReal
open SpherePacking EuclideanSpace MeasureTheory Metric Zspan Bornology

section aux_lemmas

variable {d : ‚Ñï} (S : PeriodicSpherePacking d)
  (D : Set (EuclideanSpace ‚Ñù (Fin d))) (hD_fd : IsAddFundamentalDomain S.lattice D)
  (hD_isBounded : IsBounded D)

lemma aux1 : IsBounded (‚ãÉ x ‚àà S.centers ‚à© D, ball x (S.separation / 2)) := by
  apply isBounded_iff_forall_norm_le.mpr
  obtain ‚ü®L, hL‚ü© := isBounded_iff_forall_norm_le.mp <| hD_isBounded
  use L + S.separation / 2
  intro x hx
  obtain ‚ü®y, s, hy, hy'‚ü© := Set.mem_iUnion.mp hx
  rw [Set.mem_range, exists_prop] at hy
  obtain ‚ü®hy, rfl‚ü© := hy
  rw [mem_ball, dist_eq_norm] at hy'
  specialize hL y hy.right
  exact (norm_le_norm_add_norm_sub' x y).trans (by gcongr)

lemma aux2 (D : Set (EuclideanSpace ‚Ñù (Fin d))) :
    Set.PairwiseDisjoint (S.centers ‚à© D) (fun x ‚Ü¶ ball x (S.separation / 2)) := by
  intro x hx y hy hxy
  apply ball_disjoint_ball
  rw [add_halves]
  exact S.centers_dist' _ _ hx.left hy.left hxy

theorem aux3 {Œπ œÑ : Type*} {s : Set Œπ} {f : Œπ ‚Üí Set (EuclideanSpace ‚Ñù œÑ)} {c : ‚Ñù‚â•0‚àû} (hc : 0 < c)
    [Fintype œÑ] [NoAtoms (volume : Measure (EuclideanSpace ‚Ñù œÑ))]
    (h_measurable : ‚àÄ x ‚àà s, MeasurableSet (f x))
    (h_bounded : IsBounded (‚ãÉ x ‚àà s, f x))
    (h_volume : ‚àÄ x ‚àà s, c ‚â§ volume (f x))
    (h_disjoint : s.PairwiseDisjoint f) :
    s.Finite := by
  clear D hD_fd hD_isBounded S
  wlog h_countable : s.Countable with h_wlog
  ¬∑ by_contra! h_finite
    rw [Set.Countable, ‚Üê Cardinal.mk_le_aleph0_iff, not_le] at h_countable
    -- Brilliant(!!) idea by Etienne Marion on Zulip
    -- If s is uncountable, then we can argue on a countable subset!
    obtain ‚ü®t, ‚ü®ht_subset, ht_aleph0‚ü©‚ü© := Cardinal.le_mk_iff_exists_subset.mp h_countable.le
    have ht_infinite : Infinite t := Cardinal.aleph0_le_mk_iff.mp ht_aleph0.symm.le
    have ht_countable := Cardinal.mk_le_aleph0_iff.mp ht_aleph0.le
    specialize @h_wlog d _ _ t f c hc _ _ ?_ ?_ ?_ ?_ ht_countable
    ¬∑ exact fun x hx ‚Ü¶ h_measurable x (ht_subset hx)
    ¬∑ exact h_bounded.subset <| Set.biUnion_mono ht_subset (by intros; rfl)
    ¬∑ exact fun x hx ‚Ü¶ h_volume x (ht_subset hx)
    ¬∑ exact Set.Pairwise.mono ht_subset h_disjoint
    ¬∑ exact ht_infinite.not_finite h_wlog
  ¬∑ haveI : Countable s := h_countable
    obtain ‚ü®L, hL‚ü© := h_bounded.subset_ball 0
    have h_volume' := volume.mono hL
    rw [OuterMeasure.measureOf_eq_coe, Measure.coe_toOuterMeasure, Set.biUnion_eq_iUnion,
      measure_iUnion] at h_volume'
    ¬∑ have h_le := tsum_mono (f := fun _ ‚Ü¶ c) (g := fun (x : s) ‚Ü¶ volume (f x)) ?_ ?_ ?_
      ¬∑ have h‚ÇÅ := (ENNReal.tsum_const_eq' _ _ ‚ñ∏ h_le).trans h_volume'
        rw [‚Üê Set.encard_lt_top_iff, ‚Üê ENat.toENNReal_lt, ENat.toENNReal_top]
        refine lt_of_le_of_lt ((ENNReal.le_div_iff_mul_le ?_ ?_).mpr h‚ÇÅ) <|
          ENNReal.div_lt_top ?_ hc.ne.symm
        ¬∑ left; positivity
        ¬∑ right; exact (volume_ball_lt_top _).ne
        ¬∑ exact (volume_ball_lt_top _).ne
      ¬∑ exact ENNReal.summable
      ¬∑ exact ENNReal.summable
      ¬∑ intro x
        exact h_volume x.val x.prop
    ¬∑ intro ‚ü®x, hx‚ü© ‚ü®y, hy‚ü© hxy
      exact h_disjoint hx hy (by simpa using hxy)
    ¬∑ exact fun ‚ü®x, hx‚ü© ‚Ü¶ h_measurable x hx

lemma aux4 (hd : 0 < d) : Finite ‚Üë(S.centers ‚à© D) := by
  haveI : Nonempty (Fin d) := Fin.pos_iff_nonempty.mp hd
  apply aux3 (c := volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) (S.separation / 2))) ?_ ?_
      (aux1 S D hD_isBounded)
  ¬∑ intros
    simp [Measure.addHaar_ball_center]
  ¬∑ intro x hx y hy hxy
    apply ball_disjoint_ball
    simpa [add_halves] using S.centers_dist' _ _ hx.left hy.left hxy
  ¬∑ apply volume_ball_pos
    linarith [S.separation_pos]
  ¬∑ intros
    exact measurableSet_ball

lemma aux4' {Œπ : Type*} [Fintype Œπ] (b : Basis Œπ ‚Ñ§ S.lattice) (hd : 0 < d) :
    Finite ‚Üë(S.centers ‚à© fundamentalDomain (b.ofZlatticeBasis ‚Ñù _)) :=
  aux4 S _ (Zspan.fundamentalDomain_isBounded _) hd

open scoped Pointwise in
lemma aux4''
    {Œπ : Type*} [Fintype Œπ] (b : Basis Œπ ‚Ñ§ S.lattice) (hd : 0 < d) (v : EuclideanSpace ‚Ñù (Fin d)) :
    Finite ‚Üë(S.centers ‚à© (v +·µ• fundamentalDomain (b.ofZlatticeBasis ‚Ñù _))) :=
  aux4 S _ (Bornology.isBounded_vadd_set _ _ <| Zspan.fundamentalDomain_isBounded _) hd

end aux_lemmas

section instances
variable {d : ‚Ñï} (S : PeriodicSpherePacking d)
open scoped Pointwise

-- TODO: rename + move
theorem PeriodicSpherePacking.fract_centers
    {Œπ : Type*} [Fintype Œπ] (b : Basis Œπ ‚Ñ§ S.lattice) (s : S.centers) :
    fract (b.ofZlatticeBasis ‚Ñù _) s.val ‚àà S.centers := by
  have := (floor (b.ofZlatticeBasis ‚Ñù _) s).prop
  simp_rw [S.basis_Z_span] at this
  rw [fract_apply, sub_eq_add_neg, add_comm]
  apply S.lattice_action (neg_mem this) s.prop

-- TODO: rename + move
theorem PeriodicSpherePacking.orbitRel_fract
    {Œπ : Type*} [Fintype Œπ] (b : Basis Œπ ‚Ñ§ S.lattice) (a : S.centers) :
    (S.addAction.orbitRel).Rel ‚ü®fract (b.ofZlatticeBasis ‚Ñù _) a, S.fract_centers _ _‚ü© a := by
  rw [AddAction.orbitRel_apply, AddAction.orbit, Set.mem_range]
  refine ‚ü®‚ü®-‚Üë(floor (b.ofZlatticeBasis ‚Ñù _) ‚Üëa), ?_‚ü©, ?_‚ü©
  ¬∑ apply neg_mem
    have := (floor (b.ofZlatticeBasis ‚Ñù _) a.val).prop
    simp_rw [S.basis_Z_span] at this
    exact this
  ¬∑ simp_rw [fract_apply, sub_eq_neg_add]
    rfl

noncomputable def PeriodicSpherePacking.addActionOrbitRelEquiv
    (D : Set (EuclideanSpace ‚Ñù (Fin d))) (hD_unique_covers : ‚àÄ x, ‚àÉ! g : S.lattice, g +·µ• x ‚àà D) :
    Quotient S.addAction.orbitRel ‚âÉ ‚Üë(S.centers ‚à© D) where
  toFun := by
    refine Quotient.lift ?_ ?_
    ¬∑ intro s
      let g := Classical.choose (hD_unique_covers s.val)
      use g.val + s.val, S.lattice_action g.prop s.prop,
        (Classical.choose_spec (hD_unique_covers s.val)).left
    ¬∑ intro ‚ü®u, hu‚ü© ‚ü®v, hv‚ü© h
      change (S.addAction.orbitRel).Rel ‚ü®u, hu‚ü© ‚ü®v, hv‚ü© at h
      rw [AddAction.orbitRel_apply, AddAction.orbit, Set.mem_range] at h
      obtain ‚ü®‚ü®y, hy‚ü©, hy'‚ü© := h
      have : y + v = u := Subtype.ext_iff.mp hy'
      subst this
      have hv' := (Classical.choose_spec (hD_unique_covers v)).right
      simp at hv'
      simp_rw [Subtype.forall, AddSubmonoid.mk_vadd, vadd_eq_add, Subtype.mk.injEq, ‚Üê add_assoc]
      congr 1
      convert Subtype.ext_iff.mp (hv' _ ?_ ?_)
      ¬∑ exact add_mem (SetLike.coe_mem _) hy
      ¬∑ rw [add_assoc]
        have := (Classical.choose_spec (hD_unique_covers (y + v))).left
        -- ew.
        change (Classical.choose _ : S.lattice).val + (y + v) ‚àà D at this
        convert this using 5 with x
        simp [‚Üê add_assoc]
  invFun := fun ‚ü®x, hx‚ü© ‚Ü¶ ‚ü¶‚ü®x, hx.left‚ü©‚üß
  left_inv := by
    apply Quotient.ind
    intro ‚ü®a, ha‚ü©
    simp_rw [Quotient.lift_mk, Quotient.eq]
    change (S.addAction.orbitRel).Rel _ _
    simp_rw [AddAction.orbitRel_apply, AddAction.orbit, Set.mem_range]
    simp [addAction_vadd]
  right_inv := by
    intro ‚ü®x, hx‚ü©
    simp_rw [Quotient.lift_mk, Subtype.mk.injEq, add_left_eq_self]
    obtain ‚ü®g, ‚ü®hg, hg'‚ü©‚ü© := hD_unique_covers x
    trans g.val <;> norm_cast
    ¬∑ apply hg'
      exact (Classical.choose_spec (hD_unique_covers x)).left
    ¬∑ apply (hg' 0 ?_).symm
      simpa using hx.right

noncomputable def PeriodicSpherePacking.addActionOrbitRelEquiv'
    {Œπ : Type*} [Fintype Œπ] (b : Basis Œπ ‚Ñ§ S.lattice) :
    Quotient S.addAction.orbitRel ‚âÉ ‚Üë(S.centers ‚à© (fundamentalDomain (b.ofZlatticeBasis ‚Ñù _))) := by
  refine S.addActionOrbitRelEquiv _ ?_
  intro x
  obtain ‚ü®v, ‚ü®hv, hv'‚ü©‚ü© := exist_unique_vadd_mem_fundamentalDomain (b.ofZlatticeBasis ‚Ñù _) x
  use ‚ü®v.val, ?_‚ü©, ?_, ?_
  ¬∑ apply Set.mem_of_subset_of_mem ?_ v.prop
    rw [‚Üê Submodule.coe_toAddSubgroup, Basis.ofZlatticeBasis_span]
  ¬∑ simp only at hv' ‚ä¢
    convert hv using 1
  ¬∑ intro s hs
    rw [‚Üê hv' ‚ü®s, ?_‚ü© hs]
    apply Set.mem_of_subset_of_mem _ s.prop
    rw [‚Üê Submodule.coe_toAddSubgroup, Basis.ofZlatticeBasis_span]

noncomputable def PeriodicSpherePacking.addActionOrbitRelEquiv''
    {Œπ : Type*} [Fintype Œπ] (b : Basis Œπ ‚Ñ§ S.lattice) (v : EuclideanSpace ‚Ñù (Fin d)) :
    Quotient S.addAction.orbitRel ‚âÉ
      ‚Üë(S.centers ‚à© (v +·µ• fundamentalDomain (b.ofZlatticeBasis ‚Ñù _))) := by
  apply (S.addActionOrbitRelEquiv' b).trans
  exact {
    toFun := fun ‚ü®u, ‚ü®hu_centers, _‚ü©‚ü© ‚Ü¶ by
      use u - floor (b.ofZlatticeBasis ‚Ñù _) (u - v)
      constructor
      ¬∑ rw [sub_eq_neg_add]
        apply S.lattice_action ?_ hu_centers
        apply AddSubgroup.neg_mem
        exact (mem_basis_Z_span ..).mp $ Submodule.coe_mem _
      ¬∑ rw [Set.mem_vadd_set]
        use fract (b.ofZlatticeBasis ‚Ñù _) (u - v), fract_mem_fundamentalDomain _ _, ?_
        rw [fract, vadd_eq_add]
        abel
    invFun := fun ‚ü®u, ‚ü®hu_centers, _‚ü©‚ü© ‚Ü¶ by
      use fract (b.ofZlatticeBasis ‚Ñù _) u
      constructor
      ¬∑ rw [fract, sub_eq_neg_add]
        apply S.lattice_action ?_ hu_centers
        apply AddSubgroup.neg_mem
        exact (mem_basis_Z_span ..).mp $ Submodule.coe_mem _
      ¬∑ exact fract_mem_fundamentalDomain _ _
    left_inv := fun ‚ü®u, ‚ü®hu_centers, hu_fd‚ü©‚ü© ‚Ü¶ by
      simp_rw [Subtype.mk.injEq]
      rw [sub_eq_add_neg, fract_add_zspan]
      ¬∑ exact fract_eq_self.mpr hu_fd
      ¬∑ apply neg_mem
        exact Submodule.coe_mem _
    right_inv := fun ‚ü®u, ‚ü®hu_centers, hu_fd‚ü©‚ü© ‚Ü¶ by
      simp_rw [Subtype.mk.injEq]
      rw [‚Üê EmbeddingLike.apply_eq_iff_eq (b.ofZlatticeBasis ‚Ñù _).repr, map_sub]
      have hu_fd' : u - v ‚àà fundamentalDomain (b.ofZlatticeBasis ‚Ñù _) := by
        rwa [Set.mem_vadd_set_iff_neg_vadd_mem, vadd_eq_add, neg_add_eq_sub] at hu_fd
      ext i
      set b' := b.ofZlatticeBasis ‚Ñù _
      calc
        _ = b'.repr (fract b' u) i - b'.repr (floor b' (u - floor b' u - v)) i := by rfl
        _ = b'.repr (fract b' u) i - b'.repr (floor b' (u - v - floor b' u)) i := by abel_nf
        _ = b'.repr u i - ‚åäb'.repr u i‚åã - (‚åäb'.repr (u - v) i‚åã - ‚åäb'.repr u i‚åã) := by simp
        _ = b'.repr u i - ‚åäb'.repr (u - v) i‚åã := by abel_nf
        _ = b'.repr u i := by
          rw [sub_eq_self, ‚Üê repr_floor_apply, (Zspan.floor_eq_zero ..).mp hu_fd']
          simp
  }

instance (S : PeriodicSpherePacking 0) : Subsingleton S.centers := inferInstance
instance (S : PeriodicSpherePacking 0) : Finite S.centers := inferInstance

noncomputable instance PeriodicSpherePacking.finiteOrbitRelQuotient :
    Finite (Quotient S.addAction.orbitRel) :=
  -- We choose an arbitrary ‚Ñ§-basis of S.lattice
  let b : Basis _ ‚Ñ§ S.lattice := (Zlattice.module_free ‚Ñù S.lattice).chooseBasis
  if hd : 0 < d then
    Finite.of_equiv (h := aux4' S b hd) (S.addActionOrbitRelEquiv' b).symm
  else
    have hd : d = 0 := Nat.eq_zero_of_not_pos hd
    have : Finite S.centers := by subst hd; infer_instance
    inferInstance

noncomputable instance : Fintype (Quotient S.addAction.orbitRel) :=
  Fintype.ofFinite _

end instances

section theorem_2_3
open scoped Pointwise
variable {d : ‚Ñï} (S : PeriodicSpherePacking d)
  (D : Set (EuclideanSpace ‚Ñù (Fin d)))
  (hD_isBounded : IsBounded D)
  (hD_unique_covers : ‚àÄ x, ‚àÉ! g : S.lattice, g +·µ• x ‚àà D)

-- TODO: rename
noncomputable def PeriodicSpherePacking.numReps : ‚Ñï :=
  Fintype.card (Quotient S.addAction.orbitRel)

theorem PeriodicSpherePacking.card_centers_inter_isFundamentalDomain (hd : 0 < d)
    (hD_unique_covers : ‚àÄ x, ‚àÉ! g : S.lattice, g +·µ• x ‚àà D) :
    haveI := @Fintype.ofFinite _ <| aux4 S D hD_isBounded hd
    (S.centers ‚à© D).toFinset.card = S.numReps := by
  rw [numReps]
  convert Finset.card_eq_of_equiv_fintype ?_
  simpa [Set.mem_toFinset] using (S.addActionOrbitRelEquiv D hD_unique_covers).symm

theorem PeriodicSpherePacking.encard_centers_inter_isFundamentalDomain (hd : 0 < d)
    (hD_unique_covers : ‚àÄ x, ‚àÉ! g : S.lattice, g +·µ• x ‚àà D) :
    (S.centers ‚à© D).encard = S.numReps := by
  rw [‚Üê S.card_centers_inter_isFundamentalDomain D hD_isBounded hd hD_unique_covers]
  convert Set.encard_eq_coe_toFinset_card _

theorem PeriodicSpherePacking.card_centers_inter_fundamentalDomain (hd : 0 < d)
    {Œπ : Type*} [Fintype Œπ] (b : Basis Œπ ‚Ñ§ S.lattice) :
    haveI := @Fintype.ofFinite _ <| aux4' S b hd
    (S.centers ‚à© (fundamentalDomain (b.ofZlatticeBasis ‚Ñù _))).toFinset.card = S.numReps := by
  rw [numReps]
  convert Finset.card_eq_of_equiv_fintype ?_
  simpa [Set.mem_toFinset] using (S.addActionOrbitRelEquiv' b).symm

theorem PeriodicSpherePacking.encard_centers_inter_fundamentalDomain (hd : 0 < d)
    {Œπ : Type*} [Fintype Œπ] (b : Basis Œπ ‚Ñ§ S.lattice) :
    (S.centers ‚à© (fundamentalDomain (b.ofZlatticeBasis ‚Ñù _))).encard = S.numReps := by
  rw [‚Üê S.card_centers_inter_fundamentalDomain hd b]
  convert Set.encard_eq_coe_toFinset_card _

theorem PeriodicSpherePacking.card_centers_inter_vadd_fundamentalDomain (hd : 0 < d)
    {Œπ : Type*} [Fintype Œπ] (b : Basis Œπ ‚Ñ§ S.lattice) (v : EuclideanSpace ‚Ñù (Fin d)) :
    haveI := @Fintype.ofFinite _ <| aux4'' S b hd v
    (S.centers ‚à© (v +·µ• fundamentalDomain (b.ofZlatticeBasis ‚Ñù _))).toFinset.card = S.numReps := by
  rw [numReps]
  convert Finset.card_eq_of_equiv_fintype ?_
  simpa [Set.mem_toFinset] using (S.addActionOrbitRelEquiv'' b _).symm

theorem PeriodicSpherePacking.encard_centers_inter_vadd_fundamentalDomain (hd : 0 < d)
    {Œπ : Type*} [Fintype Œπ] (b : Basis Œπ ‚Ñ§ S.lattice) (v : EuclideanSpace ‚Ñù (Fin d)) :
    (S.centers ‚à© (v +·µ• fundamentalDomain (b.ofZlatticeBasis ‚Ñù _))).encard = S.numReps := by
  rw [‚Üê S.card_centers_inter_vadd_fundamentalDomain hd b]
  convert Set.encard_eq_coe_toFinset_card _

theorem aux
    {Œπ : Type*} (b : Basis Œπ ‚Ñù (EuclideanSpace ‚Ñù (Fin d)))
    {L : ‚Ñù} (hL : ‚àÄ x ‚àà fundamentalDomain b, ‚Äñx‚Äñ ‚â§ L) (R : ‚Ñù) :
    ‚ãÉ x ‚àà ‚ÜëS.lattice ‚à© ball (0 : EuclideanSpace ‚Ñù (Fin d)) (R - L),
      x +·µ• (fundamentalDomain b : Set (EuclideanSpace ‚Ñù (Fin d)))
        ‚äÜ ball 0 R := by
  intro x hx
  simp only [Set.mem_iUnion, exists_prop] at hx
  obtain ‚ü®y, ‚ü®_, hy‚ü©, hy'‚ü© := hx
  obtain ‚ü®z, hz, rfl‚ü© := Set.mem_vadd_set.mp hy'
  simp only [mem_ball, dist_zero_right, vadd_eq_add] at hy ‚ä¢
  specialize hL z hz
  calc
    _ ‚â§ ‚Äñy‚Äñ + ‚Äñz‚Äñ := norm_add_le _ _
    _ < (R - L) + L := by linarith
    _ = R := by ring

-- Theorem 2.3, lower bound
theorem PeriodicSpherePacking.aux_ge
    (hd : 0 < d) {Œπ : Type*} [Fintype Œπ] (b : Basis Œπ ‚Ñ§ S.lattice)
    {L : ‚Ñù} (hL : ‚àÄ x ‚àà fundamentalDomain (b.ofZlatticeBasis ‚Ñù _), ‚Äñx‚Äñ ‚â§ L) (R : ‚Ñù) :
    (‚ÜëS.centers ‚à© ball 0 R).encard ‚â•
      S.numReps ‚Ä¢ (‚ÜëS.lattice ‚à© ball (0 : EuclideanSpace ‚Ñù (Fin d)) (R - L)).encard := by
  have := aux S (b.ofZlatticeBasis ‚Ñù _) hL R
  have := Set.inter_subset_inter_right S.centers this
  rw [Set.biUnion_eq_iUnion, Set.inter_iUnion] at this
  have := Set.encard_mono this
  rw [Set.encard_iUnion_of_pairwiseDisjoint] at this
  simp_rw [S.encard_centers_inter_vadd_fundamentalDomain hd] at this
  ¬∑ convert this.ge
    rw [nsmul_eq_mul, ENat.tsum_const_eq', mul_comm]
  ¬∑ intro ‚ü®x, hx‚ü© _ ‚ü®y, hy‚ü© _ hxy
    simp only [Set.disjoint_iff, Set.subset_empty_iff]
    ext u
    rw [Set.mem_inter_iff, Set.mem_empty_iff_false, iff_false, not_and]
    intro ‚ü®_, hux‚ü© ‚ü®_, huy‚ü©
    obtain ‚ü®w, hw, hw_unique‚ü© := exist_unique_vadd_mem_fundamentalDomain (b.ofZlatticeBasis ‚Ñù _) u
    rw [Set.mem_vadd_set_iff_neg_vadd_mem, vadd_eq_add, neg_add_eq_sub] at hux huy
    have hx := hw_unique ‚ü®-x, ?_‚ü© ?_
    have hy := hw_unique ‚ü®-y, ?_‚ü© ?_
    ¬∑ apply hxy
      rw [Subtype.ext_iff, ‚Üê neg_inj]
      exact Subtype.ext_iff.mp (hx.trans hy.symm)
    ¬∑ apply neg_mem
      apply Set.mem_of_subset_of_mem (s‚ÇÅ := S.lattice)
      ¬∑ rw [S.basis_Z_span]
        rfl
      ¬∑ exact hy.left
    ¬∑ simp_rw [AddSubmonoid.mk_vadd, vadd_eq_add, neg_add_eq_sub]
      exact huy
    ¬∑ apply neg_mem
      apply Set.mem_of_subset_of_mem (s‚ÇÅ := S.lattice)
      ¬∑ rw [S.basis_Z_span]
        rfl
      ¬∑ exact hx.left
    ¬∑ simp_rw [AddSubmonoid.mk_vadd, vadd_eq_add, neg_add_eq_sub]
      exact hux

theorem aux'
    {Œπ : Type*} [Fintype Œπ] (b : Basis Œπ ‚Ñ§ S.lattice)
    {L : ‚Ñù} (hL : ‚àÄ x ‚àà fundamentalDomain (b.ofZlatticeBasis ‚Ñù _), ‚Äñx‚Äñ ‚â§ L) (R : ‚Ñù) :
    ball 0 R
      ‚äÜ ‚ãÉ x ‚àà ‚ÜëS.lattice ‚à© ball (0 : EuclideanSpace ‚Ñù (Fin d)) (R + L),
        x +·µ• (fundamentalDomain (b.ofZlatticeBasis ‚Ñù _) : Set (EuclideanSpace ‚Ñù (Fin d))) := by
  intro x hx
  simp only [Set.mem_iUnion, exists_prop]
  use floor (b.ofZlatticeBasis ‚Ñù _) x
  constructor
  ¬∑ constructor
    ¬∑ rw [SetLike.mem_coe, ‚Üê S.mem_basis_Z_span b]
      exact Submodule.coe_mem _
    ¬∑ have : floor (b.ofZlatticeBasis ‚Ñù _) x = x - fract (b.ofZlatticeBasis ‚Ñù _) x := by
        rw [fract]
        abel
      rw [mem_ball_zero_iff] at hx ‚ä¢
      calc
        _ = ‚Äñx - fract (b.ofZlatticeBasis ‚Ñù _) x‚Äñ := congrArg _ this
        _ ‚â§ ‚Äñx‚Äñ + ‚Äñfract (b.ofZlatticeBasis ‚Ñù _) x‚Äñ := norm_sub_le _ _
        _ < R + L := add_lt_add_of_lt_of_le hx (hL _ (fract_mem_fundamentalDomain _ _))
  ¬∑ rw [Set.mem_vadd_set_iff_neg_vadd_mem, vadd_eq_add, neg_add_eq_sub]
    exact fract_mem_fundamentalDomain (b.ofZlatticeBasis ‚Ñù _) x

-- Theorem 2.3, upper bound - the proof is similar to lower bound
theorem PeriodicSpherePacking.aux_le
    (hd : 0 < d) {Œπ : Type*} [Fintype Œπ] (b : Basis Œπ ‚Ñ§ S.lattice)
    {L : ‚Ñù} (hL : ‚àÄ x ‚àà fundamentalDomain (b.ofZlatticeBasis ‚Ñù _), ‚Äñx‚Äñ ‚â§ L) (R : ‚Ñù) :
    (‚ÜëS.centers ‚à© ball 0 R).encard
      ‚â§ S.numReps ‚Ä¢ (‚ÜëS.lattice ‚à© ball (0 : EuclideanSpace ‚Ñù (Fin d)) (R + L)).encard := by
  have := aux' S b hL R
  have := Set.inter_subset_inter_right S.centers this
  rw [Set.biUnion_eq_iUnion, Set.inter_iUnion] at this
  have := Set.encard_mono this
  rw [Set.encard_iUnion_of_pairwiseDisjoint] at this
  simp_rw [S.encard_centers_inter_vadd_fundamentalDomain hd] at this
  ¬∑ convert this
    rw [nsmul_eq_mul, ENat.tsum_const_eq', mul_comm]
  ¬∑ intro ‚ü®x, hx‚ü© _ ‚ü®y, hy‚ü© _ hxy
    simp only [Set.disjoint_iff, Set.subset_empty_iff]
    ext u
    rw [Set.mem_inter_iff, Set.mem_empty_iff_false, iff_false, not_and]
    intro ‚ü®_, hux‚ü© ‚ü®_, huy‚ü©
    obtain ‚ü®w, hw, hw_unique‚ü© := exist_unique_vadd_mem_fundamentalDomain (b.ofZlatticeBasis ‚Ñù _) u
    rw [Set.mem_vadd_set_iff_neg_vadd_mem, vadd_eq_add, neg_add_eq_sub] at hux huy
    have hx := hw_unique ‚ü®-x, ?_‚ü© ?_
    have hy := hw_unique ‚ü®-y, ?_‚ü© ?_
    ¬∑ apply hxy
      rw [Subtype.ext_iff, ‚Üê neg_inj]
      exact Subtype.ext_iff.mp (hx.trans hy.symm)
    ¬∑ apply neg_mem
      apply Set.mem_of_subset_of_mem (s‚ÇÅ := S.lattice)
      ¬∑ rw [S.basis_Z_span]
        rfl
      ¬∑ exact hy.left
    ¬∑ simp_rw [AddSubmonoid.mk_vadd, vadd_eq_add, neg_add_eq_sub]
      exact huy
    ¬∑ apply neg_mem
      apply Set.mem_of_subset_of_mem (s‚ÇÅ := S.lattice)
      ¬∑ rw [S.basis_Z_span]
        rfl
      ¬∑ exact hx.left
    ¬∑ simp_rw [AddSubmonoid.mk_vadd, vadd_eq_add, neg_add_eq_sub]
      exact hux

end theorem_2_3

----------------------------------------------------

section theorem_2_2
open scoped Pointwise
variable {d : ‚Ñï} (S : PeriodicSpherePacking d)
  {Œπ : Type*} [Fintype Œπ] [DecidableEq Œπ]
  (D : Set (EuclideanSpace ‚Ñù (Fin d)))
  -- (hD_fd : IsAddFundamentalDomain S.lattice D)

  -- this strengthens hD_fd.ae_covers
  -- It's not actually necessary but it'll make the proof much much more annoying
  (hD_unique_covers : ‚àÄ x, ‚àÉ! g : S.lattice, g +·µ• x ‚àà D)
  -- this strengthens hD_fd.nullMeasurableSet
  -- i think this is not necessary because `volume` isdefined as the outer measure on non-measurable
  -- sets, so if D is only null measurable we can just take that approximation
  (hD_measurable : MeasurableSet D)

  {L : ‚Ñù} (hL : ‚àÄ x ‚àà D, ‚Äñx‚Äñ ‚â§ L) (R : ‚Ñù)

/- In this section we prove Theorem 2.2 -/

private theorem hD_isAddFundamentalDomain : IsAddFundamentalDomain S.lattice D where
  nullMeasurableSet := hD_measurable.nullMeasurableSet
  ae_covers := Filter.eventually_of_forall fun x ‚Ü¶ (hD_unique_covers x).exists
  aedisjoint := by
    apply Measure.pairwise_aedisjoint_of_aedisjoint_forall_ne_zero
    ¬∑ intro g hg
      apply Disjoint.aedisjoint
      rw [Set.disjoint_iff]
      intro x ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü©
      have ‚ü®y, ‚ü®_, hy_unique‚ü©‚ü© := hD_unique_covers x
      have hy‚ÇÅ := hy_unique 0 (by simpa)
      have hy‚ÇÇ := hy_unique (-g) (Set.mem_vadd_set_iff_neg_vadd_mem.mp hx‚ÇÅ)
      rw [neg_eq_iff_eq_neg.mp hy‚ÇÇ, ‚Üê hy‚ÇÅ] at hg
      norm_num at hg
    ¬∑ exact fun _ ‚Ü¶ quasiMeasurePreserving_add_left _ _

theorem aux7 :
    ball 0 (R - L) ‚äÜ ‚ãÉ x ‚àà ‚ÜëS.lattice ‚à© ball (0 : EuclideanSpace ‚Ñù (Fin d)) R, (x +·µ• D) := by
  intro x hx
  rw [mem_ball_zero_iff] at hx
  obtain ‚ü®g, hg, _‚ü© := hD_unique_covers x
  simp_rw [Set.mem_iUnion, exists_prop, Set.mem_inter_iff]
  refine ‚ü®-g.val, ‚ü®‚ü®?_, ?_‚ü©, ?_‚ü©‚ü©
  ¬∑ simp
  ¬∑ rw [‚Üê norm_neg] at hx
    rw [mem_ball_zero_iff, norm_neg]
    calc
      _ = ‚Äñ(g + x) + (-x)‚Äñ := by congr; abel
      _ ‚â§ ‚Äñ(g + x)‚Äñ + ‚Äñ(-x)‚Äñ := norm_add_le _ _
      _ < L + (R - L) := add_lt_add_of_le_of_lt (hL _ hg) hx
      _ = R := by abel
  ¬∑ rw [Set.mem_vadd_set_iff_neg_vadd_mem, neg_neg]
    exact hg

-- Theorem 2.2, lower bound
theorem PeriodicSpherePacking.aux2_ge (hd : 0 < d)  :
    (‚ÜëS.lattice ‚à© ball (0 : EuclideanSpace ‚Ñù (Fin d)) R).encard
      ‚â• volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) (R - L)) / volume D := by
  rw [ge_iff_le, ENNReal.div_le_iff]
  ¬∑ convert volume.mono <| aux7 S D hD_unique_covers hL R
    rw [OuterMeasure.measureOf_eq_coe, Measure.coe_toOuterMeasure]
    have : Countable ‚ÜëS.lattice := inferInstance
    have : Countable ‚Üë(‚ÜëS.lattice ‚à© ball (0 : EuclideanSpace ‚Ñù (Fin d)) R) :=
      Set.Countable.mono (Set.inter_subset_left) this
    rw [Set.biUnion_eq_iUnion, measure_iUnion]
    ¬∑ rw [tsum_congr fun i ‚Ü¶ measure_vadd .., ENNReal.tsum_const_eq']
    ¬∑ intro ‚ü®x, hx‚ü© ‚ü®y, hy‚ü© hxy
      replace hxy : x ‚â† y := Subtype.ext_iff.ne.mp hxy
      simp_rw [Set.disjoint_iff]
      intro v ‚ü®hxv, hyv‚ü©
      obtain ‚ü®‚ü®z, hz‚ü©, _, hz_unique‚ü© := hD_unique_covers v
      have hx' := hz_unique ‚ü®-x, neg_mem hx.left‚ü© (Set.mem_vadd_set_iff_neg_vadd_mem.mp hxv)
      have hy' := hz_unique ‚ü®-y, neg_mem hy.left‚ü© (Set.mem_vadd_set_iff_neg_vadd_mem.mp hyv)
      replace hx' : x = -z := neg_eq_iff_eq_neg.mp <| Subtype.ext_iff.mp hx'
      replace hy' : y = -z := neg_eq_iff_eq_neg.mp <| Subtype.ext_iff.mp hy'
      exact hxy (hx'.trans hy'.symm)
    ¬∑ intro i
      exact MeasurableSet.const_vadd hD_measurable i.val
  ¬∑ exact (hD_isAddFundamentalDomain S D ‚Äπ_‚Ä∫ ‚Äπ_‚Ä∫).measure_ne_zero (NeZero.ne volume)
  ¬∑ have : Nonempty (Fin d) := Fin.pos_iff_nonempty.mp hd
    rw [‚Üê lt_top_iff_ne_top]
    exact Bornology.IsBounded.measure_lt_top (isBounded_iff_forall_norm_le.mpr ‚ü®L, hL‚ü©)

theorem aux8 :
    ‚ãÉ x ‚àà ‚ÜëS.lattice ‚à© ball (0 : EuclideanSpace ‚Ñù (Fin d)) R, (x +·µ• D) ‚äÜ ball 0 (R + L) := by
  intro x hx
  rw [mem_ball_zero_iff]
  obtain ‚ü®g, _, _‚ü© := hD_unique_covers x
  simp_rw [Set.mem_iUnion, exists_prop, Set.mem_inter_iff] at hx
  obtain ‚ü®i, ‚ü®_, hi_ball‚ü©, hi_fd‚ü© := hx
  rw [mem_ball_zero_iff] at hi_ball
  have := hL (-i + x) (Set.mem_vadd_set_iff_neg_vadd_mem.mp hi_fd)
  calc
    _ = ‚Äñi + (-i + x)‚Äñ := by congr; abel
    _ ‚â§ ‚Äñi‚Äñ + ‚Äñ-i + x‚Äñ := norm_add_le _ _
    _ < R + L := add_lt_add_of_lt_of_le hi_ball this

-- Theorem 2.2, upper bound
theorem PeriodicSpherePacking.aux2_le (hd : 0 < d)  :
    (‚ÜëS.lattice ‚à© ball (0 : EuclideanSpace ‚Ñù (Fin d)) R).encard
      ‚â§ volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) (R + L)) / volume D := by
  rw [ENNReal.le_div_iff_mul_le]
  ¬∑ convert volume.mono <| aux8 S D hD_unique_covers hL R
    rw [OuterMeasure.measureOf_eq_coe, Measure.coe_toOuterMeasure]
    have : Countable ‚ÜëS.lattice := inferInstance
    have : Countable ‚Üë(‚ÜëS.lattice ‚à© ball (0 : EuclideanSpace ‚Ñù (Fin d)) R) :=
      Set.Countable.mono (Set.inter_subset_left) this
    rw [Set.biUnion_eq_iUnion, measure_iUnion]
    ¬∑ rw [tsum_congr fun i ‚Ü¶ measure_vadd .., ENNReal.tsum_const_eq']
    ¬∑ intro ‚ü®x, hx‚ü© ‚ü®y, hy‚ü© hxy
      replace hxy : x ‚â† y := Subtype.ext_iff.ne.mp hxy
      simp_rw [Set.disjoint_iff]
      intro v ‚ü®hxv, hyv‚ü©
      obtain ‚ü®‚ü®z, hz‚ü©, _, hz_unique‚ü© := hD_unique_covers v
      have hx' := hz_unique ‚ü®-x, neg_mem hx.left‚ü© (Set.mem_vadd_set_iff_neg_vadd_mem.mp hxv)
      have hy' := hz_unique ‚ü®-y, neg_mem hy.left‚ü© (Set.mem_vadd_set_iff_neg_vadd_mem.mp hyv)
      replace hx' : x = -z := neg_eq_iff_eq_neg.mp <| Subtype.ext_iff.mp hx'
      replace hy' : y = -z := neg_eq_iff_eq_neg.mp <| Subtype.ext_iff.mp hy'
      exact hxy (hx'.trans hy'.symm)
    ¬∑ intro i
      exact MeasurableSet.const_vadd hD_measurable i.val
  ¬∑ left
    exact (hD_isAddFundamentalDomain S D ‚Äπ_‚Ä∫ ‚Äπ_‚Ä∫).measure_ne_zero (NeZero.ne volume)
  ¬∑ left
    have : Nonempty (Fin d) := Fin.pos_iff_nonempty.mp hd
    rw [‚Üê lt_top_iff_ne_top]
    exact Bornology.IsBounded.measure_lt_top (isBounded_iff_forall_norm_le.mpr ‚ü®L, hL‚ü©)

open Zspan

variable
  {Œπ : Type*} [Fintype Œπ] (b : Basis Œπ ‚Ñ§ S.lattice)
  {L : ‚Ñù} (hL : ‚àÄ x ‚àà fundamentalDomain (b.ofZlatticeBasis ‚Ñù _), ‚Äñx‚Äñ ‚â§ L) (R : ‚Ñù)

-- Theorem 2.2 lower bound, in terms of fundamental domain of Z-lattice
theorem PeriodicSpherePacking.aux2_ge' (hd : 0 < d) :
    (‚ÜëS.lattice ‚à© ball (0 : EuclideanSpace ‚Ñù (Fin d)) R).encard
      ‚â• volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) (R - L))
        / volume (fundamentalDomain (b.ofZlatticeBasis ‚Ñù _)) := by
  refine S.aux2_ge _ ?_ (fundamentalDomain_measurableSet _) hL R hd
  intro x
  obtain ‚ü®‚ü®v, hv‚ü©, hv'‚ü© := exist_unique_vadd_mem_fundamentalDomain (b.ofZlatticeBasis ‚Ñù _) x
  simp only [S.basis_Z_span, AddSubmonoid.mk_vadd] at hv hv' ‚ä¢
  use ‚ü®v, hv‚ü©, hv'.left, ?_
  intro ‚ü®y, hy‚ü© hy'
  have := hv'.right ‚ü®y, ?_‚ü© hy'
  rwa [Subtype.ext_iff] at this ‚ä¢
  rw [S.basis_Z_span]
  exact hy

-- Theorem 2.2 upper bound, in terms of fundamental domain of Z-lattice
theorem PeriodicSpherePacking.aux2_le' (hd : 0 < d) :
    (‚ÜëS.lattice ‚à© ball (0 : EuclideanSpace ‚Ñù (Fin d)) R).encard
      ‚â§ volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) (R + L))
        / volume (fundamentalDomain (b.ofZlatticeBasis ‚Ñù _)) := by
  refine S.aux2_le _ ?_ (fundamentalDomain_measurableSet _) hL R hd
  intro x
  obtain ‚ü®‚ü®v, hv‚ü©, hv'‚ü© := exist_unique_vadd_mem_fundamentalDomain (b.ofZlatticeBasis ‚Ñù _) x
  simp only [S.basis_Z_span, AddSubmonoid.mk_vadd] at hv hv' ‚ä¢
  use ‚ü®v, hv‚ü©, hv'.left, ?_
  intro ‚ü®y, hy‚ü© hy'
  have := hv'.right ‚ü®y, ?_‚ü© hy'
  rwa [Subtype.ext_iff] at this ‚ä¢
  rw [S.basis_Z_span]
  exact hy

section finiteDensity_limit

/- TODO: consider moving this section. -/

open MeasureTheory Measure Metric Zspan

variable
  {d : ‚Ñï} {S : PeriodicSpherePacking d} (hd : 0 < d)
  {Œπ : Type*} [Fintype Œπ] (b : Basis Œπ ‚Ñ§ S.lattice)
  {L : ‚Ñù} (hL : ‚àÄ x ‚àà fundamentalDomain (b.ofZlatticeBasis ‚Ñù _), ‚Äñx‚Äñ ‚â§ L) (R : ‚Ñù)

theorem aux_big_le :
    S.finiteDensity R ‚â§
      S.numReps
        * volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) (S.separation / 2))
          / volume (fundamentalDomain (b.ofZlatticeBasis ‚Ñù _))
            * (volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) (R + S.separation / 2 + L * 2))
              / volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) R)) := calc
  _ ‚â§ (S.centers ‚à© ball 0 (R + S.separation / 2)).encard
      * volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) (S.separation / 2))
        / volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) R) :=
    S.finiteDensity_le hd R
  _ ‚â§ S.numReps
        ‚Ä¢ (‚ÜëS.lattice ‚à© ball (0 : EuclideanSpace ‚Ñù (Fin d)) (R + S.separation / 2 + L)).encard
          * volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) (S.separation / 2))
            / volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) R) := by
    gcongr
    convert ENat.toENNReal_le.mpr (S.aux_le hd b hL _)
    simp
  _ ‚â§ S.numReps
        * (volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) (R + S.separation / 2 + L + L))
          / volume (fundamentalDomain (b.ofZlatticeBasis ‚Ñù _)))
            * volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) (S.separation / 2))
              / volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) R) := by
    rw [nsmul_eq_mul]
    gcongr
    exact S.aux2_le' b hL _ hd
  _ = S.numReps
        * volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) (S.separation / 2))
          / volume (fundamentalDomain (b.ofZlatticeBasis ‚Ñù _))
            * (volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) (R + S.separation / 2 + L * 2))
              / volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) R)) := by
    rw [‚Üê mul_div_assoc, ‚Üê mul_div_assoc, mul_two, ‚Üê add_assoc, ‚Üê ENNReal.mul_div_right_comm,
      ‚Üê ENNReal.mul_div_right_comm, mul_assoc, mul_assoc]
    congr 3
    rw [mul_comm]

theorem aux_big_ge :
    S.finiteDensity R ‚â•
      S.numReps
        * volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) (S.separation / 2))
          / volume (fundamentalDomain (b.ofZlatticeBasis ‚Ñù _))
            * (volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) (R - S.separation / 2 - L * 2))
              / volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) R)) := calc
  _ ‚â• (S.centers ‚à© ball 0 (R - S.separation / 2)).encard
      * volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) (S.separation / 2))
        / volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) R) :=
    S.finiteDensity_ge hd R
  _ ‚â• S.numReps
        ‚Ä¢ (‚ÜëS.lattice ‚à© ball (0 : EuclideanSpace ‚Ñù (Fin d)) (R - S.separation / 2 - L)).encard
          * volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) (S.separation / 2))
            / volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) R) := by
    gcongr
    convert ENat.toENNReal_le.mpr (S.aux_ge hd b hL _)
    simp
  _ ‚â• S.numReps
        * (volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) (R - S.separation / 2 - L - L))
          / volume (fundamentalDomain (b.ofZlatticeBasis ‚Ñù _)))
            * volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) (S.separation / 2))
              / volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) R) := by
    rw [nsmul_eq_mul]
    gcongr
    exact S.aux2_ge' b hL _ hd
  _ = S.numReps
        * volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) (S.separation / 2))
          / volume (fundamentalDomain (b.ofZlatticeBasis ‚Ñù _))
            * (volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) (R - S.separation / 2 - L * 2))
              / volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) R)) := by
    rw [‚Üê mul_div_assoc, ‚Üê mul_div_assoc, mul_two, ‚Üê sub_sub, ‚Üê ENNReal.mul_div_right_comm,
      ‚Üê ENNReal.mul_div_right_comm, mul_assoc, mul_assoc]
    congr 3
    rw [mul_comm]

open Filter Topology

private lemma PeriodicSpherePacking.tendsto_finiteDensity : Tendsto S.finiteDensity atTop
    (ùìù (S.numReps * volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) (S.separation / 2))
      / volume (fundamentalDomain (b.ofZlatticeBasis ‚Ñù _)))) := by
  apply tendsto_of_tendsto_of_tendsto_of_le_of_le ?_ ?_ (aux_big_ge hd b hL) (aux_big_le hd b hL)
  ¬∑ rw [show ‚àÄ a : ENNReal, ùìù a = ùìù (a * 1) by intro; rw [mul_one]]
    apply ENNReal.Tendsto.const_mul
    ¬∑ simp_rw [sub_sub, sub_eq_add_neg]
      convert volume_ball_ratio_tendsto_nhds_one'' hd (C := -(S.separation / 2 + L * 2))
      rw [add_zero]
    ¬∑ left
      exact one_ne_zero
  ¬∑ rw [show ‚àÄ a : ENNReal, ùìù a = ùìù (a * 1) by intro; rw [mul_one]]
    apply ENNReal.Tendsto.const_mul
    ¬∑ simp_rw [add_assoc]
      convert volume_ball_ratio_tendsto_nhds_one'' hd (C := S.separation / 2 + L * 2)
      rw [add_zero]
    ¬∑ left
      exact one_ne_zero

theorem PeriodicSpherePacking.density_eq :
    S.density
      = (S.numReps * volume (ball (0 : EuclideanSpace ‚Ñù (Fin d)) (S.separation / 2))
        / volume (fundamentalDomain (b.ofZlatticeBasis ‚Ñù _))) :=
  limsSup_eq_of_le_nhds (S.tendsto_finiteDensity hd b hL)

#print axioms PeriodicSpherePacking.density_eq
