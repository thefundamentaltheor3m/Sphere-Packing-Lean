/-
Copyright (c) 2025 Sidharth Hariharan. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sidharth Hariharan

M4R File
-/

import SpherePacking.MagicFunction.PolyFourierCoeffBound
import SpherePacking.MagicFunction.a.Basic

/-! # Constructing Upper-Bounds for I‚ÇÜ

The purpose of this file is to construct bounds on the integral `I‚ÇÜ` that is part of the definition
of the function `a`. We follow the proof of Proposition 7.8 in the blueprint.

## TODO:
- Integrability of `g` and `C‚ÇÄ * rexp (-2 * œÄ * s) * rexp (-œÄ * r / s)`
-/

open MagicFunction.Parametrisations MagicFunction.a.RealIntegrals
  MagicFunction.a.RadialFunctions MagicFunction.PolyFourierCoeffBound
open Complex Real Set MeasureTheory MeasureTheory.Measure Filter intervalIntegral
open scoped Function UpperHalfPlane

namespace MagicFunction.a.IntegralEstimates.I‚ÇÜ

variable (r : ‚Ñù)

section Setup

noncomputable def g : ‚Ñù ‚Üí ‚Ñù ‚Üí ‚ÑÇ := fun r t ‚Ü¶ I
  * œÜ‚ÇÄ'' (I * t)
  * cexp (-œÄ * r * t)

lemma I‚ÇÜ'_eq_integral_g_Ioo (r : ‚Ñù) : I‚ÇÜ' r = 2 * ‚à´ t in Ici (1 : ‚Ñù), g r t := by simp [I‚ÇÜ'_eq, g]

end Setup

----------------------------------------------------------------

section Bounding

section Bounding_Integrand

lemma I‚ÇÜ'_bounding_aux_1 (r : ‚Ñù) : ‚àÄ t ‚àà Ici 1, ‚Äñg r t‚Äñ = ‚ÄñœÜ‚ÇÄ'' (I * t)‚Äñ * rexp (-œÄ * r * t) := by
  simp [g, neg_mul, norm_I, one_mul, norm_exp]

lemma I‚ÇÜ'_bounding_aux_2 (r : ‚Ñù) : ‚àÉ C‚ÇÄ > 0, ‚àÄ t ‚àà Ici (1 : ‚Ñù),
    ‚Äñg r t‚Äñ ‚â§ C‚ÇÄ * rexp (-2 * œÄ * t) * rexp (-œÄ * r * t) := by
  obtain ‚ü®C‚ÇÄ, hC‚ÇÄ_pos, hC‚ÇÄ‚ü© := norm_œÜ‚ÇÄ_le -- The `PolyFourierCoeffBound` of `œÜ‚ÇÄ`
  use C‚ÇÄ, hC‚ÇÄ_pos
  intro t ht
  rw [I‚ÇÜ'_bounding_aux_1 r t ht]
  rw [mem_Ici] at ht
  gcongr
  have him : (I * t).im = t := by simp
  have hpos : 0 < (I * t).im := by rw [him]; linarith
  have h_half_lt_one : 1 / 2 < (I * t).im := by rw [him]; linarith
  let z : ‚Ñç := ‚ü®I * t, hpos‚ü©
  have him' : z.im = t := him
  specialize hC‚ÇÄ z h_half_lt_one
  simp only [œÜ‚ÇÄ'', hpos, ‚ÜìreduceDIte]
  simp only [him'] at hC‚ÇÄ
  exact hC‚ÇÄ

end Bounding_Integrand

section Integrability

lemma Bound_integrableOn (r C‚ÇÄ : ‚Ñù) (hr : 0 ‚â§ r) :
  IntegrableOn (fun t ‚Ü¶ C‚ÇÄ * rexp (-2 * œÄ * t) * rexp (-œÄ * r * t)) (Ici (1 : ‚Ñù)) volume := by
  have hb_pos : 0 < œÄ * (r + 2) := mul_pos Real.pi_pos (by linarith)
  have h0 : IntegrableOn (fun t : ‚Ñù => rexp (-(œÄ * (r + 2)) * t)) (Ioi (1 : ‚Ñù)) := by
    simpa using exp_neg_integrableOn_Ioi (a := (1 : ‚Ñù)) (b := œÄ * (r + 2)) hb_pos
  have h1 : IntegrableOn (fun t : ‚Ñù => C‚ÇÄ * rexp (-(œÄ * (r + 2)) * t)) (Ioi (1 : ‚Ñù)) :=
    h0.const_mul C‚ÇÄ
  have h_eq (t : ‚Ñù) :
      C‚ÇÄ * rexp (-(œÄ * (r + 2)) * t) =
        C‚ÇÄ * rexp (-2 * œÄ * t) * rexp (-œÄ * r * t) := by
    have ht : -(œÄ * (r + 2)) * t = (-2 * œÄ * t) + (-œÄ * r * t) := by ring_nf
    simp [ht, Real.exp_add, mul_comm, mul_left_comm]
  have h2 : IntegrableOn (fun t : ‚Ñù => C‚ÇÄ * rexp (-2 * œÄ * t) * rexp (-œÄ * r * t))
        (Ioi (1 : ‚Ñù)) volume := by
    refine h1.congr_fun ?_ measurableSet_Ioi
    intro t _
    exact h_eq t
  exact
    (integrableOn_Ici_iff_integrableOn_Ioi
        (Œº := (volume : Measure ‚Ñù))
        (f := fun t : ‚Ñù => C‚ÇÄ * rexp (-2 * œÄ * t) * rexp (-œÄ * r * t))
        (b := (1 : ‚Ñù))).2
      h2

end Integrability

section Bounding_Integral

lemma I‚ÇÜ'_bounding_aux_3 (r : ‚Ñù) (hr : 0 ‚â§ r) : ‚àÉ C‚ÇÄ > 0,
    ‚à´ t in Ici (1 : ‚Ñù), ‚Äñg r t‚Äñ ‚â§
    ‚à´ t in Ici (1 : ‚Ñù), C‚ÇÄ * rexp (-2 * œÄ * t) * rexp (-œÄ * r * t) := by
  wlog hint : IntegrableOn (fun t ‚Ü¶ ‚Äñg r t‚Äñ) (Ici (1 : ‚Ñù)) volume
  ¬∑ refine ‚ü®1, by positivity, ?_‚ü©
    haveI h‚ÇÅ : CompleteSpace ‚Ñù := inferInstance
    have h‚ÇÇ : ¬¨ (Integrable (fun t ‚Ü¶ ‚Äñg r t‚Äñ) (volume.restrict (Ici 1))) := hint
    conv_lhs => simp only [integral, h‚ÇÅ, h‚ÇÇ, ‚ÜìreduceDIte]
    positivity
  obtain ‚ü®C‚ÇÄ, hC‚ÇÄ_pos, hC‚ÇÄ‚ü© := I‚ÇÜ'_bounding_aux_2 r
  use C‚ÇÄ, hC‚ÇÄ_pos
  exact setIntegral_mono_on hint (Bound_integrableOn r C‚ÇÄ hr) measurableSet_Ici hC‚ÇÄ

theorem I‚ÇÜ'_bounding (r : ‚Ñù) (hr : 0 ‚â§ r) : ‚àÉ C‚ÇÅ > 0,
    ‚ÄñI‚ÇÜ' r‚Äñ ‚â§ ‚à´ t in Ici (1 : ‚Ñù), C‚ÇÅ * rexp (-2 * œÄ * t) * rexp (-œÄ * r * t) := by
  obtain ‚ü®C‚ÇÄ, hC‚ÇÄ_pos, hC‚ÇÄ‚ü© := I‚ÇÜ'_bounding_aux_3 r hr
  refine ‚ü®2 * C‚ÇÄ, by positivity, ?_‚ü©
  calc
  _ = ‚Äñ2 * ‚à´ t in Ici (1 : ‚Ñù), g r t‚Äñ := by simp only [I‚ÇÜ'_eq_integral_g_Ioo, g]
  _ ‚â§ 2 * ‚à´ t in Ici (1 : ‚Ñù), ‚Äñg r t‚Äñ := by
      simp only [norm_mul, Complex.norm_ofNat, Nat.ofNat_pos, mul_le_mul_iff_right‚ÇÄ]
      exact norm_integral_le_integral_norm (g r)
  _ ‚â§ 2 * ‚à´ t in Ici (1 : ‚Ñù), C‚ÇÄ * rexp (-2 * œÄ * t) * rexp (-œÄ * r * t) := by gcongr
  _ = _ := by
      rw [‚Üê smul_eq_mul, ‚Üê MeasureTheory.integral_smul (2 : ‚Ñù)]
      congr; ext t
      rw [smul_eq_mul]
      ac_rfl

theorem I‚ÇÜ'_bounding_eq (r : ‚Ñù) (hr : 0 ‚â§ r) : ‚àÉ C‚ÇÇ > 0,
    ‚ÄñI‚ÇÜ' r‚Äñ ‚â§ C‚ÇÇ * rexp (-œÄ * (r ^ 2 + 2)) / (r ^ 2 + 2) :=
by
  obtain ‚ü®C‚ÇÅ, _, hC‚ÇÅ‚ü© := I‚ÇÜ'_bounding r hr
  let A : ‚Ñù := ‚à´ t in Ici (1 : ‚Ñù), C‚ÇÅ * rexp (-2 * œÄ * t) * rexp (-œÄ * r * t)
  let K : ‚Ñù := rexp (-œÄ * (r ^ 2 + 2)) / (r ^ 2 + 2)
  have hKpos : 0 < K := by
    have hden : 0 < r ^ 2 + 2 := by
      have : 0 ‚â§ r ^ 2 := by simpa using sq_nonneg r
      linarith
    simpa [K] using div_pos (Real.exp_pos _) hden
  refine ‚ü®|A| / K + 1, add_pos_of_nonneg_of_pos (div_nonneg (abs_nonneg _) hKpos.le) one_pos, ?_‚ü©
  have h : ‚ÄñI‚ÇÜ' r‚Äñ ‚â§ (|A| / K + 1) * K := by
    have hKne : K ‚â† 0 := ne_of_gt hKpos
    calc
      ‚ÄñI‚ÇÜ' r‚Äñ ‚â§ A := hC‚ÇÅ
      _ ‚â§ |A| + K := (le_abs_self A).trans (le_add_of_nonneg_right hKpos.le)
      _ = (|A| / K + 1) * K := by
        have h1 : (|A| / K) * K = |A| := by field_simp [div_eq_mul_inv, hKne]
        have h2 : (|A| / K + 1) * K = (|A| / K) * K + 1 * K := by ring
        simp [h2, h1]
  simpa [K, A, mul_div_assoc] using h

end Bounding_Integral

end Bounding

----------------------------------------------------------------

section Higher_iteratedFDerivs



end Higher_iteratedFDerivs

----------------------------------------------------------------

noncomputable section Schwartz_Decay

open SchwartzMap

section Zeroth_Derivative

theorem decay'‚ÇÄ : ‚àÄ (k : ‚Ñï), ‚àÉ C, ‚àÄ (x : ‚Ñù), ‚Äñx‚Äñ ^ k * ‚ÄñI‚ÇÜ' x‚Äñ ‚â§ C := by

  sorry

end Zeroth_Derivative

section Higher_iteratedFDerivs

theorem decay' : ‚àÄ (k n : ‚Ñï), ‚àÉ C, ‚àÄ (x : ‚Ñù), ‚Äñx‚Äñ ^ k * ‚ÄñiteratedFDeriv ‚Ñù n I‚ÇÜ' x‚Äñ ‚â§ C := by

  sorry

end Higher_iteratedFDerivs

-- def I‚ÇÜ'_Schwartz : ùì¢(‚Ñù, ‚ÑÇ) where
-- toFun := I‚ÇÜ'
-- smooth' := sorry
-- decay' := by extract_goal; sorry

end Schwartz_Decay

end I‚ÇÜ

end IntegralEstimates

end a

end MagicFunction
