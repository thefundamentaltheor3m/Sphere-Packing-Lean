/-
Copyright (c) 2025 Sidharth Hariharan. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sidharth Hariharan

M4R File
-/

module
public import Mathlib.Analysis.Complex.CauchyIntegral
public import Mathlib.MeasureTheory.Integral.IntegralEqImproper
public import Mathlib.Topology.EMetricSpace.Paracompact
public import Mathlib.Topology.Separation.CompletelyRegular

/-! # Deforming Paths of Integration for Open Contours

In this file, we prove that if a function tends to zero as the imaginary part of its input tends to
infinity and satisfies Cauchy-Goursat-type conditions, then we can deform paths of integration along
rectangular contours that extend infinitely in the vertical direction.
-/

namespace Complex

open Set Real intervalIntegral Metric Filter MeasureTheory
open scoped Interval Topology

section aux

@[simp] theorem re_of_real_add_real_mul_I (x y : â„) : (x + y * I).re = x := by simp

/-- Imaginary part of `x + y * I` is `y`. -/
@[simp] public theorem im_of_real_add_real_mul_I (x y : â„) : (x + y * I).im = y := by simp

end aux

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„‚ E] {f : â„‚ â†’ E} {xâ‚ xâ‚‚ : â„} (y : â„)

section Tendsto_Zero

/-- If $f(z) \to 0$ as $\Im(z) \to \infty$, then
  $\lim_{m \to \infty} \int_{x_1}^{x_2} f(x + mI) dx = 0$. -/
lemma tendsto_integral_atTop_nhds_zero_of_tendsto_im_atTop_nhds_zero
    (htendsto : âˆ€ Îµ > 0, âˆƒ M : â„, âˆ€ z : â„‚, M â‰¤ z.im â†’ â€–f zâ€– < Îµ) :
    Tendsto (fun (m : â„) â†¦ âˆ« (x : â„) in xâ‚..xâ‚‚, f (x + m * I)) atTop (ğ“ 0) := by
  by_cases h : xâ‚ = xâ‚‚
  Â· subst h; simp
  simp only [NormedAddCommGroup.tendsto_nhds_zero, eventually_atTop, ge_iff_le]
  intro Îµ hÎµ
  have hx : 0 < |xâ‚‚ - xâ‚| := abs_sub_pos.mpr (ne_comm.mp h)
  obtain âŸ¨M, hMâŸ© :=
    htendsto ((1 / 2 : â„) * (Îµ / |xâ‚‚ - xâ‚|)) (mul_pos (by norm_num) (div_pos hÎµ hx))
  use M
  intro m hm
  calc â€–âˆ« (x : â„) in xâ‚..xâ‚‚, f (x + m * I)â€–
  _ â‰¤ ((1 / 2) * (Îµ / |xâ‚‚ - xâ‚|)) * |xâ‚‚ - xâ‚| :=
      intervalIntegral.norm_integral_le_of_norm_le_const <| by
      intro x hx'
      exact le_of_lt <| hM (x + m * I) (by simpa using hm)
  _ = (1 / 2) * Îµ := by
      have hx0 : (|xâ‚‚ - xâ‚| : â„) â‰  0 := ne_of_gt hx
      field_simp [mul_assoc, hx0]
  _ < Îµ := by linarith

end Tendsto_Zero

section Eventually_Eq_Zero

lemma hzero (hcont : ContinuousOn f ([[xâ‚, xâ‚‚]] Ã—â„‚ (Ici y))) (s : Set â„‚) (hs : s.Countable)
    (hdiff : âˆ€ x âˆˆ ((Ioo (min xâ‚ xâ‚‚) (max xâ‚ xâ‚‚)) Ã—â„‚ (Ioi y)) \ s, DifferentiableAt â„‚ f x) :
    âˆ€ m â‰¥ y, (âˆ« (x : â„) in xâ‚..xâ‚‚, f (x + y * I)) - (âˆ« (x : â„) in xâ‚..xâ‚‚, f (x + m * I))
      + (I â€¢ âˆ« (t : â„) in y..m, f (xâ‚‚ + t * I)) - (I â€¢ âˆ« (t : â„) in y..m, f (xâ‚ + t * I))
    = 0 := by
  intro m hm
  simpa [re_of_real_add_real_mul_I, im_of_real_add_real_mul_I] using
    (Complex.integral_boundary_rect_eq_zero_of_differentiable_on_off_countable f (xâ‚ + y * I)
      (xâ‚‚ + m * I) s hs
      (by
        refine hcont.mono ?_
        rw [reProdIm_subset_iff]
        gcongr
        Â· simp
        Â· simpa [re_of_real_add_real_mul_I, im_of_real_add_real_mul_I, uIcc_of_le hm] using
            (Icc_subset_Ici_self : Icc y m âŠ† Ici y))
      (by
        intro z hz
        rcases (by
          simpa [re_of_real_add_real_mul_I, im_of_real_add_real_mul_I] using hz :
            z âˆˆ (Ioo (min xâ‚ xâ‚‚) (max xâ‚ xâ‚‚) Ã—â„‚ Ioo (min y m) (max y m)) âˆ§ z âˆ‰ s) with âŸ¨hz, hznsâŸ©
        refine hdiff z âŸ¨?_, hznsâŸ©
        rw [mem_reProdIm] at hz âŠ¢
        refine âŸ¨hz.1, ?_âŸ©
        have hz_im : y < z.im := by
          have hz_im := (mem_Ioo.1 hz.2).1
          simpa [min_eq_left hm] using hz_im
        exact hz_im))

/-- A direct consequence of the **Cauchy-Goursat Theorem for rectangles**: given the conditions of
the Cauchy-Goursat Theorem between two vertical lines in the Complex plane, fixing some `y`, the
integral around rectangles bounded by these vertical lines, the horizontal line with imaginary
part `y`, and a horizontal line with imaginary part `m` is eventually equal to `0`.

By Cauchy-Goursat, it is immediate that this is true when `m â‰¥ y`. Indeed, the contents of this
lemma are not particularly nontrivial. The point is to state this fact using `eventually` result so
it will be compatible with `tendsto_congr'`, which is useful for applications. -/
lemma integral_boundary_rect_eq_zero_eventually_atTop_of_differentiable_on_off_countable
    (hcont : ContinuousOn f ([[xâ‚, xâ‚‚]] Ã—â„‚ (Ici y))) (s : Set â„‚) (hs : s.Countable)
    (hdiff : âˆ€ x âˆˆ ((Ioo (min xâ‚ xâ‚‚) (max xâ‚ xâ‚‚)) Ã—â„‚ (Ioi y)) \ s, DifferentiableAt â„‚ f x) :
    (fun (m : â„) â†¦
      (âˆ« (x : â„) in xâ‚..xâ‚‚, f (x + y * I))
        - (âˆ« (x : â„) in xâ‚..xâ‚‚, f (x + m * I))
        + (I â€¢ âˆ« (t : â„) in y..m, f (xâ‚‚ + t * I))
        - (I â€¢ âˆ« (t : â„) in y..m, f (xâ‚ + t * I)))
    =á¶ [atTop] (fun (_ : â„) â†¦ 0) := by
  filter_upwards [eventually_ge_atTop y] with m hm
  exact hzero y hcont s hs hdiff m hm

end Eventually_Eq_Zero

section Contour_Deformation_Tensdsto

/-- **Deformation of open rectangular contours:** Given two infinite vertical contours such that a
function satisfies Cauchy-Goursat conditions between them, interval integrals of increasing interval
length along the first contour tend to the sum of a translation integral and the limit of interval
integrals along the second integral.

We call this a deformation of _open rectangular contours_ because it allows us to change contours
when working with contours that look like "infinite boxes without lids"---that is, rectangular
contours that are "open" at the top (we do not mean open in a topological sense). -/
theorem tendsto_integral_boundary_open_rect_one_side_atTop_nhds_sum_other_two_sides
    (hcont : ContinuousOn f ([[xâ‚, xâ‚‚]] Ã—â„‚ (Ici y))) (s : Set â„‚) (hs : s.Countable)
    (hdiff : âˆ€ x âˆˆ ((Ioo (min xâ‚ xâ‚‚) (max xâ‚ xâ‚‚)) Ã—â„‚ (Ioi y)) \ s, DifferentiableAt â„‚ f x)
    {Câ‚‚ : E} (hCâ‚‚ : Tendsto (fun m â†¦ I â€¢ âˆ« (t : â„) in y..m, f (xâ‚‚ + t * I)) atTop (ğ“ Câ‚‚))
    (htendsto : âˆ€ Îµ > 0, âˆƒ M : â„, âˆ€ z : â„‚, M â‰¤ z.im â†’ â€–f zâ€– < Îµ) :
    Tendsto (fun m â†¦ I â€¢ âˆ« (t : â„) in y..m, f (xâ‚ + t * I)) atTop <|
      ğ“ ((âˆ« (t : â„) in xâ‚..xâ‚‚, f (t + y * I)) + Câ‚‚) := by
  have heventually : (fun (m : â„) â†¦
      (âˆ« (x : â„) in xâ‚..xâ‚‚, f (x + y * I))
        - (âˆ« (x : â„) in xâ‚..xâ‚‚, f (x + m * I))
        + (I â€¢ âˆ« (t : â„) in y..m, f (xâ‚‚ + t * I)))
      =á¶ [atTop] (fun m â†¦ I â€¢ âˆ« (t : â„) in y..m, f (xâ‚ + t * I)) := by
    filter_upwards [eventually_ge_atTop y] with m hm
    rw [â† sub_eq_zero, â† (hzero y hcont s hs hdiff m hm)]
  rw [tendsto_congr' heventually.symm, â† sub_zero (âˆ« (t : â„) in xâ‚..xâ‚‚, f (â†‘t + â†‘y * I))]
  refine (Tendsto.sub ?_ ?_).add hCâ‚‚
  Â· rw [sub_zero, tendsto_const_nhds_iff]
  Â· exact tendsto_integral_atTop_nhds_zero_of_tendsto_im_atTop_nhds_zero htendsto

/-- **Deformation of open rectangular contours:** Given two infinite vertical contours such that a
function satisfies Cauchy-Goursat conditions between them, the limit of interval integrals along the
first contour equals the sum of a translation integral and the limit of interval integrals along
the second integral. -/
theorem integral_boundary_open_rect_eq_zero_of_differentiable_on_off_countable
    (hcont : ContinuousOn f ([[xâ‚, xâ‚‚]] Ã—â„‚ (Ici y))) (s : Set â„‚) (hs : s.Countable)
    (hdiff : âˆ€ x âˆˆ ((Ioo (min xâ‚ xâ‚‚) (max xâ‚ xâ‚‚)) Ã—â„‚ (Ioi y)) \ s, DifferentiableAt â„‚ f x)
    {Câ‚ : E} (hCâ‚ : Tendsto (fun m â†¦ I â€¢ âˆ« (t : â„) in y..m, f (xâ‚ + t * I)) atTop (ğ“ Câ‚))
    {Câ‚‚ : E} (hCâ‚‚ : Tendsto (fun m â†¦ I â€¢ âˆ« (t : â„) in y..m, f (xâ‚‚ + t * I)) atTop (ğ“ Câ‚‚))
    (htendsto : âˆ€ Îµ > 0, âˆƒ M : â„, âˆ€ z : â„‚, M â‰¤ z.im â†’ â€–f zâ€– < Îµ) :
    (âˆ« (t : â„) in xâ‚..xâ‚‚, f (t + y * I)) + Câ‚‚ - Câ‚ = 0 := by
  exact sub_eq_zero.2 <|
    (tendsto_nhds_unique hCâ‚ <|
        tendsto_integral_boundary_open_rect_one_side_atTop_nhds_sum_other_two_sides
          y hcont s hs hdiff hCâ‚‚ htendsto).symm

/-- **Deformation of open rectangular contours:** Given two infinite vertical contours such that a
function satisfies Cauchy-Goursat conditions between them, the limit of interval integrals along the
first contour equals the sum of a translation integral and the limit of interval integrals along
the second integral.

This is a variant of `integral_boundary_open_rect_eq_zero_of_differentiable_on_off_countable`. The
sole difference is that the assumptions in this lemma do not include the factor of `I` that comes
from contour parametrisation. The reason we state this version is that it might be more convenient
to use in certain cases.
-/
theorem integral_boundary_open_rect_eq_zero_of_differentiable_on_off_countable'
    (hcont : ContinuousOn f ([[xâ‚, xâ‚‚]] Ã—â„‚ (Ici y))) (s : Set â„‚) (hs : s.Countable)
    (hdiff : âˆ€ x âˆˆ ((Ioo (min xâ‚ xâ‚‚) (max xâ‚ xâ‚‚)) Ã—â„‚ (Ioi y)) \ s, DifferentiableAt â„‚ f x)
    {Câ‚ : E} (hCâ‚ : Tendsto (fun m â†¦ âˆ« (t : â„) in y..m, f (xâ‚ + t * I)) atTop (ğ“ Câ‚))
    {Câ‚‚ : E} (hCâ‚‚ : Tendsto (fun m â†¦ âˆ« (t : â„) in y..m, f (xâ‚‚ + t * I)) atTop (ğ“ Câ‚‚))
    (htendsto : âˆ€ Îµ > 0, âˆƒ M : â„, âˆ€ z : â„‚, M â‰¤ z.im â†’ â€–f zâ€– < Îµ) :
    (âˆ« (t : â„) in xâ‚..xâ‚‚, f (t + y * I)) + (I â€¢ Câ‚‚) - (I â€¢ Câ‚) = 0 :=
  integral_boundary_open_rect_eq_zero_of_differentiable_on_off_countable y hcont s hs hdiff
    (hCâ‚.const_smul I) (hCâ‚‚.const_smul I) htendsto

end Contour_Deformation_Tensdsto

section Contour_Deformation_of_Integrable_along_BOTH

/-- **Deformation of open rectangular contours:** Given two infinite vertical contours such that a
function satisfies Cauchy-Goursat conditions between them and is integrable along both vertical
contours, the improper integral along the first contour equals the sum of a translation integral
and the improper integrals along the second integral.

This is a variant of `integral_boundary_open_rect_eq_zero_of_differentiable_on_off_countable'` that
requires the much stronger assumption of integrability. The reason integrability is stronger is that
it requires the integral of the norm of the function to be finite rather than just that of the
function. We nevertheless include this version of the theorem because it is likely that in
applications involving specific functions, there will already be proofs of integrability.
-/
public theorem
    integral_boundary_open_rect_eq_zero_of_differentiable_on_off_countable_of_integrable_on
    (hcont : ContinuousOn f ([[xâ‚, xâ‚‚]] Ã—â„‚ (Ici y))) (s : Set â„‚) (hs : s.Countable)
    (hdiff : âˆ€ x âˆˆ ((Ioo (min xâ‚ xâ‚‚) (max xâ‚ xâ‚‚)) Ã—â„‚ (Ioi y)) \ s, DifferentiableAt â„‚ f x)
    (hintâ‚ : IntegrableOn (fun (t : â„) â†¦ f (xâ‚ + t * I)) (Ioi y) volume)
    (hintâ‚‚ : IntegrableOn (fun (t : â„) â†¦ f (xâ‚‚ + t * I)) (Ioi y) volume)
    (htendsto : âˆ€ Îµ > 0, âˆƒ M : â„, âˆ€ z : â„‚, M â‰¤ z.im â†’ â€–f zâ€– < Îµ) :
    (âˆ« (x : â„) in xâ‚..xâ‚‚, f (x + y * I)) + (I â€¢ âˆ« (t : â„) in Ioi y, f (xâ‚‚ + t * I))
      - (I â€¢ âˆ« (t : â„) in Ioi y, f (xâ‚ + t * I)) = 0 := by
  refine integral_boundary_open_rect_eq_zero_of_differentiable_on_off_countable' y hcont s hs hdiff
    (intervalIntegral_tendsto_integral_Ioi y hintâ‚ tendsto_id)
    (intervalIntegral_tendsto_integral_Ioi y hintâ‚‚ tendsto_id) htendsto

end Contour_Deformation_of_Integrable_along_BOTH

section Contour_Deformation_Tendsto_Top

/-- Finite-rectangle deformation on a bounded strip.

This is a convenience lemma for applications where one already knows directly that the top-edge
interval integral tends to `0` as the height tends to `âˆ`. -/
public theorem rect_deform_of_tendsto_top {f : â„‚ â†’ E} {xâ‚ xâ‚‚ y : â„}
    (hcont : ContinuousOn f (Set.uIcc xâ‚ xâ‚‚ Ã—â„‚ Set.Ici y))
    (hdiff :
      âˆ€ z âˆˆ (Set.Ioo (min xâ‚ xâ‚‚) (max xâ‚ xâ‚‚) Ã—â„‚ Set.Ioi y), DifferentiableAt â„‚ f z)
    (hintâ‚ :
      IntegrableOn (fun t : â„ => f ((xâ‚ : â„‚) + (t : â„‚) * I)) (Set.Ioi y) volume)
    (hintâ‚‚ :
      IntegrableOn (fun t : â„ => f ((xâ‚‚ : â„‚) + (t : â„‚) * I)) (Set.Ioi y) volume)
    (htop :
      Tendsto (fun m : â„ => âˆ« x in xâ‚..xâ‚‚, f ((x : â„‚) + (m : â„‚) * I)) atTop (ğ“ 0)) :
    (âˆ« x in xâ‚..xâ‚‚, f ((x : â„‚) + (y : â„‚) * I)) +
          (I â€¢ âˆ« t in Set.Ioi y, f ((xâ‚‚ : â„‚) + (t : â„‚) * I)) -
            (I â€¢ âˆ« t in Set.Ioi y, f ((xâ‚ : â„‚) + (t : â„‚) * I)) = 0 := by
  have hrect :
      âˆ€ m : â„, y â‰¤ m â†’
        (âˆ« x in xâ‚..xâ‚‚, f ((x : â„‚) + (y : â„‚) * I)) -
              (âˆ« x in xâ‚..xâ‚‚, f ((x : â„‚) + (m : â„‚) * I)) +
            (I â€¢ âˆ« t in y..m, f ((xâ‚‚ : â„‚) + (t : â„‚) * I)) -
              (I â€¢ âˆ« t in y..m, f ((xâ‚ : â„‚) + (t : â„‚) * I)) = 0 := by
    intro m hm
    have hdiff' :
        âˆ€ z âˆˆ ((Set.Ioo (min xâ‚ xâ‚‚) (max xâ‚ xâ‚‚) Ã—â„‚ Set.Ioi y) \ (âˆ… : Set â„‚)),
          DifferentiableAt â„‚ f z := by
      intro z hz
      exact hdiff z (by simpa using hz.1)
    simpa using
      (Complex.hzero (f := f) (xâ‚ := xâ‚) (xâ‚‚ := xâ‚‚) (y := y) hcont (s := (âˆ… : Set â„‚))
        (hs := by simp) hdiff' m hm)
  have hEq :
      (fun m : â„ =>
          (âˆ« x in xâ‚..xâ‚‚, f ((x : â„‚) + (y : â„‚) * I)) +
              (I â€¢ âˆ« t in y..m, f ((xâ‚‚ : â„‚) + (t : â„‚) * I)) -
                (I â€¢ âˆ« t in y..m, f ((xâ‚ : â„‚) + (t : â„‚) * I))) =á¶ [atTop]
        fun m : â„ => âˆ« x in xâ‚..xâ‚‚, f ((x : â„‚) + (m : â„‚) * I) := by
    filter_upwards [eventually_ge_atTop y] with m hm
    have h0 := hrect m hm
    have h1 :=
      congrArg (fun z : E => z + âˆ« x in xâ‚..xâ‚‚, f ((x : â„‚) + (m : â„‚) * I)) h0
    simpa [sub_eq_add_neg, add_assoc, add_left_comm, add_comm] using h1
  have hF0 :
      Tendsto
          (fun m : â„ =>
            (âˆ« x in xâ‚..xâ‚‚, f ((x : â„‚) + (y : â„‚) * I)) +
                (I â€¢ âˆ« t in y..m, f ((xâ‚‚ : â„‚) + (t : â„‚) * I)) -
                  (I â€¢ âˆ« t in y..m, f ((xâ‚ : â„‚) + (t : â„‚) * I))) atTop (ğ“ 0) :=
    (tendsto_congr' hEq).2 htop
  have hVâ‚ :
      Tendsto (fun m : â„ => âˆ« t in y..m, f ((xâ‚ : â„‚) + (t : â„‚) * I)) atTop
        (ğ“ (âˆ« t in Set.Ioi y, f ((xâ‚ : â„‚) + (t : â„‚) * I))) :=
    intervalIntegral_tendsto_integral_Ioi y hintâ‚ tendsto_id
  have hVâ‚‚ :
      Tendsto (fun m : â„ => âˆ« t in y..m, f ((xâ‚‚ : â„‚) + (t : â„‚) * I)) atTop
        (ğ“ (âˆ« t in Set.Ioi y, f ((xâ‚‚ : â„‚) + (t : â„‚) * I))) :=
    intervalIntegral_tendsto_integral_Ioi y hintâ‚‚ tendsto_id
  have hFlim :
      Tendsto
          (fun m : â„ =>
            (âˆ« x in xâ‚..xâ‚‚, f ((x : â„‚) + (y : â„‚) * I)) +
                (I â€¢ âˆ« t in y..m, f ((xâ‚‚ : â„‚) + (t : â„‚) * I)) -
                  (I â€¢ âˆ« t in y..m, f ((xâ‚ : â„‚) + (t : â„‚) * I))) atTop
        (ğ“ ((âˆ« x in xâ‚..xâ‚‚, f ((x : â„‚) + (y : â„‚) * I)) +
              (I â€¢ âˆ« t in Set.Ioi y, f ((xâ‚‚ : â„‚) + (t : â„‚) * I)) -
                (I â€¢ âˆ« t in Set.Ioi y, f ((xâ‚ : â„‚) + (t : â„‚) * I)))) := by
    have hVâ‚' := hVâ‚.const_smul I
    have hVâ‚‚' := hVâ‚‚.const_smul I
    exact (tendsto_const_nhds.add hVâ‚‚').sub hVâ‚'
  have :
      (âˆ« x in xâ‚..xâ‚‚, f ((x : â„‚) + (y : â„‚) * I)) +
            (I â€¢ âˆ« t in Set.Ioi y, f ((xâ‚‚ : â„‚) + (t : â„‚) * I)) -
              (I â€¢ âˆ« t in Set.Ioi y, f ((xâ‚ : â„‚) + (t : â„‚) * I)) = (0 : E) :=
    tendsto_nhds_unique hFlim hF0
  simpa using this

end Contour_Deformation_Tendsto_Top

end Complex
